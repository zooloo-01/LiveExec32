#import "bridge.h"
#include "crash_exception.h"
#include "LC32ObjCBridgeABI.h"
#include "LC32DebugLog.h"

#import <dispatch/dispatch.h>
#import <mach/mach_init.h>
#import <mach/vm_map.h>
#import <objc/message.h>

#include <atomic>
#include <array>
#include <cerrno>
#include <cstddef>
#include <memory>
#include <mutex>
#include <new>
#include <pthread.h>
#include <stdio.h>
#include <stdarg.h>
#include <string>
#include <unordered_map>
#include <vector>

// These Objective-C runtime entry points let the host manipulate ownership
// explicitly. bridge.mm is normally built with manual reference counting,
// while keeping the weak-slot declarations valid if ARC is enabled later.
extern "C" id objc_autorelease(id object);
extern "C" void objc_release(id object);
extern "C" id objc_retain(id object);
extern "C" id _objc_rootRetain(id object);
extern "C" void _objc_rootRelease(id object);
extern "C" bool _objc_rootTryRetain(id object);
extern "C" bool _objc_rootIsDeallocating(id object);
extern "C" uintptr_t _objc_rootRetainCount(id object);
#if __has_feature(objc_arc)
typedef __weak id LC32NativeWeakSlot;
#else
typedef id LC32NativeWeakSlot;
#endif
extern "C" void objc_destroyWeak(LC32NativeWeakSlot *location);
extern "C" id objc_initWeakOrNil(LC32NativeWeakSlot *location, id object);
extern "C" id objc_loadWeakRetained(LC32NativeWeakSlot *location);
extern "C" id objc_storeWeakOrNil(LC32NativeWeakSlot *location, id object);

struct LC32HostMessageTwoDoubles {
    double d0, d1;
};

struct LC32HostMessageTwoU64 {
    u64 x0, x1;
};

struct LC32HostMessageFourDoubles {
    double d0, d1, d2, d3;
};

struct alignas(16) LC32HostMessageInvocation {
    u64 invokeSuper;
    u64 target;
    u64 selector;
    u64 integerArguments[9];
    u64 floatingArguments[8];
};

static_assert(offsetof(LC32HostMessageInvocation, invokeSuper) == 0);
static_assert(offsetof(LC32HostMessageInvocation, target) == 8);
static_assert(offsetof(LC32HostMessageInvocation, selector) == 16);
static_assert(offsetof(LC32HostMessageInvocation, integerArguments) == 24);
static_assert(offsetof(LC32HostMessageInvocation, floatingArguments) == 96);
static_assert(sizeof(LC32HostMessageInvocation) == 160);

extern "C" u64 LC32InvokeHostMessageInteger(
    const LC32HostMessageInvocation *invocation);
extern "C" float LC32InvokeHostMessageFloat(
    const LC32HostMessageInvocation *invocation);
extern "C" double LC32InvokeHostMessageDouble(
    const LC32HostMessageInvocation *invocation);
extern "C" LC32HostMessageTwoDoubles LC32InvokeHostMessageTwoDoubles(
    const LC32HostMessageInvocation *invocation);
extern "C" LC32HostMessageTwoU64 LC32InvokeHostMessageTwoU64(
    const LC32HostMessageInvocation *invocation);
extern "C" LC32HostMessageFourDoubles LC32InvokeHostMessageFourDoubles(
    const LC32HostMessageInvocation *invocation);
extern "C" LC32_SixDoubles LC32InvokeHostMessageSixDoubles(
    const LC32HostMessageInvocation *invocation);

static id LC32RetainOwnedHostObject(id object);

class LC32GuestHostCallQuiescence {
public:
    LC32GuestHostCallQuiescence()
        : active(Dynarmic_guest_host_call_quiescence_begin()) {}

    ~LC32GuestHostCallQuiescence() {
        finish();
    }

    void finish() {
        if (!active) {
            return;
        }
        active = false;
        Dynarmic_guest_host_call_quiescence_end();
    }

    LC32GuestHostCallQuiescence(
        const LC32GuestHostCallQuiescence &) = delete;
    LC32GuestHostCallQuiescence &operator=(
        const LC32GuestHostCallQuiescence &) = delete;

private:
    bool active;
};

#if __has_feature(objc_arc)
static void LC32ObjCAutoreleaseWithoutARC(id object) {
    (void)objc_autorelease(object);
}
#endif

static void LC32ObjCRetainWithoutARC(id object) {
    (void)LC32RetainOwnedHostObject(object);
}

@interface LC32ObjCMethodResolver : NSObject
+ (void)registerClass:(Class)cls;
@end

static void LC32PinGuestObjectToHost(id hostObject, u32 guestObject,
                                     bool retainGuestObject);
static void LC32DrainDeferredGuestPinReleases();
static u32 LC32GuestObjectForBorrowedHostResult(id hostObject);
static void LC32InstallGuestMirrorReferenceCounting(Class cls);
static id LC32GuestMirrorRetain(id self, SEL selector);
static void LC32GuestMirrorRelease(id self, SEL selector);
static void LC32GuestMirrorReleaseImplementation(
    id self, unsigned ownedReferenceCount = 1);
static std::mutex& LC32GuestMirrorReleaseMutex();
static void LC32RetireGuestMirrorWithTransferredReference(id hostObject);
static void LC32ClearGuestSelfIfEqualWhileSynchronized(
    id hostObject, u32 expectedGuestObject);
static void LC32ClearGuestSelfIfEqual(id hostObject,
                                      u32 expectedGuestObject);
static u32 LC32ReleaseNativeProxyOwnership(
    u32 guestObject, u64 hostAddress, bool releaseHostOwnership);

/*
 * Some native initializers consume the allocation's original +1 even when
 * they return the same object. A guest mirror needs an invocation-only +1 so
 * that such an initializer cannot destroy its receiver while it is running;
 * if the native call consumes the original ownership, that surviving guard
 * becomes the initializer result's +1 instead of being released afterwards.
 *
 * Keep this state thread-local and nestable. A native initializer can call
 * guest code, which can synchronously make another host call before the outer
 * invocation returns.
 */
class LC32HostInitializerInvocationScope {
public:
    explicit LC32HostInitializerInvocationScope(id candidate)
        : receiver(candidate), previous(activeScope) {
        /* Every native dispatch is a boundary, including non-initializers.
         * A synchronous nested bridge call must hide an outer initializer so
         * native releases performed by the nested call are not attributed to
         * the outer method's ownership contract. */
        activeScope = this;
    }

    ~LC32HostInitializerInvocationScope() {
        assert(activeScope == this);
        activeScope = previous;
    }

    int ownershipDelta() const {
        return nativeReceiverOwnershipDelta;
    }

    static void observeNativeRetain(id retainedObject) {
        if(!retainedObject) return;
        if(activeScope && activeScope->receiver == retainedObject) {
            activeScope->nativeReceiverOwnershipDelta++;
        }
    }

    static void observeNativeRelease(id releasedObject) {
        if(!releasedObject) return;
        if(activeScope && activeScope->receiver == releasedObject) {
            activeScope->nativeReceiverOwnershipDelta--;
        }
    }

    LC32HostInitializerInvocationScope(
        const LC32HostInitializerInvocationScope &) = delete;
    LC32HostInitializerInvocationScope &operator=(
        const LC32HostInitializerInvocationScope &) = delete;

private:
    id __unsafe_unretained receiver = nil;
    LC32HostInitializerInvocationScope *previous = nullptr;
    int nativeReceiverOwnershipDelta = 0;
    static thread_local LC32HostInitializerInvocationScope *activeScope;
};

thread_local LC32HostInitializerInvocationScope *
    LC32HostInitializerInvocationScope::activeScope = nullptr;

enum class LC32HostOwnershipOperation {
    Retain,
    WeakRetain,
};

class LC32HostOwnershipObservationSuppression {
public:
    LC32HostOwnershipObservationSuppression(
            id target, LC32HostOwnershipOperation operation)
        : target(target), operation(operation), previous(activeScope) {
        activeScope = this;
    }

    ~LC32HostOwnershipObservationSuppression() {
        assert(activeScope == this);
        activeScope = previous;
    }

    static bool consumeIfMatching(
            id object, LC32HostOwnershipOperation operation) {
        if(!activeScope || activeScope->consumed ||
           activeScope->target != object ||
           activeScope->operation != operation) {
            return false;
        }
        activeScope->consumed = true;
        return true;
    }

    LC32HostOwnershipObservationSuppression(
        const LC32HostOwnershipObservationSuppression &) = delete;
    LC32HostOwnershipObservationSuppression &operator=(
        const LC32HostOwnershipObservationSuppression &) = delete;

private:
    id __unsafe_unretained target;
    LC32HostOwnershipOperation operation;
    LC32HostOwnershipObservationSuppression *previous = nullptr;
    bool consumed = false;
    static thread_local LC32HostOwnershipObservationSuppression *activeScope;
};

thread_local LC32HostOwnershipObservationSuppression *
    LC32HostOwnershipObservationSuppression::activeScope = nullptr;

static id LC32RetainOwnedHostObject(id object) {
    if(!object) return nil;
    /* Do not inspect an explicitly-owned raw result before retaining it: it
     * may not have a registry entry yet. The custom retain IMP consults this
     * suppression state if the object turns out to be a guest mirror. */
    LC32HostOwnershipObservationSuppression suppression(
        object, LC32HostOwnershipOperation::Retain);
    return objc_retain(object);
}

static void LC32ReleaseOwnedHostObject(id object) {
    if(!object) return;
    Method releaseMethod = class_getInstanceMethod(
        object_getClass(object), @selector(release));
    if(releaseMethod && method_getImplementation(releaseMethod) ==
            (IMP)&LC32GuestMirrorRelease) {
        LC32GuestMirrorReleaseImplementation(object);
    } else {
        objc_release(object);
    }
}

static id LC32LoadWeakRetainedHostObject(
        LC32NativeWeakSlot *location, id expectedObject) {
    LC32HostOwnershipObservationSuppression suppression(
        expectedObject, LC32HostOwnershipOperation::WeakRetain);
    return objc_loadWeakRetained(location);
}

class LC32HostInvocationReceiverGuard {
public:
    LC32HostInvocationReceiverGuard() = default;

    bool adoptRetained(id candidate) {
        if(!candidate || object) return false;
        object = candidate;
        return true;
    }

    bool acquireUnmapped(id candidate) {
        if(!candidate) return true;
        return adoptRetained(LC32RetainOwnedHostObject(candidate));
    }

    bool ownsReference() const {
        return object != nil;
    }

    bool relinquishIfEqual(id candidate) {
        if(!object || object != candidate) return false;
        object = nil;
        return true;
    }

    void releaseNow() {
        if(!object) return;

        id __unsafe_unretained ownedObject = object;
        object = nil;

        /* Releasing the invocation-only +1 may be the operation which begins
         * native teardown and calls back into guest code. Keep the emulated
         * register state quiescent for that entire transition too, not only
         * for the objc_msgSend which preceded it. */
        LC32GuestHostCallQuiescence quiescence;

        /*
         * A synthesized mirror coordinates its final native release with the
         * guest lifetime pin. Call that implementation directly when the
         * object kept its guest class, instead of relying on objc_release's
         * cached custom-RR decision. An initializer is allowed to replace or
         * change the allocated object, so fall back to normal dynamic release
         * when the current class no longer carries our override.
         */
        LC32ReleaseOwnedHostObject(ownedObject);
        quiescence.finish();
    }

    ~LC32HostInvocationReceiverGuard() {
        releaseNow();
    }

    LC32HostInvocationReceiverGuard(
        const LC32HostInvocationReceiverGuard &) = delete;
    LC32HostInvocationReceiverGuard &operator=(
        const LC32HostInvocationReceiverGuard &) = delete;

private:
    id __unsafe_unretained object = nil;
};

static const void *LC32GuestLifetimePinKey =
    &LC32GuestLifetimePinKey;
static const void *kGuestClass = &kGuestClass;
static const void *kGuestSelf = &kGuestSelf;

/*
 * NSOperation ownership diagnostics are intentionally runtime-gated because
 * retain/release traffic is both frequent and timing-sensitive.  Keep a raw
 * address registry so a final setCompletionBlock: sent through a stale guest
 * proxy can be diagnosed without messaging (and crashing on) the freed native
 * receiver.  Enable with LC32_OPERATION_TRACE=1.
 */
struct LC32OperationTraceRecord {
    u32 guestObject;
    bool alive;
    std::array<char, 96> className;
};

static pthread_once_t LC32OperationTraceOnce = PTHREAD_ONCE_INIT;
static bool LC32OperationTraceIsEnabled;
static std::atomic<u64> LC32OperationTraceSequence{0};

static pthread_once_t LC32NetworkTraceOnce = PTHREAD_ONCE_INIT;
static bool LC32NetworkTraceIsEnabled;
static pthread_once_t LC32GuestCallbackTraceOnce = PTHREAD_ONCE_INIT;
static bool LC32GuestCallbackTraceIsEnabled;
static pthread_once_t LC32BlockArgumentTraceOnce = PTHREAD_ONCE_INIT;
static bool LC32BlockArgumentTraceIsEnabled;

static void LC32InitializeNetworkTrace() {
    const char *value = getenv("LC32_NETWORK_TRACE");
    LC32NetworkTraceIsEnabled =
        value && value[0] && strcmp(value, "0") != 0;
}

static bool LC32NetworkTraceEnabled() {
    pthread_once(&LC32NetworkTraceOnce, LC32InitializeNetworkTrace);
    return LC32NetworkTraceIsEnabled;
}

static void LC32InitializeGuestCallbackTrace() {
    const char *value = getenv("LC32_CALLBACK_TRACE");
    LC32GuestCallbackTraceIsEnabled =
        value && value[0] && strcmp(value, "0") != 0;
}

static bool LC32GuestCallbackTraceEnabled() {
    pthread_once(&LC32GuestCallbackTraceOnce,
                 LC32InitializeGuestCallbackTrace);
    return LC32GuestCallbackTraceIsEnabled;
}

static void LC32InitializeBlockArgumentTrace() {
    const char *value = getenv("LC32_BLOCK_TRACE");
    LC32BlockArgumentTraceIsEnabled =
        value && value[0] && strcmp(value, "0") != 0;
}

static bool LC32BlockArgumentTraceEnabled() {
    pthread_once(&LC32BlockArgumentTraceOnce,
                 LC32InitializeBlockArgumentTrace);
    return LC32BlockArgumentTraceIsEnabled;
}

static void LC32TraceGuestMethodCallback(id receiver, SEL selector) {
    if(!LC32GuestCallbackTraceEnabled()) return;
    NSOperationQueue *operationQueue = NSOperationQueue.currentQueue;
    const char *queueLabel = dispatch_queue_get_label(
        DISPATCH_CURRENT_QUEUE_LABEL);
    fprintf(stderr,
        "LC32 callback: %c[%s %s] registered=%d "
        "hostThread=%p main=%d dispatch=%s operationQueue=%p "
        "operationMain=%d\n",
        object_isClass(receiver) ? '+' : '-',
        receiver ? class_getName(object_getClass(receiver)) : "(null)",
        selector ? sel_getName(selector) : "(null)",
        Dynarmic_guest_thread_is_registered(),
        (void *)pthread_self(), pthread_main_np(), queueLabel ?: "",
        operationQueue, operationQueue != nil &&
            operationQueue == NSOperationQueue.mainQueue);
    fflush(stderr);
}

static void LC32TraceNativeNetworkObject(const char *direction,
                                          SEL selector,
                                          unsigned int argumentIndex,
                                          id object) {
    if(!LC32NetworkTraceEnabled() || !object) return;

    @autoreleasepool {
        @try {
            if([object isKindOfClass:NSURLRequest.class]) {
                NSURLRequest *request = (NSURLRequest *)object;
                fprintf(stderr,
                    "LC32 network %s %s arg=%u request=%s %s headers=%s\n",
                    direction, sel_getName(selector), argumentIndex,
                    request.HTTPMethod.UTF8String ?: "?",
                    request.URL.absoluteString.UTF8String ?: "?",
                    request.allHTTPHeaderFields.description.UTF8String ?: "{}");
            } else if([object isKindOfClass:NSHTTPURLResponse.class]) {
                NSHTTPURLResponse *response = (NSHTTPURLResponse *)object;
                fprintf(stderr,
                    "LC32 network %s %s arg=%u response=%ld %s headers=%s\n",
                    direction, sel_getName(selector), argumentIndex,
                    (long)response.statusCode,
                    response.URL.absoluteString.UTF8String ?: "?",
                    response.allHeaderFields.description.UTF8String ?: "{}");
            } else if([object isKindOfClass:NSError.class]) {
                NSError *error = (NSError *)object;
                fprintf(stderr,
                    "LC32 network %s %s arg=%u error=%s\n",
                    direction, sel_getName(selector), argumentIndex,
                    error.description.UTF8String ?: "?");
            }
        } @catch(NSException *exception) {
            if (LC32IsGuestCrashException(exception)) {
                @throw;
            }
            fprintf(stderr,
                "LC32 network trace failed for %s arg=%u: %s\n",
                sel_getName(selector), argumentIndex,
                exception.description.UTF8String ?: "?");
        }
    }
}

static void LC32InitializeOperationTrace() {
    const char *value = getenv("LC32_OPERATION_TRACE");
    LC32OperationTraceIsEnabled =
        value && value[0] && strcmp(value, "0") != 0;
}

static bool LC32OperationTraceEnabled() {
    pthread_once(&LC32OperationTraceOnce, LC32InitializeOperationTrace);
    return LC32OperationTraceIsEnabled;
}

static std::mutex& LC32OperationTraceMutex() {
    static std::mutex *mutex = new std::mutex;
    return *mutex;
}

static std::unordered_map<u64, LC32OperationTraceRecord>&
LC32OperationTraceRecords() {
    static auto *records =
        new std::unordered_map<u64, LC32OperationTraceRecord>;
    return *records;
}

static void LC32OperationTraceGuestContext(u32 *pc, u32 *lr) {
    *pc = 0;
    *lr = 0;
    if(!threadHandle.jit) return;
    const auto &registers = threadHandle.jit->Regs();
    *pc = registers[Reg::PC];
    *lr = registers[Reg::LR];
}

static bool LC32HostObjectIsOperation(id object) {
    if(!object) return false;
    Class operationClass = objc_getClass("NSOperation");
    return operationClass && [object isKindOfClass:operationClass];
}

static void LC32OperationTraceRemember(id object, u32 guestObject,
                                       bool alive) {
    if(!LC32OperationTraceEnabled() || !object) return;

    const u64 address = (u64)object;
    LC32OperationTraceRecord record = {};
    record.guestObject = guestObject;
    record.alive = alive;
    const char *name = class_getName(object_getClass(object));
    if(name) {
        snprintf(record.className.data(), record.className.size(), "%s", name);
    }
    std::lock_guard<std::mutex> lock(LC32OperationTraceMutex());
    LC32OperationTraceRecords()[address] = record;
}

static bool LC32OperationTraceLookup(
        u64 address, LC32OperationTraceRecord *record) {
    if(!LC32OperationTraceEnabled()) return false;
    std::lock_guard<std::mutex> lock(LC32OperationTraceMutex());
    auto iterator = LC32OperationTraceRecords().find(address);
    if(iterator == LC32OperationTraceRecords().end()) return false;
    *record = iterator->second;
    return true;
}

static void LC32OperationTracePrint(const char *event, u64 hostObject,
                                    const LC32OperationTraceRecord &record,
                                    long nativeRetainCount,
                                    u64 detail = 0) {
    u32 pc, lr;
    LC32OperationTraceGuestContext(&pc, &lr);
    const u64 sequence = LC32OperationTraceSequence.fetch_add(
        1, std::memory_order_relaxed) + 1;
    fprintf(stderr,
        "LC32 operation trace #%llu %s host=0x%llx guest=0x%x "
        "class=%s alive=%d nativeRC=%ld detail=0x%llx pc=0x%x "
        "lr=0x%x thread=%p\n",
        sequence, event, hostObject, record.guestObject,
        record.className[0] ? record.className.data() : "?",
        record.alive, nativeRetainCount, detail, pc, lr,
        (void *)pthread_self());
}

static void LC32OperationTraceLiveObject(const char *event, id object,
                                         u32 guestObject, u64 detail = 0) {
    if(!LC32OperationTraceEnabled() ||
       !LC32HostObjectIsOperation(object)) return;

    LC32OperationTraceRemember(object, guestObject, true);
    LC32OperationTraceRecord record = {};
    if(!LC32OperationTraceLookup((u64)object, &record)) return;
    LC32OperationTracePrint(event, (u64)object, record,
        (long)CFGetRetainCount((CFTypeRef)object), detail);
}

static void LC32OperationTraceRawSelector(id receiver, SEL selector,
                                          u64 firstArgument) {
    if(!LC32OperationTraceEnabled()) return;
    const char *selectorName = sel_getName(selector);
    const bool completion = selectorName &&
        strcmp(selectorName, "setCompletionBlock:") == 0;
    const bool ownership = selectorName &&
        (!strcmp(selectorName, "retain") ||
         !strcmp(selectorName, "release") ||
         !strcmp(selectorName, "retainCount"));
    if(!completion && !ownership) return;

    LC32OperationTraceRecord record = {};
    if(!LC32OperationTraceLookup((u64)receiver, &record)) return;
    const long nativeRetainCount = record.alive
        ? (long)CFGetRetainCount((CFTypeRef)receiver)
        : -1;
    LC32OperationTracePrint(selectorName, (u64)receiver, record,
                            nativeRetainCount,
                            completion ? firstArgument : 0);
}

static void LC32OperationTraceDeallocated(u64 hostObject, u32 guestObject,
                                          const char *className) {
    if(!LC32OperationTraceEnabled()) return;

    LC32OperationTraceRecord record = {};
    record.guestObject = guestObject;
    record.alive = false;
    if(className) {
        snprintf(record.className.data(), record.className.size(), "%s",
                 className);
    }
    {
        std::lock_guard<std::mutex> lock(LC32OperationTraceMutex());
        LC32OperationTraceRecords()[hostObject] = record;
    }
    LC32OperationTracePrint("native-dealloc", hostObject, record, 0);
}

/*
 * Guest objc_loadWeakRetained holds the ARM32 runtime's weak side-table lock
 * while it asks an object with custom RR to retain itself.  Looking up the
 * native peer through a guest associated object from that callback can retain
 * the associated value and recursively acquire the same striped lock.
 *
 * Keep the authoritative host identity in native weak storage instead.  A
 * generation makes retirement conditional so a delayed pin belonging to an
 * old object can never erase a new mapping which reuses the same ARM address.
 */
enum class LC32HostWeakMappingState : uint8_t {
    Live,
    Retiring,
    Superseded,
};

enum class LC32HostMappingLifetime : uint8_t {
    Provisional,
    Pinned,
    Permanent,
};

enum class LC32HostMappingRetirementProvenance : uint8_t {
    None,
    /* Guest -dealloc is running while its paired native +1 is still owned. */
    GuestTeardownWithNativeOwnership,
    /* Guest -dealloc is running after the native peer already disappeared. */
    GuestTeardownWithoutNativePeer,
    /* A synthesized mirror's final native +1 was transferred to retirement. */
    GuestMirrorWithTransferredReference,
    /* The lifetime pin is being destroyed by native object deallocation. */
    NativePeerDeallocating,
    /* The paired native +1 is being consumed by the finish-and-release path. */
    FinalHostRelease,
};

static bool LC32RetirementAllowsCurrentThreadInvocation(
        LC32HostMappingRetirementProvenance provenance) {
    return provenance ==
               LC32HostMappingRetirementProvenance::
                   GuestTeardownWithNativeOwnership ||
           provenance ==
               LC32HostMappingRetirementProvenance::
                   GuestMirrorWithTransferredReference;
}

static bool LC32HostObjectIsAutoreleasePool(id hostObject) {
    if(!hostObject) return false;
    Class autoreleasePoolClass = objc_getClass("NSAutoreleasePool");
    for(Class cls = object_getClass(hostObject); cls;
            cls = class_getSuperclass(cls)) {
        if(cls == autoreleasePoolClass) return true;
    }
    return false;
}

static bool LC32HostObjectIsSharedURLPlaceholder(id hostObject) {
    Class urlClass = objc_getClass("NSURL");
    if(!urlClass || object_getClass(hostObject) != urlClass) return false;

    /* NSURL's native allocation placeholder rejects weak references, even
     * though +alloc returns a shared process-lifetime object. Verify that
     * identity instead of exempting arbitrary weak-incompatible NSURLs.
     * Keep one allocation's ownership for the lifetime of the process. A
     * platform with ordinary per-allocation NSURL objects gets no exemption.
     * This runs while publishing a known-live object, outside registry locks.
     */
    static id placeholder = nil;
    static dispatch_once_t once;
    dispatch_once(&once, ^{
        id first = [urlClass alloc];
        id second = [urlClass alloc];
        if(first && first == second) {
            placeholder = first;
        } else {
            objc_release(first);
        }
        objc_release(second);
    });
    return hostObject == placeholder;
}

struct LC32HostWeakMappingEntry {
    u32 guestObject;
    u64 generation;
    u64 expectedHostAddress;
    LC32HostWeakMappingState state;
    LC32HostMappingLifetime lifetime;
    u64 retiringOwnerThreadToken;
    LC32HostMappingRetirementProvenance retirementProvenance;
    LC32NativeWeakSlot weakHostObject;
    bool weakCompatible;
    bool invocationRetainCompatible;
    /* Only ordinary/logical guest decrements acquire this private gate.
     * Native weak RR and guest weak-retain callbacks never take it. */
    std::mutex nativeProxyReleaseMutex;

    LC32HostWeakMappingEntry(id hostObject, u32 guest, u64 serial,
                             LC32HostMappingLifetime mappingLifetime)
        : guestObject(guest), generation(serial),
          expectedHostAddress((u64)hostObject),
          state(LC32HostWeakMappingState::Live),
          lifetime(mappingLifetime), retiringOwnerThreadToken(0),
          retirementProvenance(
              LC32HostMappingRetirementProvenance::None),
          weakHostObject(nil), weakCompatible(false),
          invocationRetainCompatible(true) {
        /* NSAutoreleasePool uses its object as a one-shot pool token. A
         * balanced retain/release around -init or -drain can pop that token
         * early on modern Foundation, so its guest owner remains the call
         * lifetime instead of adding an invocation-only native reference.
         * The verified shared URL placeholder likewise needs no invocation
         * lease: our cached allocation keeps its exact address alive. */
        invocationRetainCompatible =
            !LC32HostObjectIsAutoreleasePool(hostObject) &&
            !LC32HostObjectIsSharedURLPlaceholder(hostObject);
        /* Weak-host-incompatible objects become a live entry containing nil;
         * a guest weak load then fails safely instead of raising here. */
#if __has_feature(objc_arc)
        weakCompatible =
            objc_storeWeakOrNil(&weakHostObject, hostObject) == hostObject;
#else
        weakCompatible =
            objc_initWeakOrNil(&weakHostObject, hostObject) == hostObject;
#endif
    }

    ~LC32HostWeakMappingEntry() {
#if !__has_feature(objc_arc)
        objc_destroyWeak(&weakHostObject);
#endif
    }
};

struct LC32HostWeakRegistry {
    std::mutex mutex;
    std::unordered_map<u32,
        std::shared_ptr<LC32HostWeakMappingEntry>> entries;
    /* A native class-cluster allocation placeholder can be shared by more
     * than one guest allocation, so the reverse index is intentionally not a
     * one-to-one map. The guest-keyed entries above remain authoritative. */
    std::unordered_multimap<u64, u32> guestObjectsByHostAddress;
    /* Guest root -dealloc detaches a dead peer before freeing its allocation.
     * Keep native addresses noncallable until lifetime-pin cleanup, without
     * reserving ARM32 addresses which can already be reused. */
    std::unordered_map<u64,
        std::shared_ptr<LC32HostWeakMappingEntry>> retiringEntries;
    bool hostAddressIndexUsable = true;
    std::atomic<u64> nextGeneration{1};
    dispatch_queue_t deferredReleaseQueue;

    LC32HostWeakRegistry()
        : deferredReleaseQueue(dispatch_queue_create(
              "org.liveexec32.host-weak-release", DISPATCH_QUEUE_SERIAL)) {}
};

static LC32HostWeakRegistry& LC32HostWeakMappings() {
    /* Host weak slots and their synchronization intentionally survive process
     * teardown; Objective-C framework destruction order is not deterministic. */
    static LC32HostWeakRegistry *registry = new LC32HostWeakRegistry;
    return *registry;
}

static void LC32DisableHostAddressIndexLocked(
        LC32HostWeakRegistry &registry) {
    registry.hostAddressIndexUsable = false;
    registry.guestObjectsByHostAddress.clear();
}

static void LC32IndexHostMappingLocked(
        LC32HostWeakRegistry &registry,
        const std::shared_ptr<LC32HostWeakMappingEntry>& entry) {
    if(!registry.hostAddressIndexUsable || !entry) return;
    try {
        registry.guestObjectsByHostAddress.emplace(
            entry->expectedHostAddress, entry->guestObject);
    } catch(const std::bad_alloc &) {
        /* Correctness does not depend on this acceleration structure. A
         * failed insertion permanently selects the authoritative linear scan
         * rather than leaving a partially indexed registry. */
        LC32DisableHostAddressIndexLocked(registry);
    }
}

static void LC32UnindexHostMappingLocked(
        LC32HostWeakRegistry &registry,
        const std::shared_ptr<LC32HostWeakMappingEntry>& entry) {
    if(!registry.hostAddressIndexUsable || !entry) return;
    auto range = registry.guestObjectsByHostAddress.equal_range(
        entry->expectedHostAddress);
    for(auto iterator = range.first; iterator != range.second;) {
        if(iterator->second == entry->guestObject) {
            iterator = registry.guestObjectsByHostAddress.erase(iterator);
        } else {
            ++iterator;
        }
    }
}

static void LC32DeferHostWeakEntryRelease(
        std::shared_ptr<LC32HostWeakMappingEntry> entry) {
    if(!entry) return;
    /* The SVC path runs inside guest objc_loadWeakRetained while its weak
     * SideTable stripe is locked.  Keep the final native weak-slot teardown
     * off that thread even if registry retirement races this lookup. */
    dispatch_async(LC32HostWeakMappings().deferredReleaseQueue, ^{
        (void)entry->generation;
    });
}

static u64 LC32NextHostWeakGeneration() {
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    u64 generation = registry.nextGeneration.fetch_add(
        1, std::memory_order_relaxed);
    if(generation) return generation;
    /* A wrap is practically unreachable, but zero is reserved for "none". */
    do {
        generation = registry.nextGeneration.fetch_add(
            1, std::memory_order_relaxed);
    } while(!generation);
    return generation;
}

static u64 LC32CurrentHostMappingThreadToken() {
    static std::atomic<u64> nextToken{1};
    static thread_local const u64 token = [] {
        u64 value = nextToken.fetch_add(1, std::memory_order_relaxed);
        if(value) return value;
        do {
            value = nextToken.fetch_add(1, std::memory_order_relaxed);
        } while(!value);
        return value;
    }();
    return token;
}

static u64 LC32PublishHostMapping(
        id hostObject, u32 guestObject,
        LC32HostMappingLifetime requestedLifetime) {
    if(!hostObject || !guestObject) return 0;

    const u64 generation = LC32NextHostWeakGeneration();
    std::shared_ptr<LC32HostWeakMappingEntry> entry;
    try {
        /* Initializing native weak storage may acquire libobjc's SideTable.
         * It must remain outside the registry mutex. */
        entry = std::make_shared<LC32HostWeakMappingEntry>(
            hostObject, guestObject, generation, requestedLifetime);
    } catch(const std::bad_alloc &) {
        return 0;
    }

    std::shared_ptr<LC32HostWeakMappingEntry> replaced;
    std::shared_ptr<LC32HostWeakMappingEntry> unused;
    u64 publishedGeneration = generation;
    bool rejectedStableReplacement = false;
    u64 rejectedHostAddress = 0;
    LC32HostMappingLifetime rejectedLifetime =
        LC32HostMappingLifetime::Provisional;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end()) {
            try {
                registry.entries.emplace(guestObject, entry);
                LC32IndexHostMappingLocked(registry, entry);
            } catch(const std::bad_alloc &) {
                publishedGeneration = 0;
                unused = std::move(entry);
            }
        } else if(iterator->second->state ==
                      LC32HostWeakMappingState::Live &&
                  iterator->second->expectedHostAddress ==
                      (u64)hostObject) {
            /* A host lifetime pin promotes the provisional publication which
             * preceded it.  Reuse the exact generation stored by that pin so
             * its delayed retirement remains conditional.  Later guest-side
             * publications of the same pair must never demote it. */
            if(requestedLifetime == LC32HostMappingLifetime::Pinned &&
                    iterator->second->lifetime ==
                        LC32HostMappingLifetime::Provisional) {
                iterator->second->lifetime =
                    LC32HostMappingLifetime::Pinned;
            } else if(requestedLifetime ==
                          LC32HostMappingLifetime::Permanent &&
                      iterator->second->lifetime ==
                          LC32HostMappingLifetime::Provisional) {
                iterator->second->lifetime =
                    LC32HostMappingLifetime::Permanent;
            }
            publishedGeneration = iterator->second->generation;
            unused = std::move(entry);
        } else if(iterator->second->state ==
                      LC32HostWeakMappingState::Live &&
                  iterator->second->lifetime !=
                      LC32HostMappingLifetime::Provisional) {
            /* Pinned and permanent pairs are immutable.  A Retiring entry is
             * deliberately excluded: a newly allocated pair may reuse both
             * raw addresses and still requires a fresh generation. */
            rejectedStableReplacement = true;
            rejectedHostAddress =
                iterator->second->expectedHostAddress;
            rejectedLifetime = iterator->second->lifetime;
            publishedGeneration = 0;
            unused = std::move(entry);
        } else {
            iterator->second->state =
                LC32HostWeakMappingState::Superseded;
            LC32UnindexHostMappingLocked(registry, iterator->second);
            replaced = std::move(iterator->second);
            iterator->second = entry;
            LC32IndexHostMappingLocked(registry, entry);
        }
    }

    if(rejectedStableReplacement) {
        fprintf(stderr,
            "LC32: refusing to replace %s host mapping for guest 0x%x "
            "(old host 0x%llx, new host 0x%llx)\n",
            rejectedLifetime == LC32HostMappingLifetime::Pinned
                ? "pinned" : "permanent",
            guestObject,
            (unsigned long long)rejectedHostAddress,
            (unsigned long long)(u64)hostObject);
    }
    LC32DeferHostWeakEntryRelease(std::move(unused));
    LC32DeferHostWeakEntryRelease(std::move(replaced));
    return publishedGeneration;
}

static u64 LC32RegisterHostWeakMapping(id hostObject, u32 guestObject) {
    return LC32PublishHostMapping(
        hostObject, guestObject, LC32HostMappingLifetime::Pinned);
}

extern "C" u64 LC32LookupHostMapping(u32 guestObject) {
    if(!guestObject) return 0;

    const u64 currentThreadToken = LC32CurrentHostMappingThreadToken();
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    auto iterator = registry.entries.find(guestObject);
    if(iterator == registry.entries.end()) {
        return 0;
    }
    if(iterator->second->state == LC32HostWeakMappingState::Live) {
        return iterator->second->expectedHostAddress;
    }
    /* Only retirement paths which explicitly own a native lifetime lease may
     * keep resolving the raw address. LC32GuestLifetimePin can also publish a
     * Retiring entry while its native peer is already deallocating; thread
     * identity alone must never make that dead address callable. */
    if(iterator->second->state != LC32HostWeakMappingState::Retiring ||
            !LC32RetirementAllowsCurrentThreadInvocation(
                iterator->second->retirementProvenance) ||
            iterator->second->retiringOwnerThreadToken !=
                currentThreadToken) {
        return LC32_HOST_MAPPING_DEAD;
    }
    return iterator->second->expectedHostAddress;
}

enum class LC32HostInvocationMappingKind : uint8_t {
    Unmapped,
    Live,
    Permanent,
    RetiringOnCurrentThread,
    MappedDead,
};

struct LC32HostInvocationMappingSnapshot {
    LC32HostInvocationMappingKind kind;
    std::shared_ptr<LC32HostWeakMappingEntry> entry;
};

static LC32HostInvocationMappingSnapshot
LC32SnapshotHostInvocationMapping(u64 hostAddress) {
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    const u64 currentThreadToken = LC32CurrentHostMappingThreadToken();
    std::shared_ptr<LC32HostWeakMappingEntry> liveEntry;
    std::shared_ptr<LC32HostWeakMappingEntry> retiringEntry;
    bool foundAddress = false;

    std::lock_guard<std::mutex> lock(registry.mutex);
    const auto consider = [&](u32 guestObject) {
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end()) return;
        const std::shared_ptr<LC32HostWeakMappingEntry>& entry =
            iterator->second;
        if(entry->expectedHostAddress != hostAddress) return;
        foundAddress = true;
        if(entry->state == LC32HostWeakMappingState::Live) {
            const bool entryIsPermanent = entry->lifetime ==
                LC32HostMappingLifetime::Permanent;
            const bool selectedIsPermanent = liveEntry &&
                liveEntry->lifetime == LC32HostMappingLifetime::Permanent;
            if(!liveEntry || (entryIsPermanent && !selectedIsPermanent) ||
               (entryIsPermanent == selectedIsPermanent &&
                entry->generation > liveEntry->generation)) {
                liveEntry = entry;
            }
        } else if(!retiringEntry &&
                  entry->state == LC32HostWeakMappingState::Retiring &&
                  LC32RetirementAllowsCurrentThreadInvocation(
                      entry->retirementProvenance) &&
                  entry->retiringOwnerThreadToken == currentThreadToken) {
            retiringEntry = entry;
        }
    };

    if(registry.hostAddressIndexUsable) {
        auto range = registry.guestObjectsByHostAddress.equal_range(
            hostAddress);
        for(auto iterator = range.first; iterator != range.second;
                ++iterator) {
            consider(iterator->second);
        }
    } else {
        for(const auto &mapping : registry.entries) {
            consider(mapping.first);
        }
    }

    if(liveEntry) {
        return {
            liveEntry->lifetime == LC32HostMappingLifetime::Permanent
                ? LC32HostInvocationMappingKind::Permanent
                : LC32HostInvocationMappingKind::Live,
            std::move(liveEntry),
        };
    }
    if(retiringEntry) {
        return {
            LC32HostInvocationMappingKind::RetiringOnCurrentThread,
            std::move(retiringEntry),
        };
    }
    if(!foundAddress) {
        for(const auto &retiring : registry.retiringEntries) {
            if(retiring.second->expectedHostAddress == hostAddress) {
                foundAddress = true;
                break;
            }
        }
    }
    return {
        foundAddress ? LC32HostInvocationMappingKind::MappedDead
                     : LC32HostInvocationMappingKind::Unmapped,
        nullptr,
    };
}

static bool LC32HostInvocationMappingIsCurrent(
        const std::shared_ptr<LC32HostWeakMappingEntry>& entry) {
    if(!entry) return false;
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    const auto isCurrentLiveMapping = [&](u32 guestObject) {
        auto iterator = registry.entries.find(guestObject);
        return iterator != registry.entries.end() &&
            iterator->second->expectedHostAddress ==
                entry->expectedHostAddress &&
            iterator->second->state == LC32HostWeakMappingState::Live;
    };

    /* Prefer the exact generation selected before the weak load. A shared
     * class-cluster placeholder may legitimately lose that guest mapping while
     * another Live mapping to the same retained native object remains. */
    auto exact = registry.entries.find(entry->guestObject);
    if(exact != registry.entries.end() &&
       exact->second.get() == entry.get() &&
       exact->second->generation == entry->generation &&
       exact->second->state == LC32HostWeakMappingState::Live) {
        return true;
    }

    if(registry.hostAddressIndexUsable) {
        auto range = registry.guestObjectsByHostAddress.equal_range(
            entry->expectedHostAddress);
        for(auto iterator = range.first; iterator != range.second;
                ++iterator) {
            if(isCurrentLiveMapping(iterator->second)) return true;
        }
    } else {
        for(const auto &mapping : registry.entries) {
            if(isCurrentLiveMapping(mapping.first)) return true;
        }
    }
    return false;
}

static bool LC32AcquireHostInvocationReceiver(
        u64 hostAddress, LC32HostInvocationReceiverGuard &guard,
        id *receiver, bool allowUnmappedReceiver) {
    if(!receiver) return false;
    *receiver = (id)(uintptr_t)hostAddress;
    if(!hostAddress) return true;

    const auto acquireExplicitlyOwnedRawReceiver = [&]() {
        if(!allowUnmappedReceiver ||
                !guard.acquireUnmapped(*receiver)) {
            return false;
        }
        return true;
    };

    LC32HostInvocationMappingSnapshot snapshot =
        LC32SnapshotHostInvocationMapping(hostAddress);
    switch(snapshot.kind) {
        case LC32HostInvocationMappingKind::Permanent:
        case LC32HostInvocationMappingKind::RetiringOnCurrentThread:
            /* Classes/process-lifetime constants cannot disappear. During
             * same-thread retirement, the teardown path already owns the
             * transferred root guard which keeps a guest mirror valid. */
            return true;
        case LC32HostInvocationMappingKind::MappedDead:
            /* A raw-result caller may independently own a newly allocated
             * object which reused an address still quarantined by an older
             * mapping generation. Its explicit lifetime guarantee is the only
             * case where registry evidence may be bypassed. */
            return acquireExplicitlyOwnedRawReceiver();
        case LC32HostInvocationMappingKind::Unmapped:
            /* Some valid raw results are intentionally messaged before they
             * acquire a guest mapping (notably class-cluster init results).
             * There is no weak slot with which to promote those atomically;
             * use normal Objective-C ownership for that narrow fallback.
             * Never inspect the object's class first: an erased stale mapping
             * is indistinguishable from this case until it is retained. */
            return acquireExplicitlyOwnedRawReceiver();
        case LC32HostInvocationMappingKind::Live:
            break;
    }

    if(!snapshot.entry->invocationRetainCompatible) {
        return LC32HostInvocationMappingIsCurrent(snapshot.entry);
    }

    id retainedReceiver = nil;
    if(snapshot.entry->weakCompatible) {
        /* The captured shared_ptr keeps the weak slot allocated. Never perform
         * this load under the registry mutex: custom weak RR runs while
         * libobjc holds its SideTable lock and may reenter the bridge. */
        retainedReceiver = LC32LoadWeakRetainedHostObject(
            &snapshot.entry->weakHostObject,
            (id)(uintptr_t)snapshot.entry->expectedHostAddress);
        if(!retainedReceiver) {
            return acquireExplicitlyOwnedRawReceiver();
        }
        if(!guard.adoptRetained(retainedReceiver)) {
            LC32GuestHostCallQuiescence quiescence;
            LC32ReleaseOwnedHostObject(retainedReceiver);
            quiescence.finish();
            return false;
        }
        if((u64)retainedReceiver != hostAddress) return false;
    } else {
        /* There is no atomic way to retain a weak-incompatible mapped object
         * from its raw address. Permanent objects and NSAutoreleasePool were
         * handled above; reject every other case instead of racing dealloc in
         * objc_retain. */
        return acquireExplicitlyOwnedRawReceiver();
    }

    return LC32HostInvocationMappingIsCurrent(snapshot.entry);
}

static u32 LC32ClearHostMappingIfEqual(
        u32 guestObject, u64 expectedHostAddress) {
    if(!guestObject || !expectedHostAddress) return 0;

    std::shared_ptr<LC32HostWeakMappingEntry> removed;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end()) {
            return 1;
        }
        if(iterator->second->expectedHostAddress != expectedHostAddress) {
            /* The old guest object may already have completed -dealloc and
             * its ARM address may now name a new pair.  A conditional clear
             * which loses that ABA race has still succeeded: preserving the
             * newer entry is exactly its contract. */
            return 1;
        }
        iterator->second->state = LC32HostWeakMappingState::Superseded;
        LC32UnindexHostMappingLocked(registry, iterator->second);
        removed = std::move(iterator->second);
        registry.entries.erase(iterator);
    }
    LC32DeferHostWeakEntryRelease(std::move(removed));
    return 1;
}

struct LC32PendingGuestMappingTeardown {
    u32 guestObject;
    u64 generation;
    u64 expectedHostAddress;
    u64 retainedHostObject;
    LC32HostMappingRetirementProvenance provenance;
    std::shared_ptr<LC32HostWeakMappingEntry> entry;
};

static std::mutex& LC32PendingGuestMappingTeardownMutex() {
    static std::mutex *mutex = new std::mutex;
    return *mutex;
}

static std::unordered_map<u32, LC32PendingGuestMappingTeardown>&
LC32PendingGuestMappingTeardowns() {
    static auto *teardowns =
        new std::unordered_map<u32, LC32PendingGuestMappingTeardown>;
    return *teardowns;
}

static u32 LC32CreatePendingGuestMappingTeardown(
        const std::shared_ptr<LC32HostWeakMappingEntry>& entry,
        LC32HostMappingRetirementProvenance provenance,
        id retainedHostObject) {
    if(!entry) return 0;
    static u32 nextToken = 1;
    std::lock_guard<std::mutex> lock(
        LC32PendingGuestMappingTeardownMutex());
    auto &teardowns = LC32PendingGuestMappingTeardowns();
    for(;;) {
        const u32 token = nextToken++;
        if(!nextToken) nextToken = 1;
        if(!token || teardowns.count(token)) continue;
        try {
            teardowns.emplace(token, LC32PendingGuestMappingTeardown{
                entry->guestObject, entry->generation,
                entry->expectedHostAddress, (u64)retainedHostObject,
                provenance, entry,
            });
        } catch(const std::bad_alloc &) {
            return 0;
        }
        return token;
    }
}

static bool LC32TakePendingGuestMappingTeardown(
        u32 token, u32 guestObject,
        LC32PendingGuestMappingTeardown *result) {
    if(!token || !guestObject || !result) return false;
    std::lock_guard<std::mutex> lock(
        LC32PendingGuestMappingTeardownMutex());
    auto &teardowns = LC32PendingGuestMappingTeardowns();
    auto iterator = teardowns.find(token);
    if(iterator == teardowns.end() ||
            iterator->second.guestObject != guestObject) {
        return false;
    }
    *result = std::move(iterator->second);
    teardowns.erase(iterator);
    return true;
}

static u32 LC32BeginGuestMappingTeardown(
        u32 guestObject, u64 expectedHostAddress) {
    if(!guestObject) return 0;

    /* A native lifetime pin may already have entered deallocation and zeroed
     * its weak slot. The last remaining guest owner still needs an exact token
     * with which to remove that tombstone, but must never synchronize on or
     * otherwise dereference the former native address. */
    if(!expectedHostAddress) {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end() ||
           iterator->second->state !=
               LC32HostWeakMappingState::Retiring ||
           iterator->second->retirementProvenance !=
               LC32HostMappingRetirementProvenance::
                   NativePeerDeallocating) {
            return 0;
        }
        const u32 token = LC32CreatePendingGuestMappingTeardown(
            iterator->second,
            LC32HostMappingRetirementProvenance::
                GuestTeardownWithoutNativePeer,
            nil);
        if(!token) return 0;
        iterator->second->retiringOwnerThreadToken = 0;
        iterator->second->retirementProvenance =
            LC32HostMappingRetirementProvenance::
                GuestTeardownWithoutNativePeer;
        return token;
    }

    std::shared_ptr<LC32HostWeakMappingEntry> entry;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end() ||
           iterator->second->state != LC32HostWeakMappingState::Live ||
           iterator->second->lifetime !=
               LC32HostMappingLifetime::Provisional ||
           iterator->second->expectedHostAddress != expectedHostAddress) {
            return 0;
        }
        entry = iterator->second;
    }

    id retainedHostObject = nil;
    bool nativePeerIsLive = true;
    if(entry->weakCompatible && entry->invocationRetainCompatible) {
        retainedHostObject = LC32LoadWeakRetainedHostObject(
            &entry->weakHostObject,
            (id)(uintptr_t)entry->expectedHostAddress);
        nativePeerIsLive = retainedHostObject &&
            (u64)retainedHostObject == expectedHostAddress;
        if(retainedHostObject && !nativePeerIsLive) {
            LC32HostInvocationReceiverGuard rollback;
            if(!rollback.adoptRetained(retainedHostObject)) abort();
            rollback.releaseNow();
            retainedHostObject = nil;
        }
    }

    if(!nativePeerIsLive) {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end() ||
           iterator->second.get() != entry.get() ||
           iterator->second->generation != entry->generation ||
           iterator->second->expectedHostAddress != expectedHostAddress ||
           !((iterator->second->state ==
                  LC32HostWeakMappingState::Live &&
              iterator->second->lifetime ==
                  LC32HostMappingLifetime::Provisional) ||
             (iterator->second->state ==
                  LC32HostWeakMappingState::Retiring &&
              iterator->second->retirementProvenance ==
                  LC32HostMappingRetirementProvenance::
                      NativePeerDeallocating))) {
            return 0;
        }
        const u32 token = LC32CreatePendingGuestMappingTeardown(
            iterator->second,
            LC32HostMappingRetirementProvenance::
                GuestTeardownWithoutNativePeer,
            nil);
        if(!token) return 0;
        iterator->second->state = LC32HostWeakMappingState::Retiring;
        iterator->second->retiringOwnerThreadToken = 0;
        iterator->second->retirementProvenance =
            LC32HostMappingRetirementProvenance::
                GuestTeardownWithoutNativePeer;
        return token;
    }

    id hostObject = retainedHostObject ?: (id)(uintptr_t)expectedHostAddress;
    u32 token = 0;
    /* Publish retirement under the same host->registry lock order used by
     * host-to-guest conversion. The promoted native +1 remains owned by the
     * pending token until Finish, so every host monitor access below is safe. */
    @synchronized(hostObject) {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator != registry.entries.end() &&
           iterator->second.get() == entry.get() &&
           iterator->second->generation == entry->generation &&
           iterator->second->state == LC32HostWeakMappingState::Live &&
           iterator->second->lifetime ==
               LC32HostMappingLifetime::Provisional &&
           iterator->second->expectedHostAddress == expectedHostAddress) {
            token = LC32CreatePendingGuestMappingTeardown(
                iterator->second,
                LC32HostMappingRetirementProvenance::
                    GuestTeardownWithNativeOwnership,
                retainedHostObject);
            if(token) {
                iterator->second->state =
                    LC32HostWeakMappingState::Retiring;
                iterator->second->retiringOwnerThreadToken =
                    LC32CurrentHostMappingThreadToken();
                iterator->second->retirementProvenance =
                    LC32HostMappingRetirementProvenance::
                        GuestTeardownWithNativeOwnership;
                retainedHostObject = nil;
            }
        }
    }
    if(retainedHostObject) {
        LC32HostInvocationReceiverGuard rollback;
        if(!rollback.adoptRetained(retainedHostObject)) abort();
        rollback.releaseNow();
    }
    return token;
}

static u32 LC32FinishGuestMappingTeardown(
        u32 guestObject, u32 token, bool releaseHostOwnership) {
    LC32PendingGuestMappingTeardown pending = {};
    if(!LC32TakePendingGuestMappingTeardown(
            token, guestObject, &pending)) return 0;

    auto releaseRetainedHostObject = [&] {
        if(!pending.retainedHostObject) return;
        LC32HostInvocationReceiverGuard guard;
        if(!guard.adoptRetained(
                (id)(uintptr_t)pending.retainedHostObject)) abort();
        pending.retainedHostObject = 0;
        guard.releaseNow();
    };

    if(pending.provenance ==
            LC32HostMappingRetirementProvenance::
                GuestTeardownWithoutNativePeer) {
        std::shared_ptr<LC32HostWeakMappingEntry> removed;
        bool invalidCurrentGeneration = false;
        {
            LC32HostWeakRegistry &registry = LC32HostWeakMappings();
            std::lock_guard<std::mutex> lock(registry.mutex);
            auto iterator = registry.entries.find(guestObject);
            if(iterator != registry.entries.end() &&
               iterator->second.get() == pending.entry.get() &&
               iterator->second->generation == pending.generation) {
                if(iterator->second->state !=
                       LC32HostWeakMappingState::Retiring ||
                   iterator->second->expectedHostAddress !=
                       pending.expectedHostAddress ||
                   iterator->second->retirementProvenance !=
                       pending.provenance) {
                    invalidCurrentGeneration = true;
                } else {
                    iterator->second->state =
                        LC32HostWeakMappingState::Superseded;
                    LC32UnindexHostMappingLocked(
                        registry, iterator->second);
                    removed = std::move(iterator->second);
                    registry.entries.erase(iterator);
                }
            }
        }
        LC32DeferHostWeakEntryRelease(std::move(removed));
        LC32DeferHostWeakEntryRelease(std::move(pending.entry));
        return invalidCurrentGeneration ? 0 : 1;
    }

    if(pending.provenance !=
            LC32HostMappingRetirementProvenance::
                GuestTeardownWithNativeOwnership) {
        releaseRetainedHostObject();
        LC32DeferHostWeakEntryRelease(std::move(pending.entry));
        return 0;
    }

    id hostObject = (id)(uintptr_t)pending.expectedHostAddress;
    std::shared_ptr<LC32HostWeakMappingEntry> removedCurrent;
    bool clearOldReverseMapping = false;
    bool invalidCurrentGeneration = false;
    @synchronized(hostObject) {
        {
            LC32HostWeakRegistry &registry = LC32HostWeakMappings();
            std::lock_guard<std::mutex> lock(registry.mutex);
            auto iterator = registry.entries.find(guestObject);
            if(iterator == registry.entries.end()) {
                clearOldReverseMapping = true;
            } else if(iterator->second->generation == pending.generation) {
                if(iterator->second->state !=
                        LC32HostWeakMappingState::Retiring ||
                        iterator->second->expectedHostAddress !=
                            pending.expectedHostAddress ||
                        iterator->second->retirementProvenance !=
                            pending.provenance) {
                    invalidCurrentGeneration = true;
                } else if(releaseHostOwnership) {
                    /* Keep this exact generation indexed while its paired
                     * native +1 is consumed below. It is no longer callable:
                     * native -release may begin deallocation and reenter the
                     * bridge before the entry is finally removed. */
                    iterator->second->retirementProvenance =
                        LC32HostMappingRetirementProvenance::
                            FinalHostRelease;
                    clearOldReverseMapping = true;
                } else {
                    iterator->second->state =
                        LC32HostWeakMappingState::Superseded;
                    LC32UnindexHostMappingLocked(
                        registry, iterator->second);
                    removedCurrent = std::move(iterator->second);
                    registry.entries.erase(iterator);
                    clearOldReverseMapping = true;
                }
            } else {
                /* Same-host reuse owns the same numeric reverse pointer and
                 * must preserve it. A different host leaves the old reverse
                 * mapping stale and safe to clear under this monitor. */
                clearOldReverseMapping =
                    iterator->second->expectedHostAddress !=
                        pending.expectedHostAddress;
            }
            /* Keep the forward-map decision and reverse-map clear atomic
             * with respect to publishers. They use this same host->registry
             * lock order, so a newer same-host generation cannot appear in
             * the gap and have its reverse mapping cleared by old teardown. */
            if(!invalidCurrentGeneration && clearOldReverseMapping) {
                LC32ClearGuestSelfIfEqualWhileSynchronized(
                    hostObject, guestObject);
            }
        }
    }
    if(invalidCurrentGeneration) {
        releaseRetainedHostObject();
        LC32DeferHostWeakEntryRelease(std::move(pending.entry));
        return 0;
    }

    if(!releaseHostOwnership) {
        releaseRetainedHostObject();
        LC32DeferHostWeakEntryRelease(std::move(removedCurrent));
        LC32DeferHostWeakEntryRelease(std::move(pending.entry));
        return 1;
    }

    /* The Begin token proves that the guest still owns this exact native +1.
     * Consume it directly instead of erasing the generation and then making a
     * second raw-address SVC. The pending weak promotion keeps ordinary peers
     * alive through this operation; NSAutoreleasePool deliberately relies on
     * its paired ownership and has no extra promotion. */
    {
        LC32GuestHostCallQuiescence quiescence;
        LC32ReleaseOwnedHostObject(hostObject);
        quiescence.finish();
    }
    /* If the paired release left only the promotion token, dropping it may run
     * native teardown and guest callbacks. FinalHostRelease remains indexed
     * and noncallable until that complete transition returns. */
    releaseRetainedHostObject();

    /* The releases may run arbitrary teardown and allow a recycled guest
     * address to publish a newer generation. Remove only the exact entry
     * represented by the Begin token. */
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    auto iterator = registry.entries.find(guestObject);
    if(iterator != registry.entries.end() &&
       iterator->second.get() == pending.entry.get() &&
       iterator->second->generation == pending.generation &&
       iterator->second->expectedHostAddress ==
           pending.expectedHostAddress &&
       iterator->second->state ==
           LC32HostWeakMappingState::Retiring &&
       iterator->second->retirementProvenance ==
           LC32HostMappingRetirementProvenance::FinalHostRelease) {
        iterator->second->state =
            LC32HostWeakMappingState::Superseded;
        LC32UnindexHostMappingLocked(registry, iterator->second);
        removedCurrent = std::move(iterator->second);
        registry.entries.erase(iterator);
    }

    LC32DeferHostWeakEntryRelease(std::move(removedCurrent));
    LC32DeferHostWeakEntryRelease(std::move(pending.entry));
    return 1;
}

static bool LC32DetachDeadNativePeerGuestKey(u32 guestObject) {
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    auto iterator = registry.entries.find(guestObject);
    if(iterator == registry.entries.end() ||
       iterator->second->state != LC32HostWeakMappingState::Retiring ||
       iterator->second->retirementProvenance !=
            LC32HostMappingRetirementProvenance::NativePeerDeallocating) {
        return true;
    }
    try {
        registry.retiringEntries.emplace(
            iterator->second->generation, iterator->second);
    } catch(const std::bad_alloc &) {
        /* The guest must not free its allocation while its old registry key
         * remains visible. Report failure before root -dealloc disposes it. */
        return false;
    }
    LC32UnindexHostMappingLocked(registry, iterator->second);
    registry.entries.erase(iterator);
    return true;
}

extern "C" u32 LC32UpdateHostMapping(
        u32 guestObject, LC32HostMappingOperation operation,
        u64 hostObject) {
    static_assert(sizeof(LC32HostMappingOperation) == sizeof(u32));
    switch(operation) {
        case LC32HostMappingPublishProvisional:
            return LC32PublishHostMapping(
                (id)(uintptr_t)hostObject, guestObject,
                LC32HostMappingLifetime::Provisional) != 0;
        case LC32HostMappingPublishPermanent:
            return LC32PublishHostMapping(
                (id)(uintptr_t)hostObject, guestObject,
                LC32HostMappingLifetime::Permanent) != 0;
        case LC32HostMappingClearIfEqual:
            return LC32ClearHostMappingIfEqual(
                guestObject, hostObject);
        case LC32HostMappingBeginGuestTeardown:
            return LC32BeginGuestMappingTeardown(
                guestObject, hostObject);
        case LC32HostMappingFinishGuestTeardown:
            return LC32FinishGuestMappingTeardown(
                guestObject, static_cast<u32>(hostObject), false);
        case LC32HostMappingFinishGuestTeardownAndReleaseHost:
            return LC32FinishGuestMappingTeardown(
                guestObject, static_cast<u32>(hostObject), true);
        case LC32HostMappingGuestRootDealloc:
            return LC32DetachDeadNativePeerGuestKey(guestObject);
        case LC32HostMappingReleaseNativeProxy:
            return LC32ReleaseNativeProxyOwnership(
                guestObject, hostObject, true);
        case LC32HostMappingReleaseNativeProxyLogicalOwnership:
            return LC32ReleaseNativeProxyOwnership(
                guestObject, hostObject, false);
    }
    return 0;
}

static void LC32MarkHostWeakMappingRetiring(
        u32 guestObject, u64 generation,
        LC32HostMappingRetirementProvenance provenance) {
    if(!guestObject || !generation) return;
    assert(provenance != LC32HostMappingRetirementProvenance::None);
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    auto iterator = registry.entries.find(guestObject);
    if(iterator != registry.entries.end() &&
       iterator->second->generation == generation) {
        iterator->second->state = LC32HostWeakMappingState::Retiring;
        iterator->second->retiringOwnerThreadToken =
            LC32CurrentHostMappingThreadToken();
        iterator->second->retirementProvenance = provenance;
    }
}

static void LC32RefreshHostWeakMappingRetiringOwner(
        u32 guestObject, u64 generation) {
    if(!guestObject || !generation) return;
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    auto iterator = registry.entries.find(guestObject);
    if(iterator != registry.entries.end() &&
       iterator->second->generation == generation &&
       iterator->second->state == LC32HostWeakMappingState::Retiring &&
       LC32RetirementAllowsCurrentThreadInvocation(
           iterator->second->retirementProvenance)) {
        iterator->second->retiringOwnerThreadToken =
            LC32CurrentHostMappingThreadToken();
    }
}

static void LC32RestoreHostWeakMappingLive(
        u32 guestObject, u64 generation) {
    if(!guestObject || !generation) return;
    LC32HostWeakRegistry &registry = LC32HostWeakMappings();
    std::lock_guard<std::mutex> lock(registry.mutex);
    auto iterator = registry.entries.find(guestObject);
    if(iterator != registry.entries.end() &&
       iterator->second->generation == generation &&
       iterator->second->state == LC32HostWeakMappingState::Retiring &&
       iterator->second->retirementProvenance ==
           LC32HostMappingRetirementProvenance::
               GuestMirrorWithTransferredReference) {
        iterator->second->state = LC32HostWeakMappingState::Live;
        iterator->second->retiringOwnerThreadToken = 0;
        iterator->second->retirementProvenance =
            LC32HostMappingRetirementProvenance::None;
    }
}

static void LC32FinalizeHostWeakMappingRetirement(
        u32 guestObject, u64 generation) {
    if(!guestObject || !generation) return;
    std::shared_ptr<LC32HostWeakMappingEntry> retired;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator != registry.entries.end() &&
           iterator->second->generation == generation) {
            LC32UnindexHostMappingLocked(registry, iterator->second);
            retired = std::move(iterator->second);
            registry.entries.erase(iterator);
        } else {
            auto detached = registry.retiringEntries.find(generation);
            if(detached == registry.retiringEntries.end() ||
                    detached->second->guestObject != guestObject) return;
            retired = std::move(detached->second);
            registry.retiringEntries.erase(detached);
        }
    }
    /* Destroying the native weak slot may acquire its SideTable stripe.  The
     * shared_ptr deliberately leaves the registry lock first and is then
     * destroyed on the host release queue. */
    LC32DeferHostWeakEntryRelease(std::move(retired));
}

static void LC32DeferHostWeakMappingRetirementFinalization(
        u32 guestObject, u64 generation) {
    if(!guestObject || !generation) return;
    /* Guest root -dealloc has already detached its ARM32 key before freeing
     * the allocation. Only the native-address tombstone and weak slot remain;
     * their destruction must stay outside the native deallocation stack. */
    dispatch_async(LC32HostWeakMappings().deferredReleaseQueue, ^{
        LC32FinalizeHostWeakMappingRetirement(guestObject, generation);
    });
}

static void LC32DeferOwnedHostRelease(void *ownedHostObject) {
    if(!ownedHostObject) return;
    dispatch_async(LC32HostWeakMappings().deferredReleaseQueue, ^{
        LC32ReleaseOwnedHostObject((__bridge id)ownedHostObject);
    });
}

struct LC32PendingHostWeakRetain {
    void *ownedHostObject;
    u32 guestObject;
    u64 weakRegistryGeneration;
};

static std::mutex& LC32PendingHostWeakRetainMutex() {
    static std::mutex *mutex = new std::mutex;
    return *mutex;
}

static std::unordered_map<u32, LC32PendingHostWeakRetain>&
LC32PendingHostWeakRetains() {
    static auto *retains =
        new std::unordered_map<u32, LC32PendingHostWeakRetain>;
    return *retains;
}

static u32 LC32CreatePendingHostWeakRetain(
        void *ownedHostObject, u32 guestObject, u64 generation) {
    if(!ownedHostObject || !guestObject || !generation) return 0;

    static u32 nextToken = LC32HostWeakRetainFirstToken;
    std::lock_guard<std::mutex> lock(LC32PendingHostWeakRetainMutex());
    auto &retains = LC32PendingHostWeakRetains();
    for(;;) {
        u32 token = nextToken++;
        if(nextToken < LC32HostWeakRetainFirstToken) {
            nextToken = LC32HostWeakRetainFirstToken;
        }
        if(token < LC32HostWeakRetainFirstToken || retains.count(token)) {
            continue;
        }
        try {
            retains.emplace(token, LC32PendingHostWeakRetain{
                ownedHostObject, guestObject, generation,
            });
        } catch(const std::bad_alloc &) {
            return 0;
        }
        return token;
    }
}

extern "C" u32 LC32FinishHostWeakRetain(
        u32 token, u32 guestObject, u32 commit) {
    if(token < LC32HostWeakRetainFirstToken || !guestObject || commit > 1) {
        return 0;
    }

    LC32PendingHostWeakRetain pending = {};
    {
        std::lock_guard<std::mutex> lock(
            LC32PendingHostWeakRetainMutex());
        auto &retains = LC32PendingHostWeakRetains();
        auto iterator = retains.find(token);
        if(iterator == retains.end() ||
           iterator->second.guestObject != guestObject) {
            return 0;
        }
        pending = iterator->second;
        retains.erase(iterator);
    }

    if(!commit) {
        /* Never release inline: SVC 1021 is called from the guest runtime's
         * retainWeakReference hook while its SideTable stripe is locked. */
        LC32DeferOwnedHostRelease(pending.ownedHostObject);
    }
    return 1;
}

extern "C" LC32HostWeakRetainResult
LC32TryRetainHostWeakReference(u32 guestObject) {
    std::shared_ptr<LC32HostWeakMappingEntry> entry;
    bool mappingWasLive = false;
    bool mappingWasPermanent = false;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end()) {
            return LC32HostWeakRetainNoMapping;
        }
        entry = iterator->second;
        mappingWasLive =
            entry->state == LC32HostWeakMappingState::Live;
        mappingWasPermanent = mappingWasLive &&
            entry->lifetime == LC32HostMappingLifetime::Permanent;
    }
    /* A class or process-lifetime constant needs no paired native +1.  Let
     * guest libobjc perform its ordinary local weak try-retain; in particular,
     * a native tagged singleton may not support a weak slot at all. */
    if(mappingWasPermanent) {
        LC32DeferHostWeakEntryRelease(std::move(entry));
        return LC32HostWeakRetainNoMapping;
    }
    if(!mappingWasLive) {
        LC32DeferHostWeakEntryRelease(std::move(entry));
        return LC32HostWeakRetainMappedDead;
    }

    id retainedHostObject = LC32LoadWeakRetainedHostObject(
        &entry->weakHostObject,
        (id)(uintptr_t)entry->expectedHostAddress);
    if(!retainedHostObject) {
        LC32DeferHostWeakEntryRelease(std::move(entry));
        return LC32HostWeakRetainMappedDead;
    }

    bool mappingIsCurrent = false;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        mappingIsCurrent = iterator != registry.entries.end() &&
            iterator->second.get() == entry.get() &&
            iterator->second->generation == entry->generation &&
            iterator->second->state == LC32HostWeakMappingState::Live;
    }

#if __has_feature(objc_arc)
    /* Move the load's +1 out of ARC.  On success it belongs to the matching
     * guest try-retain.  A superseded load must be released away from this
     * guest thread: inline deallocation could run the lifetime pin and reenter
     * the guest SideTable stripe which objc_loadWeakRetained still holds. */
    void *ownedHostObject = (__bridge_retained void *)retainedHostObject;
    retainedHostObject = nil;
#else
    void *ownedHostObject = retainedHostObject;
#endif
    if(mappingIsCurrent) {
        const u64 generation = entry->generation;
        LC32DeferHostWeakEntryRelease(std::move(entry));
        const u32 token = LC32CreatePendingHostWeakRetain(
            ownedHostObject, guestObject, generation);
        if(token) return token;
        LC32DeferOwnedHostRelease(ownedHostObject);
        return LC32HostWeakRetainMappedDead;
    }

    LC32DeferOwnedHostRelease(ownedHostObject);
    LC32DeferHostWeakEntryRelease(std::move(entry));
    return LC32HostWeakRetainMappedDead;
}

static int LC32UniqueSelectorArgumentIndexNamed(SEL selector,
                                                 const char *expectedName) {
    const char *component = sel_getName(selector);
    const size_t expectedLength = strlen(expectedName);
    int matchingIndex = -1;
    unsigned int argumentIndex = 0;
    while(component) {
        const char *colon = strchr(component, ':');
        if(!colon) break;
        if((size_t)(colon - component) == expectedLength &&
           memcmp(component, expectedName, expectedLength) == 0) {
            if(matchingIndex >= 0) return -1;
            matchingIndex = (int)argumentIndex;
        }
        component = colon + 1;
        argumentIndex++;
    }
    return matchingIndex;
}

template<typename T>
static bool LC32ReadGuestInvocationValue(u32 guestStorage, T &value) {
    return guestStorage && Dynarmic_mem_1read(
        guestStorage, sizeof(value), reinterpret_cast<char *>(&value)) == 0;
}

template<typename T>
static void LC32StoreHostInvocationValue(
        std::array<u8, 16> &storage, T value) {
    static_assert(sizeof(value) <= 16, "invocation value exceeds staging");
    memcpy(storage.data(), &value, sizeof(value));
}

/*
 * -[NSInvocation setArgument:atIndex:] copies bytes using native type sizes.
 * The supplied pointer, however, names raw ARM32 storage. Rebuild the value
 * into host-owned aligned storage instead of exposing a guest address or
 * letting Foundation read eight-byte pointers from four-byte guest values.
 */
static bool LC32PrepareHostInvocationArgument(
        NSInvocation *invocation, u32 guestStorage, int32_t argumentIndex,
        std::array<u8, 16> &hostStorage) {
    if(!invocation || !guestStorage || argumentIndex < 0) return false;

    NSMethodSignature *signature = invocation.methodSignature;
    if(!signature || (NSUInteger)argumentIndex >= signature.numberOfArguments)
        return false;

    const char *type = [signature getArgumentTypeAtIndex:
        (NSUInteger)argumentIndex];
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    if(!type || !*type) return false;

    NSUInteger nativeSize = 0;
    NSUInteger nativeAlignment = 0;
    NSGetSizeAndAlignment(type, &nativeSize, &nativeAlignment);
    if(!nativeSize || nativeSize > hostStorage.size()) return false;
    hostStorage.fill(0);

    switch(*type) {
        case '@':
        case '#': {
            u32 guestObject = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, guestObject))
                return false;
            const u64 hostObject = guestObject
                ? LC32GuestToHostReturnType(
                    const_cast<char *>(type), guestObject)
                : 0;
            LC32StoreHostInvocationValue(hostStorage, hostObject);
            return nativeSize == sizeof(hostObject);
        }
        case ':': {
            u32 guestSelector = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, guestSelector))
                return false;
            const u64 hostSelector = guestSelector
                ? LC32GetHostSelector(guestSelector)
                : 0;
            LC32StoreHostInvocationValue(hostStorage, hostSelector);
            return nativeSize == sizeof(hostSelector);
        }
        case 'B':
        case 'C': {
            uint8_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'c': {
            int8_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'S': {
            uint16_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 's': {
            int16_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'I': {
            uint32_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'i': {
            int32_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'L': {
            uint32_t guestValue = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, guestValue))
                return false;
            const unsigned long hostValue = guestValue;
            LC32StoreHostInvocationValue(hostStorage, hostValue);
            return nativeSize == sizeof(hostValue);
        }
        case 'l': {
            int32_t guestValue = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, guestValue))
                return false;
            const long hostValue = guestValue;
            LC32StoreHostInvocationValue(hostStorage, hostValue);
            return nativeSize == sizeof(hostValue);
        }
        case 'Q': {
            uint64_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'q': {
            int64_t value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'f': {
            float value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        case 'd': {
            double value = 0;
            if(!LC32ReadGuestInvocationValue(guestStorage, value))
                return false;
            LC32StoreHostInvocationValue(hostStorage, value);
            return nativeSize == sizeof(value);
        }
        default:
            return false;
    }
}

#pragma mark Guest -> Host functions

u32 LC32HostToGuestCopyClassName(u32 guest_output, size_t length, u64 host_object) {
    const char *input = class_getName([(id)host_object class]);
    length = MIN(strlen(input), length);
    // write null terminator aswell
    Dynarmic_mem_1write(guest_output, length+1, (char *)input);
    return length;
}

u32 LC32CopyHostCString(u64 host_cstring, u32 guest_output,
                        size_t capacity) {
    if(!host_cstring) return 0;
    /*
     * This SVC receives a native pointer from guest state.  Do not dereference
     * it with strlen: a stale or malformed value would turn a guest failure
     * into a host EXC_BAD_ACCESS, and a missing terminator would scan without a
     * bound.  Reading our own task through Mach gives invalid addresses a
     * recoverable error and keeps Objective-C type encodings reasonably
     * bounded.
     */
    constexpr size_t maximumByteCount = 4096;
    std::array<char, maximumByteCount> bytes = {};
    size_t byteCount = 0;
    while(byteCount < bytes.size()) {
        if(host_cstring > UINT64_MAX - byteCount) return 0;
        const vm_address_t address = (vm_address_t)(host_cstring + byteCount);
        const size_t pageOffset = address & (vm_page_size - 1);
        const size_t pageRemaining = vm_page_size - pageOffset;
        const size_t requested = MIN(
            pageRemaining, bytes.size() - byteCount);
        vm_size_t copied = 0;
        const kern_return_t result = vm_read_overwrite(
            mach_task_self(), address, requested,
            reinterpret_cast<vm_address_t>(bytes.data() + byteCount),
            &copied);
        if(result != KERN_SUCCESS || copied == 0 || copied > requested) {
            return 0;
        }
        const void *terminator = memchr(
            bytes.data() + byteCount, '\0', (size_t)copied);
        if(terminator) {
            byteCount = static_cast<const char *>(terminator) -
                bytes.data() + 1;
            break;
        }
        byteCount += (size_t)copied;
        if(copied != requested) return 0;
    }
    if(byteCount == bytes.size() && bytes.back() != '\0') return 0;
    if(guest_output && capacity) {
        const size_t copyCount = MIN(byteCount, capacity);
        if(Dynarmic_mem_1write(
                guest_output, copyCount, bytes.data()) != 0) {
            return 0;
        }
        if(copyCount < byteCount) {
            const char terminator = '\0';
            if(Dynarmic_mem_1write(guest_output + copyCount - 1, 1,
                                   (char *)&terminator) != 0) {
                return 0;
            }
        }
    }
    return (u32)byteCount;
}

u32 LC32CopyHostStringUTF8(u64 host_object, u32 guest_output,
                           size_t capacity) {
    const char *bytes = [(NSString *)(id)host_object UTF8String];
    if(!bytes) return 0;

    const size_t byteCount = strlen(bytes) + 1;
    if(byteCount > UINT32_MAX) return 0;
    if(guest_output && capacity) {
        const size_t copyCount = MIN(byteCount, capacity);
        if(Dynarmic_mem_1write(guest_output, copyCount, (char *)bytes) != 0) {
            return 0;
        }
        if(copyCount < byteCount) {
            const char terminator = '\0';
            if(Dynarmic_mem_1write(guest_output + copyCount - 1, 1,
                                   (char *)&terminator) != 0) {
                return 0;
            }
        }
    }
    return (u32)byteCount;
}

static size_t LC32HostStringTerminatorSize(NSStringEncoding encoding) {
    switch(encoding) {
        case NSUTF16StringEncoding:
        case NSUTF16BigEndianStringEncoding:
        case NSUTF16LittleEndianStringEncoding:
            return sizeof(uint16_t);
        case NSUTF32StringEncoding:
        case NSUTF32BigEndianStringEncoding:
        case NSUTF32LittleEndianStringEncoding:
            return sizeof(uint32_t);
        default:
            return sizeof(char);
    }
}

u32 LC32CopyHostStringBytes(u64 host_object, u32 encoding,
                            u32 guest_output, u32 capacity) {
    NSString *string = (NSString *)(id)host_object;
    const NSStringEncoding nativeEncoding = (NSStringEncoding)encoding;
    const NSUInteger payloadCount =
        [string lengthOfBytesUsingEncoding:nativeEncoding];
    const size_t terminatorSize =
        LC32HostStringTerminatorSize(nativeEncoding);
    /* Some legacy clients pass UTF-16 C strings to a four-byte wchar_t
     * scanner. Keep the requested UTF-16 payload, but pad through the next
     * aligned wide NUL so single-character input and deletion do not scan
     * stale guest-buffer bytes. This tolerates that historical misuse; it
     * does not reinterpret multi-character UTF-16 strings as UTF-32. */
    const size_t widePadding = terminatorSize == sizeof(uint16_t)
        ? sizeof(uint32_t) +
            (sizeof(uint32_t) - payloadCount % sizeof(uint32_t)) % sizeof(uint32_t)
        : terminatorSize;
    if(payloadCount > UINT32_MAX - widePadding) return 0;

    const u32 byteCount = (u32)(payloadCount + widePadding);
    char *bytes = (char *)malloc(byteCount);
    if(!bytes) return 0;
    if(![string getCString:bytes maxLength:byteCount
                  encoding:nativeEncoding]) {
        free(bytes);
        return 0;
    }
    /* Foundation terminates -getCString: with one zero byte even for its
     * fixed-width UTF-16/UTF-32 encodings.  C callers consume a complete
     * encoded NUL code unit, so make the remaining bytes deterministic. */
    memset(bytes + payloadCount, 0, widePadding);

    if(guest_output && capacity >= byteCount &&
            Dynarmic_mem_1write(guest_output, byteCount, bytes) != 0) {
        free(bytes);
        return 0;
    }
    free(bytes);
    return byteCount;
}

u64 LC32HostStringRangeOfString(
        const LC32FoundationStringRangeRequest *request) {
    const u64 hostString = (u64)request->hostStringLow |
        ((u64)request->hostStringHigh << 32);
    const u64 hostNeedle = (u64)request->hostNeedleLow |
        ((u64)request->hostNeedleHigh << 32);
    const u64 hostLocale = (u64)request->hostLocaleLow |
        ((u64)request->hostLocaleHigh << 32);
    NSString *source = (NSString *)(id)hostString;
    NSString *needle = (NSString *)(id)hostNeedle;
    NSRange range;
    switch(request->variant) {
        case LC32FoundationStringRangePlain:
            range = [source rangeOfString:needle];
            break;
        case LC32FoundationStringRangeWithOptions:
            range = [source rangeOfString:needle
                                  options:(NSStringCompareOptions)
                                              request->options];
            break;
        case LC32FoundationStringRangeWithRange:
            range = [source rangeOfString:needle
                                  options:(NSStringCompareOptions)
                                              request->options
                                    range:NSMakeRange(request->rangeLocation,
                                                      request->rangeLength)];
            break;
        case LC32FoundationStringRangeWithLocale:
            range = [source rangeOfString:needle
                                  options:(NSStringCompareOptions)
                                              request->options
                                    range:NSMakeRange(request->rangeLocation,
                                                      request->rangeLength)
                                   locale:(NSLocale *)(id)hostLocale];
            break;
        default:
            return (u64)INT32_MAX;
    }

    /* NSNotFound is NSIntegerMax in each process, so it must be translated
     * rather than merely truncating the ARM64 value to its low word. */
    const u32 location = range.location == NSNotFound
        ? (u32)INT32_MAX : (u32)range.location;
    const u32 length = (u32)range.length;
    return (u64)location | ((u64)length << 32);
}

static bool LC32HostObjectIsDispatchData(id object) {
    for(Class cls = object_getClass(object); cls;
            cls = class_getSuperclass(cls)) {
        const char *name = class_getName(cls);
        if(name && (!strcmp(name, "OS_dispatch_data") ||
                    !strcmp(name, "OS_dispatch_data_empty"))) {
            return true;
        }
    }
    return false;
}

u32 LC32CopyHostDataBytes(u64 host_object, u32 guest_output, u32 length,
                          u32 offset) {
    id object = (id)host_object;
    const void *bytes = nullptr;
    size_t dataLength = 0;
    dispatch_data_t mappedData = nullptr;

    if(LC32HostObjectIsDispatchData(object)) {
        dispatch_data_t data = (dispatch_data_t)object;
        dataLength = dispatch_data_get_size(data);
        if(offset > dataLength || length > dataLength - offset) {
            return UINT32_MAX;
        }
        if(!length) return 0;

        size_t mappedLength = 0;
        mappedData = dispatch_data_create_map(data, &bytes, &mappedLength);
        if(!mappedData || mappedLength < dataLength) {
            return UINT32_MAX;
        }
    } else {
        NSData *data = (NSData *)object;
        dataLength = data.length;
        if(offset > dataLength || length > dataLength - offset) {
            return UINT32_MAX;
        }
        if(!length) return 0;
        bytes = data.bytes;
    }

    if(!bytes || !guest_output || Dynarmic_mem_1write(
            guest_output, length,
            (char *)bytes + offset) != 0) {
        return UINT32_MAX;
    }
    return length;
}

static id LC32CoreDataMergePolicyForSymbol(const char *symbolName) {
    struct MergePolicySymbol {
        const char *symbol;
        const char *getter;
    };
    static constexpr MergePolicySymbol symbols[] = {
        { "NSErrorMergePolicy", "errorMergePolicy" },
        { "NSMergeByPropertyStoreTrumpMergePolicy",
          "mergeByPropertyStoreTrumpMergePolicy" },
        { "NSMergeByPropertyObjectTrumpMergePolicy",
          "mergeByPropertyObjectTrumpMergePolicy" },
        { "NSOverwriteMergePolicy", "overwriteMergePolicy" },
        { "NSRollbackMergePolicy", "rollbackMergePolicy" },
    };

    const char *getter = nullptr;
    for(const MergePolicySymbol &candidate : symbols) {
        if(!strcmp(symbolName, candidate.symbol)) {
            getter = candidate.getter;
            break;
        }
    }
    if(!getter) return nil;

    /*
     * On current Core Data, dlopen exposes the legacy data symbols but leaves
     * them nil until +[NSMergePolicy initialize]. Some releases may omit the
     * exports entirely. The replacement class properties preserve the same
     * immortal singleton identities in either case, without asking the host
     * object to enter guest code while guest dyld is running constructors.
     */
    Class mergePolicyClass = objc_getClass("NSMergePolicy");
    SEL selector = sel_registerName(getter);
    if(!mergePolicyClass ||
       !class_respondsToSelector(object_getClass(mergePolicyClass), selector)) {
        return nil;
    }
    using Getter = id (*)(id, SEL);
    return reinterpret_cast<Getter>(objc_msgSend)(mergePolicyClass, selector);
}

u64 LC32Dlsym(u32 guest_name, bool isFunction) {
    DynarmicHostString host_name(guest_name);

    u64 r = (u64)dlsym(RTLD_DEFAULT, host_name.hostPtr);
    if(r && !isFunction) r = *(u64*)r;
    if(!r && !isFunction) {
        r = (u64)LC32CoreDataMergePolicyForSymbol(host_name.hostPtr);
    }
    if(!r) {
        printf("LC32: dlsym %s = 0x%llx\n", host_name.hostPtr, r);
    } else {
        LC32_DEBUG_PRINTF("LC32: dlsym %s = 0x%llx\n", host_name.hostPtr, r);
    }
    return r;
}

extern "C" u32 LC32LoadNativeFramework(u32 guestFrameworkName) {
    DynarmicHostString frameworkName(guestFrameworkName);
    if(!frameworkName.hostPtr || !frameworkName.hostPtr[0]) {
        fprintf(stderr, "LC32: refusing an empty native framework name\n");
        return 0;
    }

    const char *cursor = frameworkName.hostPtr;
    size_t length = 0;
    for(; *cursor; cursor++, length++) {
        const unsigned char character = (unsigned char)*cursor;
        const bool valid =
            (character >= 'a' && character <= 'z') ||
            (character >= 'A' && character <= 'Z') ||
            (character >= '0' && character <= '9') ||
            character == '_' || character == '-';
        if(!valid || length >= 127) {
            fprintf(stderr,
                "LC32: refusing invalid native framework name: %s\n",
                frameworkName.hostPtr);
            return 0;
        }
    }

    static std::mutex cacheMutex;
    static std::unordered_map<std::string, void *> cache;
    const std::string name(frameworkName.hostPtr, length);
    {
        const std::lock_guard<std::mutex> lock(cacheMutex);
        const auto cached = cache.find(name);
        if(cached != cache.end()) return cached->second != nullptr;
    }

    /*
     * LiveExec32's Catalyst build is produced by rewriting the iOS build
     * version, so TARGET_OS_MACCATALYST cannot distinguish it here.  Prefer
     * the iOSSupport image at runtime and fall back to the ordinary iOS path;
     * the first lookup simply fails on a real iOS device.
     */
    const std::string catalystPath =
        "/System/iOSSupport/System/Library/Frameworks/" + name +
        ".framework/" + name;
    void *handle = dlopen(catalystPath.c_str(), RTLD_NOW | RTLD_GLOBAL);
    std::string catalystError;
    if(!handle) {
        const char *error = dlerror();
        if(error) catalystError = error;

        const std::string systemPath = "/System/Library/Frameworks/" + name +
            ".framework/" + name;
        handle = dlopen(systemPath.c_str(), RTLD_NOW | RTLD_GLOBAL);
    }

    {
        /*
         * Do not hold cacheMutex while dlopen runs framework initializers:
         * one of them may synchronously enter another guest framework shim.
         * Concurrent loads are harmless because native image handles remain
         * open for the process lifetime.
         */
        const std::lock_guard<std::mutex> lock(cacheMutex);
        const auto inserted = cache.emplace(name, handle);
        if(!inserted.second) {
            /* If two first-time loads raced, retain a successful result. */
            if(!inserted.first->second && handle) {
                inserted.first->second = handle;
            }
            handle = inserted.first->second;
        }
    }
    if(!handle) {
        const char *systemError = dlerror();
        fprintf(stderr,
            "LC32: failed to load native framework %s: %s%s%s\n",
            name.c_str(),
            catalystError.empty() ? "unknown iOSSupport error" :
                catalystError.c_str(),
            systemError ? "; system fallback: " : "",
            systemError ? systemError : "");
        return 0;
    }

    return 1;
}

/*
 * Metal's public creation function returns a native Objective-C object at
 * +1.  An ARM32 caller cannot consume that 64-bit pointer directly, so keep
 * the ABI-specific ownership conversion beside the generic bridge.  The
 * guest half loads Metal before resolving this thunk; do not cache a failed
 * dlsym lookup in case an older host loads the framework lazily.
 */
extern "C" u32 LC32_Metal_MTLCreateSystemDefaultDevice(void) {
    using CreateDefaultDevice = id (*)(void);
    CreateDefaultDevice createDefaultDevice =
        reinterpret_cast<CreateDefaultDevice>(
            dlsym(RTLD_DEFAULT, "MTLCreateSystemDefaultDevice"));
    if(!createDefaultDevice) return 0;

    id device = createDefaultDevice();
    return device ? LC32GuestObjectForOwnedHostObject((CFTypeRef)device) : 0;
}

inline id LC32GetHostConstString(u32 guest_self) {
    // Construct a __NSCFConstantString { isa, flags, buffer, length }
    u64 *constStr = (u64 *)malloc(sizeof(u64[4]));
    constStr[0] = (u64)__CFConstantStringClassReference;
    constStr[1] = (u64)Dynarmic_current_user_callbacks()->MemoryRead32(guest_self + sizeof(u32[1]));
    u64 length = (u64)Dynarmic_current_user_callbacks()->MemoryRead32(guest_self + sizeof(u32[3]));
    /* CFString's 0x10 flag denotes UTF-16 storage. Its length still counts
     * code units, whereas the guest memory wrapper takes bytes. Copying only
     * `length` bytes truncates multi-page Unicode literals and lets native
     * Foundation read beyond the detached allocation. */
    constexpr u32 CFStringUnicodeFlag = 0x10;
    const u64 byteLength = length *
        ((constStr[1] & CFStringUnicodeFlag) ? sizeof(UniChar) : 1);
    if(byteLength > UINT32_MAX) abort();
    DynarmicHostString host_str(Dynarmic_current_user_callbacks()->MemoryRead32(guest_self + sizeof(u32[2])), (u32)byteLength);
    /*
     * A cross-page copy is detached for this immortal native constant.  The
     * bit-63 ownership marker is meaningful only while returning the pointer
     * to guest code for a later SVC 1004; it is never part of a host address.
     */
    constStr[2] = (u64)host_str.hostPtrForGuest() &
        ~DynarmicHostString_NEED_FREE;
    constStr[3] = length;
    return (id)constStr;
}

static id LC32AllocateGuestClassMirror(Class cls) {
    /*
     * A synthesized guest class may override +allocWithZone: with a guest
     * trampoline.  Dispatching +alloc normally would enter that trampoline,
     * which asks for this same host mirror and recurses.  Raw
     * class_createInstance avoids the recursion, but it also bypasses native
     * framework allocators.  UIKit relies on its allocator to prepare UIView
     * subclasses before -initWithFrame: starts messaging them.
     *
     * Invoke the first native superclass's allocator implementation directly,
     * while keeping the synthesized class as the receiver.  That preserves
     * native allocation setup and the mirror's dynamic class without
     * dispatching through any guest class method.
     */
    Class nativeSuperclass = class_getSuperclass(cls);
    while(nativeSuperclass && [(id)nativeSuperclass isGuestClass]) {
        nativeSuperclass = class_getSuperclass(nativeSuperclass);
    }
    if(!nativeSuperclass) {
        fprintf(stderr,
            "LC32: guest class %s has no native superclass allocator\n",
            class_getName(cls));
        return nil;
    }

    const SEL selector = @selector(allocWithZone:);
    Method method = class_getClassMethod(nativeSuperclass, selector);
    if(!method) {
        fprintf(stderr,
            "LC32: native superclass %s has no +allocWithZone: for %s\n",
            class_getName(nativeSuperclass), class_getName(cls));
        return nil;
    }

    using Allocator = id (*)(id, SEL, NSZone *);
    Allocator allocator = reinterpret_cast<Allocator>(
        method_getImplementation(method));
    id object = allocator((id)cls, selector, nullptr);
    if(!object) {
        fprintf(stderr,
            "LC32: native allocator returned nil for guest class %s\n",
            class_getName(cls));
        return nil;
    }

    Class actualClass = object_getClass(object);
    if(actualClass != cls) {
        fprintf(stderr,
            "LC32: native allocator for %s returned unexpected class %s\n",
            class_getName(cls),
            actualClass ? class_getName(actualClass) : "<nil>");
        LC32ReleaseOwnedHostObject(object);
        return nil;
    }
    return object;
}

u64 LC32GetHostObject(u32 guest_self, u32 guest_className, bool returnClass) {
    DynarmicHostString host_className(guest_className);
    Class cls = objc_getClass(host_className.hostPtr);
    if(returnClass) {
        [(id)cls setGuest_self:guest_self];
        return (u64)cls;
    }

    const bool isGuestClass = [(id)cls isGuestClass];
    const bool isConstantStringClass =
        object_getClass(cls) ==
            object_getClass((Class)__CFConstantStringClassReference);
    id obj;
    if(isConstantStringClass) {
        obj = LC32GetHostConstString(guest_self);
    } else if(isGuestClass) {
        obj = LC32AllocateGuestClassMirror(cls);
    } else {
        obj = [cls alloc];
    }
    [obj setGuest_self:guest_self];
    LC32OperationTraceLiveObject(
        "guest-created-host-peer", obj, guest_self);
    if(isGuestClass) {
        // Dynamic guest classes have unique native allocations but no native
        // initializer shim where the final mirror can be pinned.
        LC32PinGuestObjectToHost(obj, guest_self, true);
    }
    return (u64)obj;
}

namespace {
void LC32RegisterGuestSelectorMapping(
    SEL hostSelector, u32 guestSelector);
u32 LC32LookupGuestSelectorMapping(SEL hostSelector);
}

u64 LC32GetHostSelector(u32 guest_selector) {
    DynarmicHostString host_selector(guest_selector);
    SEL selector = sel_registerName(host_selector.hostPtr);
    LC32RegisterGuestSelectorMapping(selector, guest_selector);
    return (u64)selector;
}

static bool LC32NativeNSRangeType(const char *type) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    if(!type ||
       (strncmp(type, "{_NSRange=", sizeof("{_NSRange=") - 1) &&
        strncmp(type, "{NSRange=", sizeof("{NSRange=") - 1))) {
        return false;
    }
    const char *fields = strchr(type, '=');
    return fields && fields[1] == 'Q' && fields[2] == 'Q' &&
        fields[3] == '}' && fields[4] == '\0';
}

static bool LC32NativeNSDecimalType(const char *type) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    return type &&
        strcmp(type, "{?=b8b4b1b1b18[8S]}") == 0;
}

static bool LC32SelectorUsesHostStackVarargs(SEL selector) {
    if(!selector) return false;
    const char *name = sel_getName(selector);
    if(!name) return false;

    /*
     * Darwin's ARM64 ABI puts unnamed variadic arguments on the stack even
     * when integer argument registers remain unused.  Objective-C method
     * encodings do not record the trailing ellipsis, so keep the small set of
     * framework entry points which still cross this bridge explicitly.  The
     * Foundation collection/string variants are implemented in guest code
     * and therefore do not normally reach here, but recognizing the
     * collection selectors makes the fallback path safe as well.
     */
    return !strcmp(name,
               "initWithTitle:message:delegate:cancelButtonTitle:"
               "otherButtonTitles:") ||
        !strcmp(name,
               "initWithTitle:delegate:cancelButtonTitle:"
               "destructiveButtonTitle:otherButtonTitles:") ||
        !strcmp(name, "arrayWithObjects:") ||
        !strcmp(name, "initWithObjects:") ||
        !strcmp(name, "setWithObjects:") ||
        !strcmp(name, "orderedSetWithObjects:") ||
        !strcmp(name, "dictionaryWithObjectsAndKeys:") ||
        !strcmp(name, "initWithObjectsAndKeys:") ||
        !strcmp(name, "appearanceWhenContainedIn:") ||
        !strcmp(name, "appearanceForTraitCollection:whenContainedIn:");
}

static bool LC32SelectorIsInInitializerFamily(SEL selector) {
    const char *name = selector ? sel_getName(selector) : nullptr;
    if(!name) return false;
    while(*name == '_') name++;

    static constexpr char family[] = "init";
    if(strncmp(name, family, sizeof(family) - 1) != 0) return false;
    const unsigned char next =
        static_cast<unsigned char>(name[sizeof(family) - 1]);
    return next == '\0' || next < 'a' || next > 'z';
}

// guest to host call of objc_msgSend*
u64 LC32InvokeHostSelector(u64 host_self, u64 host_cmd, u64 va_args) {
    /* Shadow any initializer which synchronously called guest code for this
     * entire nested bridge operation—not just its native dispatch. Receiver
     * acquisition, argument/result conversion, and guard destruction can all
     * perform ownership work which belongs to this call rather than the outer
     * initializer. Declaring this first keeps the boundary alive until every
     * later local has unwound. */
    LC32HostInitializerInvocationScope invocationBoundary(nil);

    // ARMv7 stores parameters in r0-r3 and stack pointer. r0-r3 is already reserved for self and cmd, so we read the rest from stack pointer

    u32 structPtr = 0, structLen;
    const bool returnGuestObject =
        (host_cmd & SEL_RETURN_GUEST_OBJECT) != 0;
    const bool allowUnmappedReceiver =
        (host_cmd & SEL_ALLOW_UNMAPPED_RECEIVER) != 0;
    if(returnGuestObject && (host_cmd & SEL_RETURN_STRUCT)) {
        fprintf(stderr,
            "LC32: selector cannot request both struct and guest-object "
            "returns\n");
        return 0;
    }
    if(host_cmd & SEL_RETURN_STRUCT) {
        host_cmd &= ~SEL_RETURN_STRUCT;
        structPtr = Dynarmic_current_user_callbacks()->MemoryRead32(va_args);
        structLen = Dynarmic_current_user_callbacks()->MemoryRead32(va_args += sizeof(u32));
        va_args += sizeof(u32);
    }
    host_cmd &= ~SEL_RETURN_GUEST_OBJECT;
    host_cmd &= ~SEL_ALLOW_UNMAPPED_RECEIVER;

    // FIXME: how to read number of args for variadic methods and translate its values?
    u64 args[9] = {
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64)),
        Dynarmic_current_user_callbacks()->MemoryRead64(va_args += sizeof(u64))
    };

    /*
     * A guest pointer cannot be handed to an ARM64 Objective-C method. Shim
     * sources tag pointers to 64-bit guest temporaries; substitute native
     * stack storage for objc_msgSend and copy the result back afterwards.
     * Looking at the method encoding prevents a negative scalar or floating
     * bit pattern from ever being mistaken for an indirect argument.
     */
    u32 indirectGuestStorage[9] = {};
    u64 indirectHostStorage[9] = {};
    u32 sizedIndirectGuestStorage[9] = {};
    u32 sizedIndirectSize[9] = {};
    alignas(16) std::array<u8,
        LC32_HOST_SIZED_INDIRECT_MAX_SIZE> sizedIndirectHostStorage[9] = {};
    enum class LC32FloatingIndirectType : u8 {
        None,
        Float,
        Double,
    };
    LC32FloatingIndirectType floatingIndirectType[9] = {};
    float floatingIndirectFloatStorage[9] = {};
    double floatingIndirectDoubleStorage[9] = {};
    alignas(16) std::array<u8, 64> aggregateHostStorage[9] = {};
    size_t aggregateHostSize[9] = {};
    alignas(16) std::array<u8, 16> invocationHostStorage[9] = {};
    std::unique_ptr<u64[]> objectArrayHostStorage[9];
    SEL selector = (SEL)host_cmd;
    LC32HostInvocationReceiverGuard receiverGuard;
    id receiver = nil;
    if(!LC32AcquireHostInvocationReceiver(
            host_self, receiverGuard, &receiver,
            allowUnmappedReceiver)) {
        fprintf(stderr,
            "LC32: refusing to invoke %s on unregistered or dead "
            "receiver 0x%llx\n",
            selector ? sel_getName(selector) : "<null selector>",
            (unsigned long long)host_self);
        return 0;
    }
    /*
     * Diagnostics may inspect the native retain count. Keep them after the
     * registry's weak promotion so a stale receiver is never dereferenced by
     * tracing before the dispatch path has rejected it.
    */
    LC32OperationTraceRawSelector(receiver, selector, args[0]);
    if(returnGuestObject && selector == @selector(view)) {
        id loadedView = nil;
        if(LC32UIKitGetViewDuringGuestLoad(receiver, &loadedView)) {
            /* Asking for self.view from inside a guest -loadView must observe
             * the view currently installed by that override, without starting
             * UIViewController's native lazy loader a second time. */
            return loadedView
                ? LC32GuestObjectForBorrowedHostResult(loadedView) : 0;
        }
    }
    Class dispatchClass = object_getClass(receiver);
    const bool invokeSuper = [(id)dispatchClass isGuestClass];
    if(invokeSuper && !class_isMetaClass(dispatchClass)) {
        /*
         * The receiver guard acquired the mirror before its isa was read and
         * keeps it alive across the complete native dispatch and result
         * conversion interval. Verify that an initializer or runtime hook did
         * not change its dynamic class during acquisition.
         */
        if(object_getClass(receiver) != dispatchClass) {
            fprintf(stderr,
                "LC32: guest mirror %p changed class before %s\n",
                receiver, sel_getName(selector));
            return 0;
        }
    }
    if(invokeSuper && selector == @selector(retain)) {
        Method retainMethod = class_getInstanceMethod(
            dispatchClass, selector);
        if(retainMethod && method_getImplementation(retainMethod) ==
                (IMP)&LC32GuestMirrorRetain) {
            /* objc_msgSendSuper to NSObject's retain does not reliably add a
             * root reference when the receiver's class implements custom RR.
             * This is the native half of an ordinary guest retain, so invoke
             * the synthesized mirror's exact implementation directly. */
            return (u64)(uintptr_t)LC32GuestMirrorRetain(
                receiver, selector);
        }
    }
    if(invokeSuper && selector == @selector(release)) {
        Method releaseMethod = class_getInstanceMethod(
            dispatchClass, selector);
        if(releaseMethod && method_getImplementation(releaseMethod) ==
                (IMP)&LC32GuestMirrorRelease) {
            /* Ordinary guest-to-host sends deliberately start at the first
             * native superclass, avoiding recursion into guest methods. The
             * synthesized mirror's release override is host-only, however,
             * and must coordinate its final native +1 with the guest lifetime
             * pin while the mirror is still alive. */
            /* This SVC owns the +1 paired with the non-final guest ownership
             * being released. When receiverGuard also owns a weak-promotion +1,
             * transfer it into the coordinated release instead of first leaving
             * it as the sole Live native reference and releasing it again from
             * the guard destructor. Consuming the available references beneath
             * the release mutex makes finality one serialized decision. */
            const unsigned ownedReferenceCount =
                receiverGuard.relinquishIfEqual(receiver) ? 2 : 1;
            LC32GuestHostCallQuiescence quiescence;
            LC32GuestMirrorReleaseImplementation(
                receiver, ownedReferenceCount);
            quiescence.finish();
            return 0;
        }
    }
    if(invokeSuper) {
        do {
            dispatchClass = class_getSuperclass(dispatchClass);
        } while(dispatchClass && [(id)dispatchClass isGuestClass]);
    }
    Method method = dispatchClass
        ? class_getInstanceMethod(dispatchClass, selector)
        : nullptr;
    if(!method && invokeSuper) {
        /*
         * A guest-backed mirror forwarded this message to the host, but the
         * host class chain has no implementation for it.  The guest framework
         * shims (e.g. the iOS-10 UITableView) carry delegate-callback methods
         * such as scrollViewDidEndDecelerating: which the newer host UIKit
         * refactored away, so the mirror legitimately responds to them while
         * the host dispatch finds nothing.  Real iOS would run the class's
         * own inherited implementation; the correct emulation is a no-op, not
         * raising "unrecognized selector" through the host forwarder.
         */
        printf("LC32: host dispatch for %s on guest-mirror %s has no "
               "implementation; ignoring\n",
               sel_getName(selector),
               receiver ? class_getName(object_getClass(receiver)) : "<nil>");
        return 0;
    }
    /* A native object may implement a selector through Objective-C message
     * forwarding without installing a concrete Method. GameKit uses this
     * for achievement properties, including double-valued accessors. Their
     * forwarding signature is still authoritative for the native ABI; an
     * integer-only fallback would read x0 instead of the returned d0.
     *
     * Keep ordinary Method lookups on their existing fast path. Only query
     * native receivers after the missing guest-super case above has exited:
     * a mirror's signature can describe ARM32 guest code or reenter it.
     * Hold the returned signature for this invocation, not in a global cache,
     * since forwarding can depend on the individual receiver's current state. */
    LC32HostInvocationReceiverGuard forwardingSignatureGuard;
    NSMethodSignature *forwardingSignature = nil;
    if(!method && receiver) {
        LC32GuestHostCallQuiescence quiescence;
        forwardingSignature = [receiver methodSignatureForSelector:selector];
        forwardingSignatureGuard.acquireUnmapped(forwardingSignature);
    }
    const NSUInteger methodArgumentCount = method
        ? method_getNumberOfArguments(method)
        : [forwardingSignature numberOfArguments];
    const bool hasMethodSignature =
        (method || forwardingSignature) && methodArgumentCount >= 2;
    auto copyArgumentType = [&](NSUInteger index) -> char * {
        if(method) return method_copyArgumentType(method, (unsigned int)index);
        if(!forwardingSignature || index >= methodArgumentCount) return nullptr;
        const char *type =
            [forwardingSignature getArgumentTypeAtIndex:index];
        return type ? strdup(type) : nullptr;
    };
    auto copyReturnType = [&]() -> char * {
        if(method) return method_copyReturnType(method);
        const char *type = [forwardingSignature methodReturnType];
        return type ? strdup(type) : nullptr;
    };

    if(hasMethodSignature) {
        const unsigned int argumentCount =
            (unsigned int)MIN(methodArgumentCount - 2, (NSUInteger)9);
        for(unsigned int index = 0; index < argumentCount; index++) {
            char *argumentType = copyArgumentType(index + 2);
            if(!argumentType) continue;
            const char *unqualifiedType = argumentType;
            while(*unqualifiedType &&
                    strchr("rnNoORVA", *unqualifiedType)) {
                unqualifiedType++;
            }
            if(unqualifiedType[0] == '@' &&
               unqualifiedType[1] != '?') {
                LC32TraceNativeNetworkObject(
                    "guest->host", selector, index, (id)args[index]);
            }
            if(unqualifiedType[0] == '@' &&
                    LC32BlockArgumentTraceEnabled()) {
                id nativeBlock = (id)args[index];
                const char *argumentClass = nativeBlock
                    ? class_getName(object_getClass(nativeBlock))
                    : nullptr;
                const bool isNativeBlock =
                    unqualifiedType[1] == '?' ||
                    (argumentClass && strstr(argumentClass, "Block"));
                if(isNativeBlock) {
                    fprintf(stderr,
                        "LC32 block argument: -[%s %s] index=%u "
                        "declared=%s native=%p class=%s guest=0x%08x\n",
                        receiver
                            ? class_getName(object_getClass(receiver))
                            : "(null)",
                        sel_getName(selector), index, unqualifiedType,
                        nativeBlock, argumentClass ?: "(null)",
                        nativeBlock ? [nativeBlock guest_selfOrNull] : 0);
                    fflush(stderr);
                }
            }
            /*
             * LC32GuestToHostCString marks heap-backed cross-page strings in
             * bit 63 so the guest can release them after this synchronous
             * call. That ownership bit is not part of the native address.
             */
            if(*unqualifiedType == '*' &&
               (args[index] & DynarmicHostString_NEED_FREE)) {
                args[index] &= ~DynarmicHostString_NEED_FREE;
            }
            const u64 argumentTag =
                args[index] & LC32_GUEST_ARGUMENT_TAG_MASK;
            const char *unqualifiedPointeeType = nullptr;
            if(*unqualifiedType == '^') {
                unqualifiedPointeeType = unqualifiedType + 1;
                while(*unqualifiedPointeeType &&
                        strchr("rnNoORVA", *unqualifiedPointeeType)) {
                    unqualifiedPointeeType++;
                }
            }
            const bool isTaggedPointer =
                *unqualifiedType == '^' &&
                argumentTag == LC32_GUEST_INDIRECT_ARGUMENT_TAG &&
                (u32)args[index] != 0;
            const bool isTaggedFloatingPointer =
                unqualifiedPointeeType &&
                (*unqualifiedPointeeType == 'f' ||
                 *unqualifiedPointeeType == 'd') &&
                argumentTag ==
                    LC32_GUEST_FLOATING_INDIRECT_ARGUMENT_TAG &&
                (u32)args[index] != 0;
            const bool isTaggedSizedPointer =
                *unqualifiedType == '^' &&
                argumentTag ==
                    LC32_GUEST_SIZED_INDIRECT_ARGUMENT_TAG &&
                (u32)args[index] != 0;
            const bool isTaggedObjectArray =
                unqualifiedType[0] == '^' &&
                unqualifiedType[1] == '@' &&
                argumentTag == LC32_GUEST_OBJECT_ARRAY_ARGUMENT_TAG &&
                (u32)args[index] != 0;
            const bool isTaggedAggregate =
                unqualifiedType[0] == '{' &&
                argumentTag == LC32_GUEST_AGGREGATE_ARGUMENT_TAG &&
                (u32)args[index] != 0;
            const bool isTaggedInvocationArgument =
                unqualifiedType[0] == '^' &&
                argumentTag == LC32_GUEST_INVOCATION_ARGUMENT_TAG &&
                (u32)args[index] != 0;
            if(argumentTag == LC32_GUEST_INVOCATION_ARGUMENT_TAG) {
                const bool validInvocationArgument =
                    isTaggedInvocationArgument && index == 0 &&
                    selector == @selector(setArgument:atIndex:) &&
                    [receiver isKindOfClass:NSInvocation.class];
                const int32_t invocationIndex = (int32_t)(u32)args[1];
                if(!validInvocationArgument ||
                   !LC32PrepareHostInvocationArgument(
                       (NSInvocation *)receiver, (u32)args[index],
                       invocationIndex, invocationHostStorage[index])) {
                    printf("LC32: invalid NSInvocation argument %d for %s\n",
                           invocationIndex, sel_getName(selector));
                    free(argumentType);
                    return 0;
                }
                args[index] =
                    (u64)invocationHostStorage[index].data();
                free(argumentType);
                continue;
            }
            if(argumentTag == LC32_GUEST_OBJECT_ARRAY_ARGUMENT_TAG &&
               !isTaggedObjectArray) {
                printf("LC32: refusing object-array argument %u for "
                       "non-object-pointer selector %s\n", index,
                       sel_getName(selector));
                free(argumentType);
                return 0;
            }
            if(argumentTag == LC32_GUEST_INDIRECT_ARGUMENT_TAG &&
               !isTaggedPointer) {
                printf("LC32: refusing indirect argument %u for "
                       "non-pointer selector %s\n", index,
                       sel_getName(selector));
                free(argumentType);
                return 0;
            }
            if(argumentTag ==
                    LC32_GUEST_FLOATING_INDIRECT_ARGUMENT_TAG &&
               !isTaggedFloatingPointer) {
                printf("LC32: refusing floating-indirect argument %u for "
                       "non-floating-pointer selector %s\n", index,
                       sel_getName(selector));
                free(argumentType);
                return 0;
            }
            if(argumentTag ==
                    LC32_GUEST_SIZED_INDIRECT_ARGUMENT_TAG &&
               !isTaggedSizedPointer) {
                printf("LC32: refusing sized-indirect argument %u for "
                       "non-pointer selector %s\n", index,
                       sel_getName(selector));
                free(argumentType);
                return 0;
            }
            if(argumentTag == LC32_GUEST_AGGREGATE_ARGUMENT_TAG &&
               !isTaggedAggregate) {
                printf("LC32: refusing aggregate argument %u for "
                       "non-aggregate selector %s\n", index,
                       sel_getName(selector));
                free(argumentType);
                return 0;
            }
            if(isTaggedObjectArray) {
                const u32 guestStorage = (u32)args[index];
                LC32HostObjectArrayDescriptor descriptor = {};
                const int selectorCountArgumentIndex =
                    LC32UniqueSelectorArgumentIndexNamed(selector, "count");
                if(Dynarmic_mem_1read(guestStorage, sizeof(descriptor),
                        reinterpret_cast<char *>(&descriptor)) != 0 ||
                   descriptor.magic != LC32_HOST_OBJECT_ARRAY_MAGIC ||
                   descriptor.reserved != 0 ||
                   descriptor.count > LC32_HOST_OBJECT_ARRAY_MAX_COUNT ||
                   selectorCountArgumentIndex < 0 ||
                   descriptor.countArgumentIndex !=
                       (u32)selectorCountArgumentIndex ||
                   descriptor.countArgumentIndex >= argumentCount ||
                   descriptor.countArgumentIndex == index) {
                    printf("LC32: invalid object-array descriptor for "
                           "argument %u of %s\n", index,
                           sel_getName(selector));
                    free(argumentType);
                    return 0;
                }

                char *countType = copyArgumentType(
                    descriptor.countArgumentIndex + 2);
                const char *unqualifiedCountType = countType;
                while(unqualifiedCountType && *unqualifiedCountType &&
                        strchr("rnNoORVA", *unqualifiedCountType)) {
                    unqualifiedCountType++;
                }
                const bool validCountType = unqualifiedCountType &&
                    strchr("CILQS", *unqualifiedCountType) != nullptr;
                free(countType);
                if(!validCountType ||
                   args[descriptor.countArgumentIndex] != descriptor.count) {
                    printf("LC32: mismatched object-array count for "
                           "argument %u of %s\n", index,
                           sel_getName(selector));
                    free(argumentType);
                    return 0;
                }

                const u64 objectBytes =
                    (u64)descriptor.count * sizeof(u64);
                const u64 objectAddress =
                    (u64)guestStorage + sizeof(descriptor);
                if(objectAddress + objectBytes > UINT64_C(0x100000000)) {
                    printf("LC32: overflowing object-array descriptor for "
                           "argument %u of %s\n", index,
                           sel_getName(selector));
                    free(argumentType);
                    return 0;
                }

                if(descriptor.count) {
                    objectArrayHostStorage[index].reset(
                        new(std::nothrow) u64[descriptor.count]);
                    if(!objectArrayHostStorage[index]) {
                        printf("LC32: cannot allocate object-array argument "
                               "%u of %s\n", index,
                               sel_getName(selector));
                        free(argumentType);
                        return 0;
                    }
                }
                if(objectBytes && Dynarmic_mem_1read(
                        (u32)objectAddress, (size_t)objectBytes,
                        reinterpret_cast<char *>(
                            objectArrayHostStorage[index].get())) != 0) {
                    printf("LC32: unreadable object-array descriptor for "
                           "argument %u of %s\n", index,
                           sel_getName(selector));
                    free(argumentType);
                    return 0;
                }
                args[index] = descriptor.count
                    ? (u64)objectArrayHostStorage[index].get()
                    : 0;
                free(argumentType);
                continue;
            }
            if(isTaggedAggregate) {
                NSUInteger nativeSize = 0;
                NSUInteger nativeAlignment = 0;
                if(LC32NativeNSDecimalType(unqualifiedType)) {
                    static_assert(sizeof(NSDecimal) == 20,
                        "unexpected native NSDecimal layout");
                    static_assert(alignof(NSDecimal) == 4,
                        "unexpected native NSDecimal alignment");
                    nativeSize = sizeof(NSDecimal);
                    nativeAlignment = alignof(NSDecimal);
                } else {
                    NSGetSizeAndAlignment(unqualifiedType, &nativeSize,
                                          &nativeAlignment);
                }
                if(!nativeSize || nativeSize >
                        aggregateHostStorage[index].size() ||
                   Dynarmic_mem_1read((u32)args[index], nativeSize,
                       reinterpret_cast<char *>(
                           aggregateHostStorage[index].data())) != 0) {
                    printf("LC32: invalid aggregate argument %u of %s "
                           "(size=%lu alignment=%lu)\n", index,
                           sel_getName(selector), (unsigned long)nativeSize,
                           (unsigned long)nativeAlignment);
                    free(argumentType);
                    return 0;
                }
                aggregateHostSize[index] = nativeSize;
                free(argumentType);
                continue;
            }
            if(isTaggedSizedPointer) {
                LC32HostSizedIndirectDescriptor descriptor = {};
                const u32 descriptorAddress = (u32)args[index];
                const bool validDescriptor =
                    Dynarmic_mem_1read(descriptorAddress,
                        sizeof(descriptor), reinterpret_cast<char *>(
                            &descriptor)) == 0 &&
                    descriptor.magic == LC32_HOST_SIZED_INDIRECT_MAGIC &&
                    descriptor.reserved == 0 && descriptor.storage != 0 &&
                    descriptor.size != 0 &&
                    descriptor.size <=
                        LC32_HOST_SIZED_INDIRECT_MAX_SIZE &&
                    (u64)descriptor.storage + descriptor.size <=
                        UINT64_C(0x100000000) &&
                    Dynarmic_mem_1read(descriptor.storage, descriptor.size,
                        reinterpret_cast<char *>(
                            sizedIndirectHostStorage[index].data())) == 0;
                if(!validDescriptor) {
                    printf("LC32: invalid sized-indirect argument %u of %s\n",
                           index, sel_getName(selector));
                    free(argumentType);
                    return 0;
                }
                sizedIndirectGuestStorage[index] = descriptor.storage;
                sizedIndirectSize[index] = descriptor.size;
                args[index] = (u64)sizedIndirectHostStorage[index].data();
                free(argumentType);
                continue;
            }
            if(isTaggedFloatingPointer) {
                const u32 guestStorage = (u32)args[index];
                double canonicalValue = 0.0;
                args[index] = 0;
                if(Dynarmic_mem_1read(
                        guestStorage, sizeof(canonicalValue),
                        reinterpret_cast<char *>(&canonicalValue)) != 0) {
                    free(argumentType);
                    continue;
                }
                indirectGuestStorage[index] = guestStorage;
                if(*unqualifiedPointeeType == 'f') {
                    floatingIndirectType[index] =
                        LC32FloatingIndirectType::Float;
                    floatingIndirectFloatStorage[index] =
                        (float)canonicalValue;
                    args[index] =
                        (u64)&floatingIndirectFloatStorage[index];
                } else {
                    floatingIndirectType[index] =
                        LC32FloatingIndirectType::Double;
                    floatingIndirectDoubleStorage[index] = canonicalValue;
                    args[index] =
                        (u64)&floatingIndirectDoubleStorage[index];
                }
                free(argumentType);
                continue;
            }
            free(argumentType);
            if(!isTaggedPointer) continue;

            const u32 guestStorage = (u32)args[index];
            args[index] = 0;
            if(!guestStorage || Dynarmic_mem_1read(
                    guestStorage, sizeof(indirectHostStorage[index]),
                    reinterpret_cast<char *>(
                        &indirectHostStorage[index])) != 0) {
                continue;
            }
            indirectGuestStorage[index] = guestStorage;
            args[index] = (u64)&indirectHostStorage[index];
        }
    } else {
        for(size_t index = 0; index < 9; index++) {
            const u64 argumentTag =
                args[index] & LC32_GUEST_ARGUMENT_TAG_MASK;
            if((argumentTag != LC32_GUEST_INDIRECT_ARGUMENT_TAG &&
                argumentTag !=
                    LC32_GUEST_FLOATING_INDIRECT_ARGUMENT_TAG &&
                argumentTag !=
                    LC32_GUEST_SIZED_INDIRECT_ARGUMENT_TAG &&
                argumentTag != LC32_GUEST_OBJECT_ARRAY_ARGUMENT_TAG &&
                argumentTag != LC32_GUEST_AGGREGATE_ARGUMENT_TAG &&
                argumentTag != LC32_GUEST_INVOCATION_ARGUMENT_TAG) ||
               !(u32)args[index]) {
                continue;
            }
            printf("LC32: cannot marshal pointer argument %zu for "
                   "unresolved selector %s\n", index,
                   sel_getName(selector));
            return 0;
        }
    }

    auto finishIndirectArguments = [&](u64 result) -> u64 {
        /* Legacy controller overlays can apply portrait-window geometry in
         * several consecutive setters. Let UIKit reconcile it after the
         * complete guest operation, not between transform/bounds/center. */
        if(selector == @selector(setTransform:) ||
                selector == @selector(setBounds:) ||
                selector == @selector(setCenter:) ||
                selector == @selector(setFrame:)) {
            LC32UIKitScheduleLegacyOverlayLayout(receiver, nil);
        } else if(selector == @selector(addSubview:)) {
            LC32UIKitScheduleLegacyOverlayLayout(
                receiver, (id)(uintptr_t)args[0]);
        } else if(selector == @selector(setAutoresizingMask:)) {
            LC32UIKitDidSetGuestAutoresizingMask(receiver);
        }
        for(size_t index = 0; index < 9; index++) {
            if(sizedIndirectGuestStorage[index]) {
                (void)Dynarmic_mem_1write(
                    sizedIndirectGuestStorage[index],
                    sizedIndirectSize[index],
                    reinterpret_cast<char *>(
                        sizedIndirectHostStorage[index].data()));
                continue;
            }
            if(!indirectGuestStorage[index]) continue;
            if(floatingIndirectType[index] ==
                    LC32FloatingIndirectType::Float) {
                floatingIndirectDoubleStorage[index] =
                    (double)floatingIndirectFloatStorage[index];
                (void)Dynarmic_mem_1write(
                    indirectGuestStorage[index],
                    sizeof(floatingIndirectDoubleStorage[index]),
                    reinterpret_cast<char *>(
                        &floatingIndirectDoubleStorage[index]));
                continue;
            }
            if(floatingIndirectType[index] ==
                    LC32FloatingIndirectType::Double) {
                (void)Dynarmic_mem_1write(
                    indirectGuestStorage[index],
                    sizeof(floatingIndirectDoubleStorage[index]),
                    reinterpret_cast<char *>(
                        &floatingIndirectDoubleStorage[index]));
                continue;
            }
            (void)Dynarmic_mem_1write(
                indirectGuestStorage[index],
                sizeof(indirectHostStorage[index]),
                reinterpret_cast<char *>(
                    &indirectHostStorage[index]));
        }
        return result;
    };

    /*
     * AAPCS64 allocates scalar floating-point arguments and integer/pointer
     * arguments from independent register banks. The guest shim transports
     * each logical scalar in one 64-bit stack slot, so compact those slots by
     * host type before entering objc_msgSend. In particular, a leading
     * double belongs in d0 and must not also consume x2.
     *
     * Generated float arguments are promoted to double by the variadic guest
     * call. Convert them back to float and place their bits in the low half
     * of the corresponding v register. Known UIKit/CoreGraphics aggregates
     * are transported as one tagged slot. Two- and four-double HFAs occupy
     * d-registers; the six-double CGAffineTransform is passed indirectly.
     */
    u64 integerArguments[9] = {};
    u64 floatingArguments[8] = {};
    bool useTypedScalarArguments = hasMethodSignature;
    if(hasMethodSignature) {
        const NSUInteger argumentCount = methodArgumentCount - 2;
        size_t integerArgumentCount = 0;
        size_t floatingArgumentCount = 0;
        if(argumentCount > 9) useTypedScalarArguments = false;

        for(unsigned int index = 0;
                useTypedScalarArguments && index < argumentCount; index++) {
            char *argumentType = copyArgumentType(index + 2);
            const char *unqualifiedType = argumentType;
            while(unqualifiedType && *unqualifiedType &&
                    strchr("rnNoORVA", *unqualifiedType)) {
                unqualifiedType++;
            }

            switch(unqualifiedType ? *unqualifiedType : '\0') {
                case 'f': {
                    if(floatingArgumentCount >= 8) {
                        useTypedScalarArguments = false;
                        break;
                    }
                    double promotedValue;
                    memcpy(&promotedValue, &args[index],
                           sizeof(promotedValue));
                    const float hostValue = (float)promotedValue;
                    u32 hostBits;
                    memcpy(&hostBits, &hostValue, sizeof(hostBits));
                    floatingArguments[floatingArgumentCount++] = hostBits;
                    break;
                }
                case 'd':
                    if(floatingArgumentCount >= 8) {
                        useTypedScalarArguments = false;
                        break;
                    }
                    floatingArguments[floatingArgumentCount++] = args[index];
                    break;
                case '{': {
                    const size_t nativeSize = aggregateHostSize[index];
                    if(!nativeSize) {
                        useTypedScalarArguments = false;
                        break;
                    }
                    if(LC32NativeNSRangeType(unqualifiedType)) {
                        if(nativeSize != sizeof(NSRange)) {
                            useTypedScalarArguments = false;
                            break;
                        }
                        /*
                         * AAPCS64 does not split a composite between x7 and
                         * the stack.  Leave the last register unused when
                         * only one of the two NSUInteger slots still fits.
                         */
                        if(integerArgumentCount == 5)
                            integerArgumentCount = 6;
                        if(integerArgumentCount + 2 > 9) {
                            useTypedScalarArguments = false;
                            break;
                        }
                        NSRange range;
                        memcpy(&range,
                               aggregateHostStorage[index].data(),
                               sizeof(range));
                        integerArguments[integerArgumentCount++] =
                            (u64)range.location;
                        integerArguments[integerArgumentCount++] =
                            (u64)range.length;
                        break;
                    }
                    if(nativeSize == sizeof(double) * 2 ||
                       nativeSize == sizeof(double) * 4) {
                        const size_t elementCount =
                            nativeSize / sizeof(double);
                        if(floatingArgumentCount + elementCount > 8) {
                            /* AAPCS64 spills the whole HFA when it does not
                             * fit. The fixed trampoline has no typed FP stack
                             * path yet, so reject instead of corrupting it. */
                            useTypedScalarArguments = false;
                            break;
                        }
                        for(size_t element = 0; element < elementCount;
                                element++) {
                            memcpy(&floatingArguments[
                                       floatingArgumentCount++],
                                   aggregateHostStorage[index].data() +
                                       element * sizeof(double),
                                   sizeof(double));
                        }
                        break;
                    }
                    if(nativeSize > 16) {
                        integerArguments[integerArgumentCount++] =
                            (u64)aggregateHostStorage[index].data();
                        break;
                    }
                    useTypedScalarArguments = false;
                    break;
                }
                case '@':
                case '#':
                case ':':
                case '*':
                case '^':
                case '?':
                case 'B':
                case 'C':
                case 'I':
                case 'L':
                case 'Q':
                case 'S':
                case 'b':
                case 'c':
                case 'i':
                case 'l':
                case 'q':
                case 's':
                    integerArguments[integerArgumentCount++] = args[index];
                    break;
                default:
                    useTypedScalarArguments = false;
                    break;
            }
            free(argumentType);
        }
        if(useTypedScalarArguments) {
            /*
             * Objective-C metadata describes only the fixed portion of a
             * variadic method. Preserve the shim's remaining raw slots (and
             * its explicit zero terminator) after the typed fixed arguments.
             * On Darwin ARM64, unnamed variadic arguments start on the stack,
             * not in unused x registers.  The fixed trampoline below places
             * integerArguments[0...5] in x2...x7 and begins its stack payload
             * at integerArguments[6], so leave the unused register slots
             * empty for the variadic selectors known to cross this bridge.
             */
            if(LC32SelectorUsesHostStackVarargs(selector) &&
               integerArgumentCount < 6) {
                integerArgumentCount = 6;
            }
            for(size_t index = argumentCount;
                    index < 9 && integerArgumentCount < 9; index++) {
                integerArguments[integerArgumentCount++] = args[index];
            }
        }
    }
    if(!useTypedScalarArguments) {
        memcpy(integerArguments, args, sizeof(integerArguments));
        memcpy(floatingArguments, args, sizeof(floatingArguments));
    }

    enum class HostReturnKind {
        Integer,
        Float,
        Double,
    } returnKind = HostReturnKind::Integer;
    bool returnsBlock = false;
    bool returnsNSRange = false;
    if(hasMethodSignature) {
        char *returnType = copyReturnType();
        if(returnType) {
            const char *unqualifiedType = returnType;
            while(*unqualifiedType &&
                    strchr("rnNoORVA", *unqualifiedType)) {
                unqualifiedType++;
            }
            if(*unqualifiedType == 'f') {
                returnKind = HostReturnKind::Float;
            } else if(*unqualifiedType == 'd') {
                returnKind = HostReturnKind::Double;
            }
            returnsBlock = unqualifiedType[0] == '@' &&
                unqualifiedType[1] == '?';
            returnsNSRange = LC32NativeNSRangeType(unqualifiedType);
            free(returnType);
        }
    }

    auto makeHostMessageInvocation = [&](bool invokeSuper, u64 target) {
        LC32HostMessageInvocation invocation = {};
        invocation.invokeSuper = invokeSuper;
        invocation.target = target;
        invocation.selector = host_cmd;
        memcpy(invocation.integerArguments, integerArguments,
               sizeof(integerArguments));
        memcpy(invocation.floatingArguments, floatingArguments,
               sizeof(floatingArguments));
        return invocation;
    };

    auto invokeScalar = [&](bool invokeSuper, u64 target) -> u64 {
        const LC32HostMessageInvocation invocation =
            makeHostMessageInvocation(invokeSuper, target);
        LC32GuestHostCallQuiescence quiescence;
        const bool trackInitializerOwnership =
            LC32SelectorIsInInitializerFamily(selector) &&
            receiverGuard.ownsReference();
        LC32HostInitializerInvocationScope initializerScope(
            trackInitializerOwnership ? receiver : nil);
        double floatingResult;
        switch(returnKind) {
            case HostReturnKind::Float:
                floatingResult =
                    LC32InvokeHostMessageFloat(&invocation);
                break;
            case HostReturnKind::Double:
                floatingResult =
                    LC32InvokeHostMessageDouble(&invocation);
                break;
            case HostReturnKind::Integer: {
                const u64 result =
                    LC32InvokeHostMessageInteger(&invocation);
                const int initializerOwnershipDelta =
                    initializerScope.ownershipDelta();
                if(result && initializerOwnershipDelta == -1) {
                    /* A same-object init result is still +1 by convention.
                     * If native code consumed the allocation's original +1,
                     * the invocation guard is now the only physical reference
                     * which can satisfy that contract. Transfer it to the
                     * result before the guard's destructor runs. Replacement
                     * and nil results keep the ordinary guard cleanup. */
                    (void)receiverGuard.relinquishIfEqual(
                        (id)(uintptr_t)result);
                } else if(initializerOwnershipDelta < -1) {
                    fprintf(stderr,
                        "LC32: initializer %s consumed %d more native "
                        "references than it retained for receiver %p\n",
                        sel_getName(selector), -initializerOwnershipDelta,
                        receiver);
                }
                quiescence.finish();
                return result;
            }
        }
        quiescence.finish();
        u64 resultBits;
        memcpy(&resultBits, &floatingResult, sizeof(resultBits));
        return resultBits;
    };

    auto invokeStruct = [&](bool invokeSuper, u64 target) {
        const LC32HostMessageInvocation invocation =
            makeHostMessageInvocation(invokeSuper, target);
        if(returnsNSRange) {
            if(structLen != sizeof(LC32HostMessageTwoU64)) {
                printf("LC32: invalid NSRange return size %u for selector %s\n",
                       structLen, sel_getName(selector));
                return;
            }
            LC32GuestHostCallQuiescence quiescence;
            const LC32HostMessageTwoU64 result =
                LC32InvokeHostMessageTwoU64(&invocation);
            quiescence.finish();
            (void)Dynarmic_mem_1write(structPtr, sizeof(result),
                (char *)&result);
            return;
        }
        switch(structLen) {
            case sizeof(LC32HostMessageTwoDoubles): {
                LC32GuestHostCallQuiescence quiescence;
                const LC32HostMessageTwoDoubles result =
                    LC32InvokeHostMessageTwoDoubles(&invocation);
                quiescence.finish();
                (void)Dynarmic_mem_1write(structPtr, sizeof(result),
                    (char *)&result);
                break;
            }
            case sizeof(LC32HostMessageFourDoubles): {
                LC32GuestHostCallQuiescence quiescence;
                const LC32HostMessageFourDoubles result =
                    LC32InvokeHostMessageFourDoubles(&invocation);
                quiescence.finish();
                (void)Dynarmic_mem_1write(structPtr, sizeof(result),
                    (char *)&result);
                break;
            }
            case sizeof(LC32_SixDoubles): {
                LC32GuestHostCallQuiescence quiescence;
                const LC32_SixDoubles result =
                    LC32InvokeHostMessageSixDoubles(&invocation);
                quiescence.finish();
                (void)Dynarmic_mem_1write(structPtr, sizeof(result),
                    (char *)&result);
                break;
            }
            default:
                printf("LC32: unsupported host struct return size %u "
                       "for selector %s\n", structLen,
                       sel_getName(selector));
                break;
        }
    };

    auto finishScalarResult = [&](u64 result) -> u64 {
        if(selector == @selector(retainCount) &&
           receiverGuard.ownsReference() && result && result != UINT64_MAX) {
            /* The atomic weak promotion is an implementation-only +1 and
             * must not become visible through the public retainCount result. */
            result--;
        }
        result = finishIndirectArguments(result);
        if(!returnGuestObject || !result) return result;

        /*
         * Convert the native +0 result while it is still protected by the
         * caller's autorelease-return convention.  -guest_self pins the
         * proxy to the native object's lifetime; any later guest retain adds
         * its own paired native ownership through the retain shim.
         */
        id objectResult = (id)result;
        if(returnsBlock) {
            /*
             * A mapped native block already owns a copied guest block. Its
             * caller uses _Block_copy/_Block_release, not NSObject's logical
             * retain/release pair, so return that mapping unchanged.
             */
            const u32 guestBlock = [objectResult guest_selfOrNull];
            if(!guestBlock) {
                fprintf(stderr,
                    "LC32: cannot bridge host-created block result from %s\n",
                    sel_getName(selector));
            }
            return guestBlock;
        }
        return LC32GuestObjectForBorrowedHostResult(objectResult);
    };

    // If we're calling from guest within a guest subclass, call super
    if(invokeSuper) {
        struct objc_super superInfo = {
            (id)host_self,
            dispatchClass
        };
        if(structPtr) {
            invokeStruct(true, (u64)&superInfo);
            return finishIndirectArguments(0);
        } else {
            return finishScalarResult(
                invokeScalar(true, (u64)&superInfo));
        }
    } else {
        if(structPtr) {
            invokeStruct(false, host_self);
            return finishIndirectArguments(0);
        } else {
            return finishScalarResult(
                invokeScalar(false, host_self));
        }
    }
}

void LC32SetInvokeGuestFuncPtr(u32 dlsymFunc, u32 invokeFunc) {
    sharedHandle.guest_dlsym = dlsymFunc;
    sharedHandle.guest_LC32InvokeGuestC = invokeFunc;
}

#pragma mark Host -> Guest functions

static u32 LC32CachedGuestSymbol(std::atomic<u32> &cache,
                                 const char *name) {
    u32 value = cache.load(std::memory_order_acquire);
    if(value) return value;
    const u32 resolved = guest_dlsym(name);
    if(!resolved) return 0;
    if(cache.compare_exchange_strong(value, resolved,
            std::memory_order_release, std::memory_order_acquire))
        return resolved;
    return value;
}

static u32 LC32CachedGuestSelector(std::atomic<u32> &cache,
                                   const char *name) {
    u32 value = cache.load(std::memory_order_acquire);
    if(value) return value;
    const u32 resolved = guest_sel_registerName(name);
    if(!resolved) return 0;
    if(cache.compare_exchange_strong(value, resolved,
            std::memory_order_release, std::memory_order_acquire))
        return resolved;
    return value;
}

static thread_local u32 LC32GuestCallbackNesting = 0;

u32 LC32GuestCallbackDepth(void) {
    return LC32GuestCallbackNesting;
}

u64 LC32InvokeGuestC(u32 pc, bool ret64, int argc, u32 *args) {
    if(threadHandle.jit == nullptr || threadHandle.cb == nullptr) {
        fprintf(stderr,
            "LC32: refusing guest callback on an unregistered host thread "
            "(pc=0x%x)\n", pc);
        return 0;
    }
    struct CallbackScope {
        CallbackScope() { ++LC32GuestCallbackNesting; }
        ~CallbackScope() { --LC32GuestCallbackNesting; }
    } callbackScope;
    std::array<std::uint32_t, 16> &regs = threadHandle.jit->Regs();
    struct context32 ctx;
    Dynarmic_context_1save(&ctx);

    // TODO: optimize this
    // first 4 arguments go to r0-r3
    for(int i = 0; i < MIN(argc, 4); i++) {
        regs[i] = args[i];
    }
    // Subsequent arguments go to the stack. Keep the AAPCS32 public-call
    // boundary eight-byte aligned while leaving args[4] at [sp].
    const int stackArgumentCount = argc > 4 ? argc - 4 : 0;
    if(stackArgumentCount & 1) {
        regs[Reg::SP] -= sizeof(u32);
        Dynarmic_current_user_callbacks()->MemoryWrite32(regs[Reg::SP], 0);
    }
    for(int i = argc-1; i >= 4; i--) {
        Dynarmic_current_user_callbacks()->MemoryWrite32(regs[Reg::SP] -= sizeof(u32), args[i]);
    }
    regs[12] = pc;
    Dynarmic::HaltReason reason =
        Dynarmic_emu_1start(sharedHandle.guest_LC32InvokeGuestC);
    bool callbackSteppedOut = false;
    bool debuggerSessionEnded = false;
    bool debuggerTargetExited = false;
    if(!Dynarmic::Has(reason, LC32HaltReasonRetFromGuest) &&
            !Dynarmic::Has(reason, LC32HaltReasonExit) &&
            LC32DebuggerActive() &&
            Dynarmic_debugger_begin_main_callback_stop(reason)) {
        /*
         * The outer gdbstub target callback is below UIApplicationMain on
         * this same host stack, so it cannot publish this stop itself.  Pump
         * the existing reader/packet queue re-entrantly while preserving the
         * callback register file and native call stack.
         */
        const gdbstub_run_reason_t nestedReason =
            gdbstub_run_nested_stop(
                &sharedHandle.gdbstub,
                (void *)&sharedHandle);
        callbackSteppedOut =
            nestedReason ==
                GDBSTUB_RUN_REASON_CALLBACK_STEPPED_OUT;
        if(nestedReason ==
                GDBSTUB_RUN_REASON_CALLBACK_RETURNED ||
                callbackSteppedOut) {
            reason = LC32HaltReasonRetFromGuest;
        } else if(nestedReason ==
                GDBSTUB_RUN_REASON_TARGET_EXITED) {
            reason = LC32HaltReasonExit;
            debuggerTargetExited = true;
        } else {
            /*
             * D, EOF, and protocol failure all abandon this stopped session.
             * Restore physical BKPTs before releasing all-stop, then finish
             * the preserved callback through its real guest return boundary.
             */
            if(!Dynarmic_debugger_remove_all_breakpoints()) {
                fprintf(stderr,
                    "LC32: cannot leave nested debugger stop: "
                    "one or more guest breakpoints could not be restored\n");
                fflush(stderr);
                abort();
            }
            Dynarmic_emu_1set_1debugger_1enabled(false);
            reason = Dynarmic_emu_1resume();
            if(!Dynarmic::Has(
                    reason, LC32HaltReasonRetFromGuest) &&
                    !Dynarmic::Has(
                        reason, LC32HaltReasonExit)) {
                fprintf(stderr,
                    "LC32: detached guest callback could not unwind: "
                    "entry=0x%08x reason=0x%08x pc=0x%08x\n",
                    pc, static_cast<unsigned>(reason),
                    regs[Reg::PC]);
                fflush(stderr);
                abort();
            }
            debuggerSessionEnded = true;
        }
        Dynarmic_debugger_end_main_callback_stop();
    }
    if(!Dynarmic::Has(reason, LC32HaltReasonRetFromGuest) &&
            !Dynarmic::Has(reason, LC32HaltReasonExit)) {
        fprintf(stderr,
            "LC32: guest callback stopped unexpectedly: entry=0x%08x "
            "reason=0x%08x pc=0x%08x lr=0x%08x sp=0x%08x "
            "cpsr=0x%08x\n",
            pc, static_cast<unsigned>(reason),
            regs[Reg::PC], regs[Reg::LR], regs[Reg::SP],
            threadHandle.jit->Cpsr());
        fflush(stderr);
    }
    /* An inferior exit has no callback return value.  Restore the saved
     * register owner only to unwind the native bridge stack; never sample
     * the partially terminated callback's r0/r1 as a fabricated result. */
    u64 result = 0;
    if(!debuggerTargetExited) {
        result = (u64)regs[0];
        if(ret64) result |= (u64)regs[1] << 32;
    }

    Dynarmic_context_1restore(&ctx);
    if(callbackSteppedOut && LC32DebuggerActive()) {
        /* The single-step crossed the callback's private return sentinel.
         * Stop only after exposing the restored outer guest context; the
         * suspended outer protocol frame will publish that stop reply. */
        Dynarmic_debugger_request_main_callback_step_out();
    }
    if(debuggerSessionEnded || debuggerTargetExited) {
        /* Wake the enclosing host run loop and plant a private outer-JIT
         * boundary so the original protocol frame can consume the nested
         * terminal marker without creating another socket reader. */
        Dynarmic_debugger_request_main_session_unwind();
    }
    return result;
}

u32 LC32HostToGuestArgument(char *type, u64 value) {
    while(*type && strchr("rnNoORVA", *type)) type++;
    switch(*type) {
        case 'B': // bool
        case 'I':
        case 'Q':
        case 'c':
        case 'i':
        case 'q':
            return (u32)value;
        case 'd':
            return (float)(CGFloat)value;
        case '@': // id
        case '#': // Class
            return [(id)value guest_self];
        case ':': { // SEL
            SEL selector = (SEL)value;
            return selector
                ? guest_sel_registerName(sel_getName(selector))
                : 0;
        }
        case '^':
            /*
             * Legacy Cocoa callbacks use void * as an opaque context token.
             * Guest shims pass those tokens to the host zero-extended, so
             * values which still fit in 32 bits can safely make the return
             * trip without exposing or dereferencing host memory. Reject a
             * real ARM64 pointer instead of silently truncating it.
             */
            if(type[1] == 'v' && value == (u64)(u32)value) {
                return (u32)value;
            }
            /*
             * NSZone is an obsolete allocator hint.  A native zone pointer
             * cannot be exposed to the 32-bit address space, and Cocoa
             * treats a null zone as the default zone.  This is used by
             * copyWithZone: while native collections copy guest-backed
             * objects.
             */
            if(!strncmp(type, "^{_NSZone=",
                        sizeof("^{_NSZone=") - 1) ||
               !strncmp(type, "^{NSZone=",
                        sizeof("^{NSZone=") - 1)) {
                return 0;
            }
            [[fallthrough]];
        default:
            printf("LC32HostToGuestArgument: unhandled type %s\n", type);
            abort();
    }
}

static float LC32GuestFloatReturn(u64 value) {
    const u32 bits = (u32)value;
    float result;
    memcpy(&result, &bits, sizeof(result));
    return result;
}

static double LC32GuestDoubleReturn(u64 value) {
    double result;
    memcpy(&result, &value, sizeof(result));
    return result;
}

u64 LC32GuestToHostReturnType(char *type, u64 value) {
    while(*type && strchr("rnNoORVA", *type)) type++;
    switch(*type) {
        case 'B': // bool
        case 'C':
        case 'I':
        case 'L':
        case 'S':
        case 'b':
        case 'c':
        case 'i':
        case 'l':
        case 's':
            return (u64)(u32)value;
        case 'Q':
        case 'q':
            return value;
        case 'v':
            return 0;
        case 'f': {
            const double hostValue = LC32GuestFloatReturn(value);
            u64 result;
            memcpy(&result, &hostValue, sizeof(result));
            return result;
        }
        case 'd': {
            const double hostValue = LC32GuestDoubleReturn(value);
            u64 result;
            memcpy(&result, &hostValue, sizeof(result));
            return result;
        }
        case '@': // id
        case '#': {// Class
            // don't call LC32GetHostObject here! the guest stores host pointer
            static std::atomic<u32> guestPtr{0};
            const u32 selector = LC32CachedGuestSelector(
                guestPtr, "host_self");
            u32 args[] = {(u32)value, selector};
            return guest_objc_msgSend(sizeof(args)/sizeof(*args), args);
        }
        default:
            printf("LC32GuestToHostReturnType: unhandled type %s\n", type);
            abort();
    }
}

static u64 LC32InvokeGuestSelectorWordsRaw(id self, SEL _cmd,
                                           const u32 *argumentWords,
                                           size_t argumentWordCount) {
    assert(argumentWordCount <= 18);

    u32 guest_args[20];
    size_t guest_argc = 0;
    guest_args[guest_argc++] = (u32)(u64)[self guest_self];
    guest_args[guest_argc++] = guest_sel_registerName(sel_getName(_cmd));
    for(size_t index = 0; index < argumentWordCount; index++) {
        guest_args[guest_argc++] = argumentWords[index];
    }

    return guest_objc_msgSend((int)guest_argc, guest_args);
}

static void LC32InvokeGuestSelectorFloatingArgumentBits(
        id self, SEL selector, u64 bits, bool guestDouble) {
    LC32TraceGuestMethodCallback(self, selector);
    if(Dynarmic_guest_thread_is_registered()) {
        // self/_cmd occupy r0/r1, so either a float in r2 or an aligned
        // double in r2/r3 can be passed directly in ARM32 argument words.
        const u32 words[] = {(u32)bits, (u32)(bits >> 32)};
        (void)LC32InvokeGuestSelectorWordsRaw(
            self, selector, words, guestDouble ? 2 : 1);
        return;
    }

    // Native download/KVC callbacks can arrive on a thread with no guest
    // register owner. Preserve the scalar's bits in the existing synchronous
    // selector executor rather than trying to enter the emulator here.
    LC32GuestBlockCallbackDescriptor callback = {};
    callback.kind = LC32GuestBlockCallbackKindSelector;
    callback.guestBlock = [self guest_selfOrNull];
    callback.guestInvoke = LC32LookupGuestSelectorMapping(selector);
    callback.resultKind = LC32GuestBlockValueVoid;
    callback.argumentCount = 1;
    callback.arguments[0].kind = guestDouble
        ? LC32GuestBlockValueUnsigned64 : LC32GuestBlockValueUnsigned32;
    callback.arguments[0].value = bits;

    id retainedReceiver = objc_retain(self);
    const bool submitted =
        Dynarmic_submit_guest_selector_callback(&callback);
    objc_release(retainedReceiver);
    if(!submitted) {
        fprintf(stderr,
            "LC32: cannot relay foreign-thread floating guest callback "
            "%c[%s %s]\n", object_isClass(self) ? '+' : '-',
            self ? class_getName(object_getClass(self)) : "(null)",
            selector ? sel_getName(selector) : "(null)");
    }
}

// The native scalar arrives in s0/d0, independently of x2-x7. Typed IMPs
// capture it before constructing the guest's soft-float argument words.
// CGFloat overrides may also need narrowing/widening between the two ABIs.
template<typename GuestFloat, typename HostFloat>
static void LC32InvokeGuestSelectorFloatingArgument(
        id self, SEL selector, HostFloat value) {
    const GuestFloat guestValue = static_cast<GuestFloat>(value);
    u64 bits = 0;
    memcpy(&bits, &guestValue, sizeof(guestValue));
    LC32InvokeGuestSelectorFloatingArgumentBits(
        self, selector, bits, sizeof(GuestFloat) == sizeof(double));
}

static u64 LC32InvokeGuestSelectorWords(id self, SEL _cmd,
                                        const u32 *argumentWords,
                                        size_t argumentWordCount) {
    const u64 guest_result = LC32InvokeGuestSelectorWordsRaw(
        self, _cmd, argumentWords, argumentWordCount);
    Method method = object_isClass(self)
        ? class_getClassMethod(self, _cmd)
        : class_getInstanceMethod((Class)[self class], _cmd);
    char *returnType = method_copyReturnType(method);
    const u64 host_result = LC32GuestToHostReturnType(returnType, guest_result);
    free(returnType);
    return host_result;
}

static u32 LC32GuestMalloc(u32 size) {
    static std::atomic<u32> cache{0};
    const u32 guestMalloc = LC32CachedGuestSymbol(cache, "malloc");
    if(!guestMalloc) return 0;
    u32 args[] = {size};
    return (u32)LC32InvokeGuestC(
        guestMalloc, false, sizeof(args) / sizeof(*args), args);
}

static bool LC32InputStreamReadSignatureMatches(
        const char *types, bool hostABI) {
    if(!types) return false;
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    if(!signature || signature.numberOfArguments != 4) return false;
    auto unqualified = [](const char *type) {
        while(type && *type && strchr("rnNoORVA", *type)) type++;
        return type;
    };
    const char *result = unqualified(signature.methodReturnType);
    const char *buffer = unqualified(
        [signature getArgumentTypeAtIndex:2]);
    const char *length = unqualified(
        [signature getArgumentTypeAtIndex:3]);
    const bool integerTypes = hostABI
        ? result && !strcmp(result, "q") && length && !strcmp(length, "Q")
        : result && (!strcmp(result, "i") || !strcmp(result, "l")) &&
            length && (!strcmp(length, "I") || !strcmp(length, "L"));
    // Older stream subclasses declare char * instead of uint8_t *. Both
    // are binary output buffers here, never NUL-terminated input strings.
    return integerTypes && buffer &&
        (!strcmp(buffer, "*") || !strcmp(buffer, "^c") ||
         !strcmp(buffer, "^C"));
}

static NSInteger LC32InvokeGuestSelectorInputStreamRead(
        id self, SEL selector, uint8_t *hostBuffer, NSUInteger maxLength) {
    LC32TraceGuestMethodCallback(self, selector);
    constexpr NSUInteger maximumBridgeBytes = 64u * 1024u * 1024u;
    if(maxLength > maximumBridgeBytes || maxLength > INT32_MAX ||
            (maxLength && !hostBuffer)) {
        errno = maxLength > maximumBridgeBytes || maxLength > INT32_MAX
            ? EOVERFLOW : EINVAL;
        return -1;
    }
    if(!Dynarmic_guest_thread_is_registered()) {
        // The existing selector executor only supports void results. Do not
        // fabricate EOF or expose this native output pointer to guest code.
        fprintf(stderr,
            "LC32: foreign-thread stream read callback is unsupported "
            "(-[%s %s])\n", class_getName(object_getClass(self)),
            sel_getName(selector));
        errno = ENOTSUP;
        return -1;
    }

    // Retain non-null pointer identity even for a zero-length request.
    const u32 allocationBytes = maxLength ? (u32)maxLength :
        (hostBuffer ? 1u : 0u);
    std::vector<char> staging;
    try {
        staging.resize(allocationBytes, 0);
    } catch(const std::bad_alloc &) {
        errno = ENOMEM;
        return -1;
    }
    const u32 guestBuffer = allocationBytes
        ? LC32GuestMalloc(allocationBytes) : 0;
    if(allocationBytes && !guestBuffer) {
        errno = ENOMEM;
        return -1;
    }
    struct GuestBufferCleanup {
        u32 address;
        ~GuestBufferCleanup() {
            if(address) {
                const int savedError = errno;
                guest_free(address);
                errno = savedError;
            }
        }
    } cleanup{guestBuffer};
    const auto finish = [&](NSInteger result, int error) {
        if(error) errno = error;
        return result;
    };
    if(allocationBytes && Dynarmic_mem_1write(
            guestBuffer, allocationBytes, staging.data()) != 0) {
        return finish(-1, EFAULT);
    }

    const u32 words[] = {guestBuffer, (u32)maxLength};
    const int32_t result = (int32_t)(u32)LC32InvokeGuestSelectorWordsRaw(
        self, selector, words, sizeof(words) / sizeof(*words));
    if(result < -1 || (result >= 0 && (u32)result > maxLength)) {
        fprintf(stderr,
            "LC32: guest stream read returned invalid byte count %d "
            "for capacity %llu\n", result, (unsigned long long)maxLength);
        return finish(-1, EIO);
    }
    if(result > 0) {
        if(Dynarmic_mem_1read(guestBuffer, (u32)result,
                staging.data()) != 0) return finish(-1, EFAULT);
        memcpy(hostBuffer, staging.data(), (size_t)result);
    }
    // NSInteger is signed 64-bit on the host, signed 32-bit in the guest.
    // Copy only successfully returned bytes; preserve the caller's tail and
    // leave its entire buffer untouched for EOF or the -1 error result.
    return finish((NSInteger)result, 0);
}

/*
 * NSFastEnumerationState is eight pointer-sized words on both platforms, but
 * those words are 32 bits in the ARM guest and 64 bits in the host.  A native
 * collection can therefore call a dynamically mirrored guest implementation
 * only after both the state and its object buffer have been staged in guest
 * memory.
 *
 * Keep the exact guest state in the host state's opaque extra[] storage.  A
 * fresh guest allocation is used for each synchronous batch, avoiding a guest
 * allocation leak when a native caller leaves a for-in loop early.  Batches
 * are deliberately limited to one object: the guest mutation word can then
 * be checked between every object even though its address is not meaningful
 * to the host runtime.
 */
struct LC32GuestFastEnumerationState32 {
    u32 state;
    u32 itemsPtr;
    u32 mutationsPtr;
    u32 extra[5];
};

struct LC32StoredFastEnumerationState {
    LC32GuestFastEnumerationState32 guest;
    u32 previousGuestBuffer;
    u32 mutationValue;
};

static_assert(sizeof(LC32GuestFastEnumerationState32) == 32);
static_assert(sizeof(LC32StoredFastEnumerationState) ==
              sizeof(((NSFastEnumerationState *)nullptr)->extra));

static NSUInteger LC32InvokeGuestSelectorFastEnumeration(
        id self, SEL _cmd, NSFastEnumerationState *hostState,
        id __unsafe_unretained hostObjects[], NSUInteger hostCount) {
    if(!hostState || !hostObjects || hostCount == 0) return 0;

    LC32StoredFastEnumerationState stored = {};
    const bool continuing = hostState->state != 0;
    if(continuing) {
        memcpy(&stored, hostState->extra, sizeof(stored));
        if(stored.guest.mutationsPtr) {
            u32 currentMutation = 0;
            bool mutationRead = false;
            const u32 previousGuestState =
                stored.previousGuestBuffer >=
                        sizeof(LC32GuestFastEnumerationState32)
                    ? stored.previousGuestBuffer -
                        sizeof(LC32GuestFastEnumerationState32)
                    : 0;
            if(previousGuestState &&
                    stored.guest.mutationsPtr >= previousGuestState &&
                    stored.guest.mutationsPtr <=
                        previousGuestState + sizeof(stored.guest) -
                            sizeof(currentMutation)) {
                const u32 offset =
                    stored.guest.mutationsPtr - previousGuestState;
                memcpy(&currentMutation,
                    reinterpret_cast<const char *>(&stored.guest) + offset,
                    sizeof(currentMutation));
                mutationRead = true;
            } else {
                mutationRead = Dynarmic_mem_1read(
                    stored.guest.mutationsPtr, sizeof(currentMutation),
                    reinterpret_cast<char *>(&currentMutation)) == 0;
            }
            if(!mutationRead) {
                fprintf(stderr,
                    "LC32: cannot read guest mutation state for selector "
                    "%s at 0x%08x\n",
                    sel_getName(_cmd), stored.guest.mutationsPtr);
                abort();
            }
            if(currentMutation != stored.mutationValue) {
                objc_enumerationMutation(self);
            }
        }
    }

    constexpr u32 guestObjectBytes = sizeof(u32);
    constexpr u32 guestAllocationBytes =
        sizeof(LC32GuestFastEnumerationState32) + guestObjectBytes;
    const u32 guestAllocation = LC32GuestMalloc(guestAllocationBytes);
    if(!guestAllocation) {
        fprintf(stderr,
            "LC32: could not allocate guest fast-enumeration staging for "
            "selector %s\n", sel_getName(_cmd));
        abort();
    }
    const u32 guestBuffer =
        guestAllocation + sizeof(LC32GuestFastEnumerationState32);

    /* Rebase pointers which the implementation retained into the preceding
     * caller-owned staging block. Pointers into the collection's own storage
     * are preserved unchanged. */
    if(continuing && stored.previousGuestBuffer >=
            sizeof(LC32GuestFastEnumerationState32)) {
        const u32 previousGuestAllocation =
            stored.previousGuestBuffer -
                sizeof(LC32GuestFastEnumerationState32);
        const u32 previousGuestAllocationEnd =
            previousGuestAllocation + guestAllocationBytes;
        auto rebaseStagingPointer = [&](u32 pointer) -> u32 {
            if(pointer < previousGuestAllocation ||
                    pointer >= previousGuestAllocationEnd) {
                return pointer;
            }
            return guestAllocation +
                (pointer - previousGuestAllocation);
        };
        stored.guest.itemsPtr =
            rebaseStagingPointer(stored.guest.itemsPtr);
        stored.guest.mutationsPtr =
            rebaseStagingPointer(stored.guest.mutationsPtr);
    }

    u32 zero = 0;
    bool stagingWritten = Dynarmic_mem_1write(
        guestAllocation, sizeof(stored.guest),
        reinterpret_cast<char *>(&stored.guest)) == 0;
    stagingWritten = stagingWritten && Dynarmic_mem_1write(
        guestBuffer, sizeof(zero), reinterpret_cast<char *>(&zero)) == 0;
    if(!stagingWritten) {
        guest_free(guestAllocation);
        fprintf(stderr,
            "LC32: could not initialize guest fast-enumeration staging for "
            "selector %s\n", sel_getName(_cmd));
        abort();
    }

    const u32 words[] = {guestAllocation, guestBuffer, 1};
    const u32 result = (u32)LC32InvokeGuestSelectorWordsRaw(
        self, _cmd, words, sizeof(words) / sizeof(*words));

    LC32GuestFastEnumerationState32 updatedGuestState = {};
    bool stateRead = Dynarmic_mem_1read(
        guestAllocation, sizeof(updatedGuestState),
        reinterpret_cast<char *>(&updatedGuestState)) == 0;
    u32 guestObject = 0;
    if(stateRead && result != 0 && updatedGuestState.itemsPtr != 0) {
        stateRead = Dynarmic_mem_1read(
            updatedGuestState.itemsPtr, sizeof(guestObject),
            reinterpret_cast<char *>(&guestObject)) == 0;
    }

    u32 updatedMutation = stored.mutationValue;
    if(stateRead && updatedGuestState.mutationsPtr != 0) {
        stateRead = Dynarmic_mem_1read(
            updatedGuestState.mutationsPtr, sizeof(updatedMutation),
            reinterpret_cast<char *>(&updatedMutation)) == 0;
    }
    const bool mutationChanged = stateRead && continuing &&
        updatedGuestState.mutationsPtr != 0 &&
        updatedMutation != stored.mutationValue;
    guest_free(guestAllocation);

    if(!stateRead || result > 1 || (result != 0 && guestObject == 0)) {
        fprintf(stderr,
            "LC32: invalid guest fast-enumeration result for selector %s "
            "(count=%u, items=0x%08x, mutation=0x%08x)\n",
            sel_getName(_cmd), result, updatedGuestState.itemsPtr,
            updatedGuestState.mutationsPtr);
        abort();
    }
    if(mutationChanged) objc_enumerationMutation(self);

    stored.guest = updatedGuestState;
    stored.previousGuestBuffer = guestBuffer;
    stored.mutationValue = updatedMutation;
    memcpy(hostState->extra, &stored, sizeof(stored));

    static unsigned long stableHostMutationSentinel = 0;
    hostState->state = updatedGuestState.state
        ? (NSUInteger)updatedGuestState.state : (NSUInteger)1;
    hostState->itemsPtr = hostObjects;
    hostState->mutationsPtr = &stableHostMutationSentinel;

    if(result != 0) {
        hostObjects[0] = (id)LC32GuestToHostReturnType(
            const_cast<char *>("@"), guestObject);
    }
    return result;
}

static bool LC32FastEnumerationSignatureMatches(const char *types) {
    if(!types) return false;
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    if(!signature || signature.numberOfArguments != 5) return false;

    auto unqualified = [](const char *type) -> const char * {
        while(type && *type && strchr("rnNoORVA", *type)) type++;
        return type;
    };
    const char *returnType = unqualified(signature.methodReturnType);
    const char *stateType = unqualified(
        [signature getArgumentTypeAtIndex:2]);
    const char *objectsType = unqualified(
        [signature getArgumentTypeAtIndex:3]);
    const char *countType = unqualified(
        [signature getArgumentTypeAtIndex:4]);
    return returnType && strchr("ILQ", returnType[0]) &&
        stateType && stateType[0] == '^' && stateType[1] == '{' &&
        objectsType && objectsType[0] == '^' && objectsType[1] == '@' &&
        countType && strchr("ILQ", countType[0]);
}

static bool LC32RangeTypeHasFields(const char *type, char fieldType) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    if(!type ||
       (strncmp(type, "{_NSRange=", sizeof("{_NSRange=") - 1) &&
        strncmp(type, "{NSRange=", sizeof("{NSRange=") - 1))) {
        return false;
    }
    const char *fields = strchr(type, '=');
    return fields && fields[1] == fieldType && fields[2] == fieldType &&
        fields[3] == '}' && fields[4] == '\0';
}

static bool LC32UniCharRangeSignatureMatches(const char *types,
                                             char rangeFieldType) {
    if(!types) return false;
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    const char *returnType = signature.methodReturnType;
    while(returnType && *returnType && strchr("rnNoORVA", *returnType))
        returnType++;
    const char *charactersType = signature.numberOfArguments > 2
        ? [signature getArgumentTypeAtIndex:2] : nullptr;
    while(charactersType && *charactersType &&
          strchr("rnNoORVA", *charactersType)) charactersType++;
    if(!signature || signature.numberOfArguments != 4 ||
       !returnType || strcmp(returnType, "v") ||
       !charactersType || strcmp(charactersType, "^S")) {
        return false;
    }
    return LC32RangeTypeHasFields(
        [signature getArgumentTypeAtIndex:3], rangeFieldType);
}

static bool LC32ObjectRangeSignatureMatches(const char *types,
                                            char rangeFieldType) {
    if(!types) return false;
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    if(!signature || signature.numberOfArguments != 4) return false;

    auto unqualified = [](const char *type) -> const char * {
        while(type && *type && strchr("rnNoORVA", *type)) type++;
        return type;
    };
    const char *returnType = unqualified(signature.methodReturnType);
    const char *objectsType = unqualified(
        [signature getArgumentTypeAtIndex:2]);
    return returnType && !strcmp(returnType, "v") &&
        objectsType && !strcmp(objectsType, "^@") &&
        LC32RangeTypeHasFields(
            [signature getArgumentTypeAtIndex:3], rangeFieldType);
}

/*
 * NSArray's primitive -getObjects:range: writes native object pointers into
 * caller-owned ARM64 storage.  A mirrored guest collection instead expects
 * an array of 32-bit guest object addresses.  Stage that array synchronously
 * in guest memory, then convert each borrowed element back into the native
 * caller's unsafe-unretained output buffer.
 */
static void LC32InvokeGuestSelectorObjectRange(
        id self, SEL _cmd, id __unsafe_unretained hostObjects[],
        NSRange range) {
    constexpr NSUInteger kMaximumObjectBridgeBytes =
        64u * 1024u * 1024u;
    if(range.location > UINT32_MAX ||
       range.length > UINT32_MAX / sizeof(u32) ||
       range.location > UINT32_MAX - range.length ||
       range.length * sizeof(u32) > kMaximumObjectBridgeBytes ||
       (range.length && !hostObjects)) {
        fprintf(stderr,
            "LC32: invalid host object range for selector %s "
            "(location=%llu, length=%llu, output=%p)\n",
            sel_getName(_cmd), (unsigned long long)range.location,
            (unsigned long long)range.length, hostObjects);
        abort();
    }

    const u32 objectCount = (u32)range.length;
    const u32 byteCount = objectCount * sizeof(u32);
    /* Preserve non-null pointer identity for valid zero-length ranges. Some
     * guest collection primitives validate pointer/range consistency even
     * though they do not dereference the output in that case. */
    const u32 allocationBytes = byteCount
        ? byteCount : (hostObjects ? (u32)sizeof(u32) : 0);
    const u32 guestObjects = allocationBytes
        ? LC32GuestMalloc(allocationBytes) : 0;
    if(allocationBytes && !guestObjects) {
        fprintf(stderr,
            "LC32: could not allocate %u guest object bytes for selector "
            "%s\n", allocationBytes, sel_getName(_cmd));
        abort();
    }
    if(allocationBytes) {
        std::vector<char> emptyObjects(allocationBytes, 0);
        if(Dynarmic_mem_1write(guestObjects, allocationBytes,
                reinterpret_cast<char *>(emptyObjects.data())) != 0) {
            guest_free(guestObjects);
            fprintf(stderr,
                "LC32: could not initialize guest object output for "
                "selector %s\n", sel_getName(_cmd));
            abort();
        }
    }

    const u32 words[] = {
        guestObjects,
        (u32)range.location,
        objectCount,
    };
    (void)LC32InvokeGuestSelectorWordsRaw(
        self, _cmd, words, sizeof(words) / sizeof(*words));

    std::vector<u32> guestResults(objectCount, 0);
    const bool copied = !byteCount || Dynarmic_mem_1read(
        guestObjects, byteCount,
        reinterpret_cast<char *>(guestResults.data())) == 0;
    if(guestObjects) guest_free(guestObjects);
    if(!copied) {
        fprintf(stderr,
            "LC32: could not copy guest object output for selector %s\n",
            sel_getName(_cmd));
        abort();
    }

    for(u32 index = 0; index < objectCount; index++) {
        const u32 guestObject = guestResults[index];
        if(!guestObject) {
            fprintf(stderr,
                "LC32: guest selector %s returned a null object at index "
                "%u\n", sel_getName(_cmd), index);
            abort();
        }
        id hostObject = (id)LC32GuestToHostReturnType(
            const_cast<char *>("@"), guestObject);
        if(!hostObject) {
            fprintf(stderr,
                "LC32: could not convert guest object 0x%08x from selector "
                "%s\n", guestObject, sel_getName(_cmd));
            abort();
        }
        hostObjects[index] = hostObject;
    }
}

/*
 * NSString subclasses implement this primitive in the guest, but native
 * Foundation supplies an ARM64 UniChar output buffer.  Stage that buffer in
 * guest-addressable storage for the synchronous callback, then copy the
 * result back before returning to Foundation.
 */
static void LC32InvokeGuestSelectorUniCharRange(
        id self, SEL _cmd, UniChar *hostCharacters, NSRange range) {
    constexpr NSUInteger kMaximumUniCharBridgeBytes =
        64u * 1024u * 1024u;
    if(range.location > UINT32_MAX ||
       range.length > UINT32_MAX / sizeof(UniChar) ||
       range.location > UINT32_MAX - range.length ||
       range.length * sizeof(UniChar) > kMaximumUniCharBridgeBytes ||
       (range.length && !hostCharacters)) {
        fprintf(stderr,
            "LC32: invalid host UniChar range for selector %s "
            "(location=%llu, length=%llu, output=%p)\n",
            sel_getName(_cmd), (unsigned long long)range.location,
            (unsigned long long)range.length, hostCharacters);
        abort();
    }

    const u32 byteCount = (u32)range.length * sizeof(UniChar);
    const u32 guestCharacters = byteCount ? LC32GuestMalloc(byteCount) : 0;
    if(byteCount && !guestCharacters) {
        fprintf(stderr,
            "LC32: could not allocate %u guest bytes for selector %s\n",
            byteCount, sel_getName(_cmd));
        abort();
    }
    if(byteCount) {
        std::vector<char> poison(byteCount, static_cast<char>(0xa5));
        if(Dynarmic_mem_1write(guestCharacters, byteCount,
                poison.data()) != 0) {
            guest_free(guestCharacters);
            fprintf(stderr,
                "LC32: could not initialize guest UniChar output for "
                "selector %s\n", sel_getName(_cmd));
            abort();
        }
    }

    const u32 words[] = {
        guestCharacters,
        (u32)range.location,
        (u32)range.length,
    };
    (void)LC32InvokeGuestSelectorWordsRaw(
        self, _cmd, words, sizeof(words) / sizeof(*words));

    const bool copied = !byteCount || Dynarmic_mem_1read(
        guestCharacters, byteCount,
        reinterpret_cast<char *>(hostCharacters)) == 0;
    if(guestCharacters) guest_free(guestCharacters);
    if(!copied) {
        fprintf(stderr,
            "LC32: could not copy guest UniChar output for selector %s\n",
            sel_getName(_cmd));
        abort();
    }
}

static u32 LC32GuestFloatWord(CGFloat value) {
    const float guestValue = (float)value;
    u32 word;
    memcpy(&word, &guestValue, sizeof(word));
    return word;
}

static bool LC32CGSizeTypeHasFields(const char *type, char fieldType) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    if(!type ||
       (strncmp(type, "{CGSize=", sizeof("{CGSize=") - 1) &&
        strncmp(type, "{_CGSize=", sizeof("{_CGSize=") - 1))) {
        return false;
    }
    const char *fields = strchr(type, '=');
    return fields && fields[1] == fieldType && fields[2] == fieldType &&
        fields[3] == '}' && fields[4] == '\0';
}

static bool LC32CGPointTypeHasFields(const char *type, char fieldType) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    if(!type ||
       (strncmp(type, "{CGPoint=", sizeof("{CGPoint=") - 1) &&
        strncmp(type, "{_CGPoint=", sizeof("{_CGPoint=") - 1))) {
        return false;
    }
    const char *fields = strchr(type, '=');
    return fields && fields[1] == fieldType && fields[2] == fieldType &&
        fields[3] == '}' && fields[4] == '\0';
}

static bool LC32PointObjectSignatureMatches(const char *types,
                                            char pointFieldType) {
    /*
     * NSMethodSignature in current Foundation rejects a few legacy Objective-C
     * encodings (notably anonymous unions such as "(?=...)").  This matcher is
     * consulted for every guest method, so only ask Foundation to parse methods
     * which can actually contain the CGPoint argument we are looking for.
     */
    if(!types ||
       (!strstr(types, "{CGPoint=") && !strstr(types, "{_CGPoint="))) {
        return false;
    }
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    if(!signature || signature.numberOfArguments != 4 ||
       !LC32CGPointTypeHasFields(
           [signature getArgumentTypeAtIndex:2], pointFieldType)) {
        return false;
    }
    const char *objectType = [signature getArgumentTypeAtIndex:3];
    while(objectType && *objectType && strchr("rnNoORVA", *objectType)) {
        objectType++;
    }
    const char *returnType = signature.methodReturnType;
    while(returnType && *returnType && strchr("rnNoORVA", *returnType)) {
        returnType++;
    }
    return objectType && objectType[0] == '@' && objectType[1] != '?' &&
        returnType && strchr("vBCILQSbcilqs@#", returnType[0]);
}

static bool LC32CGSizeToCGSizeSignatureMatches(const char *types,
                                               char fieldType) {
    if(!types) return false;
    const char *returnType = types;
    while(*returnType && strchr("rnNoORVA", *returnType)) returnType++;
    const char *fields = strchr(returnType, '=');
    if((strncmp(returnType, "{CGSize=", sizeof("{CGSize=") - 1) &&
        strncmp(returnType, "{_CGSize=", sizeof("{_CGSize=") - 1)) ||
       !fields || fields[1] != fieldType || fields[2] != fieldType ||
       fields[3] != '}') {
        return false;
    }
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    return signature && signature.numberOfArguments == 3 &&
        LC32CGSizeTypeHasFields(signature.methodReturnType, fieldType) &&
        LC32CGSizeTypeHasFields(
            [signature getArgumentTypeAtIndex:2], fieldType);
}

static bool LC32CGRectTypeHasFields(const char *type, char fieldType) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    if(!type ||
       (strncmp(type, "{CGRect=", sizeof("{CGRect=") - 1) &&
        strncmp(type, "{_CGRect=", sizeof("{_CGRect=") - 1))) {
        return false;
    }

    const char *field = strchr(type, '=');
    if(!field) return false;
    field++;
    if(!strncmp(field, "{CGPoint=", sizeof("{CGPoint=") - 1)) {
        field += sizeof("{CGPoint=") - 1;
    } else if(!strncmp(field, "{_CGPoint=", sizeof("{_CGPoint=") - 1)) {
        field += sizeof("{_CGPoint=") - 1;
    } else {
        return false;
    }
    if(field[0] != fieldType || field[1] != fieldType || field[2] != '}')
        return false;
    field += 3;
    if(!strncmp(field, "{CGSize=", sizeof("{CGSize=") - 1)) {
        field += sizeof("{CGSize=") - 1;
    } else if(!strncmp(field, "{_CGSize=", sizeof("{_CGSize=") - 1)) {
        field += sizeof("{_CGSize=") - 1;
    } else {
        return false;
    }
    return field[0] == fieldType && field[1] == fieldType &&
        field[2] == '}' && field[3] == '}' && field[4] == '\0';
}

static bool LC32CGRectToCGRectSignatureMatches(const char *types,
                                               char fieldType) {
    if(!types) return false;
    const char *returnType = types;
    while(*returnType && strchr("rnNoORVA", *returnType)) returnType++;
    if(strncmp(returnType, "{CGRect=", sizeof("{CGRect=") - 1) &&
       strncmp(returnType, "{_CGRect=", sizeof("{_CGRect=") - 1)) {
        return false;
    }
    NSMethodSignature *signature =
        [NSMethodSignature signatureWithObjCTypes:types];
    return signature && signature.numberOfArguments == 3 &&
        LC32CGRectTypeHasFields(signature.methodReturnType, fieldType) &&
        LC32CGRectTypeHasFields(
            [signature getArgumentTypeAtIndex:2], fieldType);
}

static void LC32GuestObjCMsgSendStret(int argc, u32 *args) {
    LC32DrainDeferredGuestPinReleases();
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "objc_msgSend_stret");
    if(!guestPtr) {
        fprintf(stderr, "LC32: guest objc_msgSend_stret is unavailable\n");
        abort();
    }
    (void)LC32InvokeGuestC(guestPtr, false, argc, args);
}

/*
 * CGSize is a two-double HFA in the ARM64 host ABI, so a typed IMP is needed
 * to receive and return it through d0-d1.  In Apple's ARMv7 ABI the same
 * two-float result is indirect: objc_msgSend_stret receives the result buffer
 * in r0, shifting self/cmd to r1/r2, with width in r3 and height on the stack.
 */
static CGSize LC32InvokeGuestSelectorCGSizeToCGSize(
        id self, SEL _cmd, CGSize hostSize) {
    const u32 guestSelf = (u32)(u64)[self guest_self];
    const u32 guestCommand = guest_sel_registerName(sel_getName(_cmd));

    std::array<std::uint32_t, 16> &regs = threadHandle.jit->Regs();
    const u32 originalStackPointer = regs[Reg::SP];
    const u32 guestResult = (originalStackPointer - sizeof(u32[2])) & ~7u;
    regs[Reg::SP] = guestResult;
    Dynarmic_current_user_callbacks()->MemoryWrite32(guestResult, 0);
    Dynarmic_current_user_callbacks()->MemoryWrite32(guestResult + 4, 0);

    u32 args[] = {
        guestResult,
        guestSelf,
        guestCommand,
        LC32GuestFloatWord(hostSize.width),
        LC32GuestFloatWord(hostSize.height),
    };
    LC32GuestObjCMsgSendStret(sizeof(args) / sizeof(*args), args);

    const u32 widthBits =
        Dynarmic_current_user_callbacks()->MemoryRead32(guestResult);
    const u32 heightBits =
        Dynarmic_current_user_callbacks()->MemoryRead32(guestResult + 4);
    regs[Reg::SP] = originalStackPointer;

    float guestWidth;
    float guestHeight;
    memcpy(&guestWidth, &widthBits, sizeof(guestWidth));
    memcpy(&guestHeight, &heightBits, sizeof(guestHeight));
    CGSize result = {};
    result.width = (CGFloat)guestWidth;
    result.height = (CGFloat)guestHeight;
    return result;
}

/*
 * CGRect is likewise returned indirectly by ARMv7 objc_msgSend_stret, while
 * the ARM64 host passes and returns its four-double CGRect as an HFA in
 * d0-d3.  Calling an ARMv7 CGRect-returning IMP through ordinary
 * objc_msgSend shifts self/cmd by one register: the IMP mistakes the selector
 * for self and crashes on its first nested message send.
 */
static CGRect LC32InvokeGuestSelectorCGRectToCGRect(
        id self, SEL _cmd, CGRect hostRect) {
    const u32 guestSelf = (u32)(u64)[self guest_self];
    const u32 guestCommand = guest_sel_registerName(sel_getName(_cmd));

    std::array<std::uint32_t, 16> &regs = threadHandle.jit->Regs();
    const u32 originalStackPointer = regs[Reg::SP];
    const u32 guestResult = (originalStackPointer - sizeof(u32[4])) & ~7u;
    regs[Reg::SP] = guestResult;
    for(u32 offset = 0; offset < sizeof(u32[4]); offset += sizeof(u32)) {
        Dynarmic_current_user_callbacks()->MemoryWrite32(
            guestResult + offset, 0);
    }

    u32 args[] = {
        guestResult,
        guestSelf,
        guestCommand,
        LC32GuestFloatWord(hostRect.origin.x),
        LC32GuestFloatWord(hostRect.origin.y),
        LC32GuestFloatWord(hostRect.size.width),
        LC32GuestFloatWord(hostRect.size.height),
    };
    LC32GuestObjCMsgSendStret(sizeof(args) / sizeof(*args), args);

    float guestFields[4] = {};
    for(u32 index = 0; index < 4; index++) {
        const u32 bits = Dynarmic_current_user_callbacks()->MemoryRead32(
            guestResult + index * sizeof(u32));
        memcpy(&guestFields[index], &bits, sizeof(bits));
    }
    regs[Reg::SP] = originalStackPointer;

    CGRect result = {
        {(CGFloat)guestFields[0], (CGFloat)guestFields[1]},
        {(CGFloat)guestFields[2], (CGFloat)guestFields[3]},
    };
    return result;
}

static u64 LC32InvokeGuestSelectorCGRectRaw(id self, SEL _cmd, CGRect rect) {
    const u32 words[] = {
        LC32GuestFloatWord(rect.origin.x),
        LC32GuestFloatWord(rect.origin.y),
        LC32GuestFloatWord(rect.size.width),
        LC32GuestFloatWord(rect.size.height),
    };
    return LC32InvokeGuestSelectorWordsRaw(
        self, _cmd, words, sizeof(words) / sizeof(*words));
}

// A CGRect is an HFA of four doubles in the arm64 host ABI, but four floats in
// the ARMv7 guest ABI. A typed IMP is required so the host values are captured
// from d0-d3 before they are narrowed and placed in the guest argument words.
static u64 LC32InvokeGuestSelectorCGRect(id self, SEL _cmd, CGRect rect) {
    const u32 words[] = {
        LC32GuestFloatWord(rect.origin.x),
        LC32GuestFloatWord(rect.origin.y),
        LC32GuestFloatWord(rect.size.width),
        LC32GuestFloatWord(rect.size.height),
    };
    return LC32InvokeGuestSelectorWords(
        self, _cmd, words, sizeof(words) / sizeof(*words));
}

/*
 * CGPoint is a two-double HFA in the ARM64 host ABI and arrives in d0-d1;
 * an object following it still arrives independently in x2.  The generic
 * integer-register trampoline therefore cannot observe the point.  Narrow it
 * through a typed IMP, then lay out the ARMv7 call as r2/r3/[sp].
 */
static u64 LC32InvokeGuestSelectorPointObject(
        id self, SEL _cmd, CGPoint point, id object) {
    const u32 words[] = {
        LC32GuestFloatWord(point.x),
        LC32GuestFloatWord(point.y),
        object ? [object guest_self] : 0,
    };
    return LC32InvokeGuestSelectorWords(
        self, _cmd, words, sizeof(words) / sizeof(*words));
}

static float LC32InvokeGuestSelectorCGRectGuestFloatHostFloat(
        id self, SEL _cmd, CGRect rect) {
    return LC32GuestFloatReturn(
        LC32InvokeGuestSelectorCGRectRaw(self, _cmd, rect));
}

static double LC32InvokeGuestSelectorCGRectGuestFloatHostDouble(
        id self, SEL _cmd, CGRect rect) {
    return (double)LC32GuestFloatReturn(
        LC32InvokeGuestSelectorCGRectRaw(self, _cmd, rect));
}

static float LC32InvokeGuestSelectorCGRectGuestDoubleHostFloat(
        id self, SEL _cmd, CGRect rect) {
    return (float)LC32GuestDoubleReturn(
        LC32InvokeGuestSelectorCGRectRaw(self, _cmd, rect));
}

static double LC32InvokeGuestSelectorCGRectGuestDoubleHostDouble(
        id self, SEL _cmd, CGRect rect) {
    return LC32GuestDoubleReturn(
        LC32InvokeGuestSelectorCGRectRaw(self, _cmd, rect));
}

// Keep x2-x7 as explicit parameters, then consume any arguments which the
// arm64 caller placed on the stack through va_list.
static u64 LC32InvokeGuestSelectorRaw(id self, SEL _cmd,
                                     u64 arg2, u64 arg3, u64 arg4,
                                     u64 arg5, u64 arg6, u64 arg7,
                                     va_list *hostStackArguments,
                                     Method *resolvedMethod) {
    LC32TraceGuestMethodCallback(self, _cmd);
    Method method = object_isClass(self) ? class_getClassMethod(self, _cmd) : class_getInstanceMethod((Class)[self class], _cmd);
    if(resolvedMethod) *resolvedMethod = method;
    if(!method) {
        fprintf(stderr,
            "LC32: cannot find guest method metadata for %c[%s %s]\n",
            object_isClass(self) ? '+' : '-',
            self ? class_getName(object_getClass(self)) : "(null)",
            _cmd ? sel_getName(_cmd) : "(null)");
        return 0;
    }

    const bool registered = Dynarmic_guest_thread_is_registered();
    const u32 guestCommand = registered
        ? guest_sel_registerName(sel_getName(_cmd))
        : LC32LookupGuestSelectorMapping(_cmd);
    const u32 guestSelf = registered
        ? (u32)(u64)[self guest_self]
        : [self guest_selfOrNull];

    LC32GuestBlockCallbackDescriptor callback = {};
    std::vector<id> callbackObjects;
    const char *callbackFailure = nullptr;
    if(!registered) {
        callback.kind = LC32GuestBlockCallbackKindSelector;
        callback.guestBlock = guestSelf;
        callback.guestInvoke = guestCommand;
        callback.resultKind = LC32GuestBlockValueVoid;

        char *returnType = method_copyReturnType(method);
        const char *unqualifiedReturnType = returnType;
        while(*unqualifiedReturnType &&
                strchr("rnNoORVA", *unqualifiedReturnType)) {
            unqualifiedReturnType++;
        }
        if(*unqualifiedReturnType != 'v') {
            callbackFailure = "non-void result";
        } else if(!guestSelf) {
            callbackFailure = "missing guest receiver mapping";
        } else if(!guestCommand) {
            callbackFailure = "missing guest selector mapping";
        }
        free(returnType);
    }

    // Objective-C method metadata describes logical arguments, but an arm64
    // NSRange occupies two general-purpose argument slots. Keep an independent
    // raw-slot cursor so following arguments stay aligned.
    const u64 hostRegisterArguments[] = {
        arg2, arg3, arg4, arg5, arg6, arg7
    };
    constexpr size_t hostRegisterArgumentCount =
        sizeof(hostRegisterArguments) / sizeof(*hostRegisterArguments);
    size_t hostArgumentSlot = 0;
    auto nextHostArgument = [&]() -> u64 {
        if(hostArgumentSlot < hostRegisterArgumentCount) {
            return hostRegisterArguments[hostArgumentSlot++];
        }
        hostArgumentSlot++;
        return va_arg(*hostStackArguments, u64);
    };

    size_t guest_argc = 0;
    u32 guest_args[20];
    if(registered) {
        guest_args[guest_argc++] = guestSelf;
        guest_args[guest_argc++] = guestCommand;
    }

    int nargs = method_getNumberOfArguments(method);
    // The generic trampoline has six logical host argument positions. Structs
    // may expand those into extra raw GPR slots (and at most one supported
    // stack argument); broader stack/FP signatures need typed trampolines.
    if(registered) {
        assert(nargs <= 8);
    } else if(nargs > 8 || nargs - 2 >
                  LC32_GUEST_BLOCK_CALLBACK_MAX_ARGUMENTS) {
        callbackFailure = "too many arguments for the callback executor";
    }
    callback.argumentCount = (uint32_t)(nargs - 2);
    for(int i = 2; i < nargs; i++) {
        if(!registered && callbackFailure) break;
        char *argType = method_copyArgumentType(method, i);
        const char *unqualifiedType = argType;
        while(*unqualifiedType && strchr("rnNoORVA", *unqualifiedType)) {
            unqualifiedType++;
        }

        const bool isNSRange =
            !strncmp(unqualifiedType, "{_NSRange=",
                     sizeof("{_NSRange=") - 1) ||
            !strncmp(unqualifiedType, "{NSRange=",
                     sizeof("{NSRange=") - 1);
        if(isNSRange) {
            /*
             * AAPCS64 never splits an aggregate between registers and the
             * stack. If only x7 remains, it is unused and both NSUInteger
             * fields begin on the stack.
             */
            if(hostArgumentSlot < hostRegisterArgumentCount &&
                    hostRegisterArgumentCount - hostArgumentSlot < 2) {
                hostArgumentSlot = hostRegisterArgumentCount;
            }
            const u64 location = nextHostArgument();
            const u64 length = nextHostArgument();
            if(registered) {
                assert(guest_argc + 2 <=
                       sizeof(guest_args) / sizeof(*guest_args));
                guest_args[guest_argc++] = (u32)location;
                guest_args[guest_argc++] = (u32)length;
            } else if(!callbackFailure) {
                LC32GuestBlockCallbackArgument &argument =
                    callback.arguments[i - 2];
                argument.kind = LC32GuestBlockValueRange;
                argument.value = location;
                argument.value2 = length;
            }
        } else {
            const u64 hostArgument = nextHostArgument();
            if(unqualifiedType[0] == '@' &&
               unqualifiedType[1] != '?') {
                LC32TraceNativeNetworkObject(
                    "host->guest", _cmd, (unsigned int)(i - 2),
                    (id)hostArgument);
            }
            if(registered) {
                assert(guest_argc <
                       sizeof(guest_args) / sizeof(*guest_args));
                const char *selectorName = sel_getName(_cmd);
                if(unqualifiedType[0] == '^' &&
                        !(unqualifiedType[1] == 'v' &&
                          hostArgument == (u64)(u32)hostArgument) &&
                        strncmp(unqualifiedType, "^{_NSZone=",
                                sizeof("^{_NSZone=") - 1) &&
                        strncmp(unqualifiedType, "^{NSZone=",
                                sizeof("^{NSZone=") - 1)) {
                    fprintf(stderr,
                        "LC32: cannot marshal host pointer argument %d "
                        "(%s) for selector %s (value=0x%llx)\n",
                        i - 2, unqualifiedType,
                        selectorName ? selectorName : "<null>",
                        (unsigned long long)hostArgument);
                }
                guest_args[guest_argc++] = LC32HostToGuestArgument(
                    argType, hostArgument);
            } else if(!callbackFailure) {
                LC32GuestBlockCallbackArgument &argument =
                    callback.arguments[i - 2];
                switch(unqualifiedType[0]) {
                    case '@':
                        /* A native block is not an ordinary mirrored object:
                         * it needs the block bridge's invoke/signature
                         * metadata before guest code can call it.  Diagnose
                         * that unsupported callback ABI instead of creating
                         * an unusable NSObject proxy for the block. */
                        if(unqualifiedType[1] == '?') {
                            callbackFailure =
                                "unsupported block argument";
                            break;
                        }
                        [[fallthrough]];
                    case '#': {
                        argument.kind = LC32GuestBlockValueObject;
                        id object = reinterpret_cast<id>(
                            static_cast<uintptr_t>(hostArgument));
                        argument.value = reinterpret_cast<u64>(
                            objc_retain(object));
                        if(object) callbackObjects.push_back(object);
                        break;
                    }
                    case 'B':
                    case 'c':
                        argument.kind = LC32GuestBlockValueSignedChar;
                        argument.value = hostArgument;
                        break;
                    case 'i':
                    case 'l':
                    case 's':
                        argument.kind = LC32GuestBlockValueSigned32;
                        argument.value = hostArgument;
                        break;
                    case 'C':
                    case 'I':
                    case 'L':
                    case 'S':
                        argument.kind = LC32GuestBlockValueUnsigned32;
                        argument.value = hostArgument;
                        break;
                    case 'q':
                        argument.kind = LC32GuestBlockValueSigned64;
                        argument.value = hostArgument;
                        break;
                    case 'Q':
                        argument.kind = LC32GuestBlockValueUnsigned64;
                        argument.value = hostArgument;
                        break;
                    case ':': {
                        argument.kind = LC32GuestBlockValueUnsigned32;
                        SEL selector = reinterpret_cast<SEL>(
                            static_cast<uintptr_t>(hostArgument));
                        argument.value = selector
                            ? LC32LookupGuestSelectorMapping(selector)
                            : 0;
                        if(selector && !argument.value) {
                            callbackFailure =
                                "unmapped selector argument";
                        }
                        break;
                    }
                    case '^':
                        argument.kind = LC32GuestBlockValueUnsigned32;
                        if(unqualifiedType[1] == 'v' &&
                                hostArgument == (u64)(u32)hostArgument) {
                            argument.value = hostArgument;
                        } else if(!strncmp(unqualifiedType, "^{_NSZone=",
                                      sizeof("^{_NSZone=") - 1) ||
                                  !strncmp(unqualifiedType, "^{NSZone=",
                                      sizeof("^{NSZone=") - 1)) {
                            argument.value = 0;
                        } else {
                            callbackFailure =
                                "unsupported pointer argument";
                        }
                        break;
                    default:
                        callbackFailure = "unsupported argument type";
                        break;
                }
            }
        }
        free(argType);
    }

    if(!registered) {
        bool submitted = false;
        if(!callbackFailure) {
            id retainedReceiver = objc_retain(self);
            submitted =
                Dynarmic_submit_guest_selector_callback(&callback);
            objc_release(retainedReceiver);
            if(!submitted) callbackFailure = "callback executor rejected it";
        }
        for(id object : callbackObjects) objc_release(object);
        if(callbackFailure) {
            fprintf(stderr,
                "LC32: cannot relay foreign-thread guest callback "
                "%c[%s %s]: %s\n",
                object_isClass(self) ? '+' : '-',
                self ? class_getName(object_getClass(self)) : "(null)",
                _cmd ? sel_getName(_cmd) : "(null)",
                callbackFailure);
        }
        return 0;
    }
    return guest_objc_msgSend((int)guest_argc, guest_args);
}

u64 LC32InvokeGuestSelector(id self, SEL _cmd, u64 arg2, u64 arg3,
                            u64 arg4, u64 arg5, u64 arg6, u64 arg7, ...) {
    va_list hostStackArguments;
    va_start(hostStackArguments, arg7);
    Method method = nullptr;
    const u64 guest_result = LC32InvokeGuestSelectorRaw(
        self, _cmd, arg2, arg3, arg4, arg5, arg6, arg7,
        &hostStackArguments, &method);
    va_end(hostStackArguments);
    if(!method) return 0;

    char *returnType = method_copyReturnType(method);
    u64 host_result = LC32GuestToHostReturnType(returnType, guest_result);
    free(returnType);
    return host_result;
}

static float LC32InvokeGuestSelectorGuestFloatHostFloat(
        id self, SEL _cmd, u64 arg2, u64 arg3, u64 arg4,
        u64 arg5, u64 arg6, u64 arg7, ...) {
    va_list hostStackArguments;
    va_start(hostStackArguments, arg7);
    const u64 guestResult = LC32InvokeGuestSelectorRaw(
        self, _cmd, arg2, arg3, arg4, arg5, arg6, arg7,
        &hostStackArguments, nullptr);
    va_end(hostStackArguments);
    return LC32GuestFloatReturn(guestResult);
}

static double LC32InvokeGuestSelectorGuestFloatHostDouble(
        id self, SEL _cmd, u64 arg2, u64 arg3, u64 arg4,
        u64 arg5, u64 arg6, u64 arg7, ...) {
    va_list hostStackArguments;
    va_start(hostStackArguments, arg7);
    const u64 guestResult = LC32InvokeGuestSelectorRaw(
        self, _cmd, arg2, arg3, arg4, arg5, arg6, arg7,
        &hostStackArguments, nullptr);
    va_end(hostStackArguments);
    return (double)LC32GuestFloatReturn(guestResult);
}

static float LC32InvokeGuestSelectorGuestDoubleHostFloat(
        id self, SEL _cmd, u64 arg2, u64 arg3, u64 arg4,
        u64 arg5, u64 arg6, u64 arg7, ...) {
    va_list hostStackArguments;
    va_start(hostStackArguments, arg7);
    const u64 guestResult = LC32InvokeGuestSelectorRaw(
        self, _cmd, arg2, arg3, arg4, arg5, arg6, arg7,
        &hostStackArguments, nullptr);
    va_end(hostStackArguments);
    return (float)LC32GuestDoubleReturn(guestResult);
}

static double LC32InvokeGuestSelectorGuestDoubleHostDouble(
        id self, SEL _cmd, u64 arg2, u64 arg3, u64 arg4,
        u64 arg5, u64 arg6, u64 arg7, ...) {
    va_list hostStackArguments;
    va_start(hostStackArguments, arg7);
    const u64 guestResult = LC32InvokeGuestSelectorRaw(
        self, _cmd, arg2, arg3, arg4, arg5, arg6, arg7,
        &hostStackArguments, nullptr);
    va_end(hostStackArguments);
    return LC32GuestDoubleReturn(guestResult);
}

/*
 * Selector names are process-global, while the same selector can refer to a
 * different backing ivar in every class.  Keep each synthetic accessor bound
 * to the class where it was installed and walk the receiver's superclass
 * chain for inherited accessors.
 */
struct LC32GuestIvarBinding {
    std::string name;
    u32 offset = 0;
    char type = '\0';
};

static std::mutex LC32GuestIvarGetterMutex;
static std::unordered_map<Class,
    std::unordered_map<SEL, LC32GuestIvarBinding>>
    LC32GuestIvarBindings;

static void LC32RegisterGuestIvarAccessor(Class cls, SEL selector,
                                           const char *ivarName,
                                           u32 offset, char type) {
    if(!cls || !selector || !ivarName || !*ivarName || !type) return;
    std::lock_guard<std::mutex> lock(LC32GuestIvarGetterMutex);
    LC32GuestIvarBindings[cls][selector] = {
        std::string(ivarName), offset, type
    };
}

static bool LC32GuestIvarBindingForReceiver(
        id receiver, SEL selector, LC32GuestIvarBinding *result) {
    if(!receiver || !selector || !result) return false;
    std::lock_guard<std::mutex> lock(LC32GuestIvarGetterMutex);
    for(Class cls = object_getClass(receiver); cls;
            cls = class_getSuperclass(cls)) {
        auto classIt = LC32GuestIvarBindings.find(cls);
        if(classIt == LC32GuestIvarBindings.end()) continue;
        auto bindingIt = classIt->second.find(selector);
        if(bindingIt == classIt->second.end()) continue;
        *result = bindingIt->second;
        return true;
    }
    return false;
}

static u64 LC32ReadGuestScalarIvar(
        u32 guestObject, const LC32GuestIvarBinding &binding) {
    auto *callbacks = Dynarmic_current_user_callbacks();
    const u32 address = guestObject + binding.offset;
    switch(binding.type) {
        case 'B':
        case 'C': return callbacks->MemoryRead8(address);
        case 'c': return (u64)(int64_t)(int8_t)
            callbacks->MemoryRead8(address);
        case 'S': return callbacks->MemoryRead16(address);
        case 's': return (u64)(int64_t)(int16_t)
            callbacks->MemoryRead16(address);
        case 'I':
        case 'L': return callbacks->MemoryRead32(address);
        case 'i':
        case 'l': return (u64)(int64_t)(int32_t)
            callbacks->MemoryRead32(address);
        case 'Q': return callbacks->MemoryRead64(address);
        case 'q': return (u64)(int64_t)callbacks->MemoryRead64(address);
        default: return 0;
    }
}

static void LC32WriteGuestScalarIvar(
        u32 guestObject, const LC32GuestIvarBinding &binding, u64 value) {
    auto *callbacks = Dynarmic_current_user_callbacks();
    const u32 address = guestObject + binding.offset;
    switch(binding.type) {
        case 'B':
        case 'C':
        case 'c': callbacks->MemoryWrite8(address, (u8)value); break;
        case 'S':
        case 's': callbacks->MemoryWrite16(address, (u16)value); break;
        case 'I':
        case 'L':
        case 'i':
        case 'l': callbacks->MemoryWrite32(address, (u32)value); break;
        case 'Q':
        case 'q': callbacks->MemoryWrite64(address, value); break;
        default: break;
    }
}

void LC32SetGuestScalarIvar(id self, SEL _cmd, u64 value) {
    LC32GuestIvarBinding binding;
    if(!LC32GuestIvarBindingForReceiver(self, _cmd, &binding)) return;
    LC32WriteGuestScalarIvar([self guest_self], binding, value);
}

void LC32SetGuestNSObjectIvar(id self, SEL _cmd, id value) {
    LC32GuestIvarBinding binding;
    if(!LC32GuestIvarBindingForReceiver(self, _cmd, &binding)) return;
    guest_object_setInstanceVariable([self guest_self], binding.name.c_str(),
                                     (u32)(u64)[value guest_self]);
}

id LC32GetGuestNSObjectIvar(id self, SEL _cmd) {
    LC32GuestIvarBinding binding;
    if(!LC32GuestIvarBindingForReceiver(self, _cmd, &binding)) return nil;
    u32 guestValue = 0;
    guest_object_getInstanceVariable([self guest_self], binding.name.c_str(),
                                     &guestValue);
    if(!guestValue) return nil;
    /* Resolve the guest object to its native peer (creating one if needed). */
    return (id)LC32GuestToHostReturnType((char *)"@", guestValue);
}

u64 LC32GetGuestScalarIvar(id self, SEL _cmd) {
    LC32GuestIvarBinding binding;
    if(!LC32GuestIvarBindingForReceiver(self, _cmd, &binding)) return 0;
    return LC32ReadGuestScalarIvar([self guest_self], binding);
}

u32 guest_dlsym(const char *host_name) {
    DynarmicGuestStackString guest_name(host_name);
    u32 args[] = {(u32)(u64)RTLD_DEFAULT, guest_name.guestPtr};
    return LC32InvokeGuestC(sharedHandle.guest_dlsym, false, sizeof(args)/sizeof(*args), args);
}

u32 guest_free(u32 guest_ptr) {
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "free");
    if(!guestPtr) {
        fprintf(stderr,
            "LC32: cannot release guest allocation because free is missing\n");
        return 0;
    }
    u32 args[] = {guest_ptr};
    return LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
}

// These class_copy*List shims are pretty much the same
u32 guest_class_copyIvarList(u32 guest_cls, unsigned int *outCount) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_copyIvarList");
    u32 guest_outCount = threadHandle.jit->Regs()[Reg::SP] -= sizeof(u32);
    u32 args[] = {guest_cls, guest_outCount};
    u32 result = LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
    *outCount = Dynarmic_current_user_callbacks()->MemoryRead32(guest_outCount);
    threadHandle.jit->Regs()[Reg::SP] += sizeof(u32);
    return result;
}
u32 guest_class_copyMethodList(u32 guest_cls, unsigned int *outCount) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_copyMethodList");
    u32 guest_outCount = threadHandle.jit->Regs()[Reg::SP] -= sizeof(u32);
    u32 args[] = {guest_cls, guest_outCount};
    u32 result = LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
    *outCount = Dynarmic_current_user_callbacks()->MemoryRead32(guest_outCount);
    threadHandle.jit->Regs()[Reg::SP] += sizeof(u32);
    return result;
}
u32 guest_class_copyProtocolList(u32 guest_cls, unsigned int *outCount) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_copyProtocolList");
    u32 guest_outCount = threadHandle.jit->Regs()[Reg::SP] -= sizeof(u32);
    u32 args[] = {guest_cls, guest_outCount};
    u32 result = LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
    *outCount = Dynarmic_current_user_callbacks()->MemoryRead32(guest_outCount);
    threadHandle.jit->Regs()[Reg::SP] += sizeof(u32);
    return result;
}

u32 guest_class_createInstance(u32 guest_cls, u32 extraBytes) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_createInstance");
    u32 args[] = {guest_cls, extraBytes};
    return LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
}

u32 guest_class_getClassMethod(u32 guest_cls, u32 guest_sel) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_getClassMethod");
    u32 args[] = {guest_cls, guest_sel};
    return LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
}

u32 guest_class_getInstanceMethod(u32 guest_cls, u32 guest_sel) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_getInstanceMethod");
    u32 args[] = {guest_cls, guest_sel};
    return LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
}

u32 guest_class_getName(u32 guest_cls) {
    if(!threadHandle.jit) return false;
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "class_getName");
    u32 args[] = {guest_cls};
    return LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
}

u32 guest_class_getSuperclass(u32 guest_cls) {
    if(!guest_cls) return 0;
    return Dynarmic_current_user_callbacks()->MemoryRead32(guest_cls + 4);
}

u32 guest_ivar_getName(u32 guest_ivar) {
    return Dynarmic_current_user_callbacks()->MemoryRead32(guest_ivar + sizeof(u32[1]));
}

u32 guest_ivar_getTypeEncoding(u32 guest_ivar) {
    return Dynarmic_current_user_callbacks()->MemoryRead32(guest_ivar + sizeof(u32[2]));
}

u32 guest_object_getClass(u32 guest_obj) {
    if(!guest_obj) return 0;
    return Dynarmic_current_user_callbacks()->MemoryRead32(guest_obj);
}

u32 guest_object_setInstanceVariable(u32 guest_obj, const char *host_name, u32 newValue) {
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(
        cache, "object_setInstanceVariable");
    DynarmicGuestStackString guest_name(host_name);
    u32 args[] = {guest_obj, guest_name.guestPtr, newValue};
    return LC32InvokeGuestC(guestPtr, false, sizeof(args)/sizeof(*args), args);
}

u32 guest_object_getInstanceVariable(u32 guest_obj, const char *host_name, u32 *outValue) {
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(
        cache, "object_getInstanceVariable");
    DynarmicGuestStackString guest_name(host_name);
    /* ARM32 object_getInstanceVariable writes one pointer-sized value; zero
     * the whole slot so unread upper bytes never leak stack garbage. */
    u32 guest_outValue = threadHandle.jit->Regs()[Reg::SP] -= sizeof(u64);
    const u64 zero = 0;
    Dynarmic_mem_1write(guest_outValue, sizeof(zero),
                        const_cast<char *>(
                            reinterpret_cast<const char *>(&zero)));
    u32 args[] = {guest_obj, guest_name.guestPtr, guest_outValue};
    const u32 result = LC32InvokeGuestC(
        guestPtr, false, sizeof(args)/sizeof(*args), args);
    if(outValue) {
        *outValue = Dynarmic_current_user_callbacks()->MemoryRead32(
            guest_outValue);
    }
    threadHandle.jit->Regs()[Reg::SP] += sizeof(u64);
    return result;
}

u32 guest_protocol_getName(u32 guest_protocol) {
    return Dynarmic_current_user_callbacks()->MemoryRead32(guest_protocol + sizeof(u32[1]));
}

namespace {

struct LC32GuestSelectorRegistry {
    std::mutex mutex;
    std::unordered_map<SEL, u32> selectors;
};

struct LC32GuestClassRegistry {
    std::mutex mutex;
    std::unordered_map<std::string, u32> classes;
};

static LC32GuestSelectorRegistry& LC32GuestSelectorRegistryInstance() {
    /* Guest and host selectors are permanent for the process.  Keep this
     * registry alive through shutdown as well: native thread teardown can
     * still release bridged objects after LC32RunGuest has returned. */
    static auto *registry = new LC32GuestSelectorRegistry;
    return *registry;
}

void LC32RegisterGuestSelectorMapping(
        SEL hostSelector, u32 guestSelector) {
    if(!hostSelector || !guestSelector) return;
    LC32GuestSelectorRegistry &registry =
        LC32GuestSelectorRegistryInstance();
    std::lock_guard<std::mutex> lock(registry.mutex);
    const auto result = registry.selectors.emplace(
        hostSelector, guestSelector);
    if(!result.second && result.first->second != guestSelector) {
        fprintf(stderr,
            "LC32: conflicting guest selectors for %s: 0x%x and 0x%x\n",
            sel_getName(hostSelector), result.first->second,
            guestSelector);
    }
}

u32 LC32LookupGuestSelectorMapping(SEL hostSelector) {
    if(!hostSelector) return 0;
    LC32GuestSelectorRegistry &registry =
        LC32GuestSelectorRegistryInstance();
    std::lock_guard<std::mutex> lock(registry.mutex);
    const auto iterator = registry.selectors.find(hostSelector);
    return iterator != registry.selectors.end()
        ? iterator->second
        : 0;
}

static LC32GuestClassRegistry& LC32GuestClassRegistryInstance() {
    /* Objective-C class objects, like selectors, are never deallocated. */
    static auto *registry = new LC32GuestClassRegistry;
    return *registry;
}

}  // namespace

u32 guest_sel_registerName(const char *host_name) {
    if(!host_name) return 0;

    const SEL hostSelector = sel_registerName(host_name);
    const u32 existing = LC32LookupGuestSelectorMapping(hostSelector);
    if(existing) return existing;
    if(!Dynarmic_guest_thread_is_registered()) return 0;

    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "sel_registerName");
    if(!guestPtr) return 0;
    DynarmicGuestStackString guest_name(host_name);
    u32 args[] = {guest_name.guestPtr};
    const u32 guestSelector = LC32InvokeGuestC(
        guestPtr, false, sizeof(args)/sizeof(*args), args);
    if(!guestSelector) return 0;

    /* Never hold the registry across guest execution: registering a selector
     * can synchronously re-enter the bridge.  Concurrent registration of the
     * same name is harmless and must resolve to the same permanent selector. */
    LC32RegisterGuestSelectorMapping(hostSelector, guestSelector);
    return LC32LookupGuestSelectorMapping(hostSelector);
}

//if(!guestPtr) guestPtr = guest_dlsym("LC32TestHostToGuestCall");
//u32 args[] = {0x40404040, 0x41414141, 0x42424242, 0x43434343, 0x44444444, 0x45454545, 0x46464646, 0x47474747};
u32 guest_objc_getClass(const char *name) {
    if(!name || !threadHandle.jit) return 0;

    LC32GuestClassRegistry &registry =
        LC32GuestClassRegistryInstance();
    {
        std::lock_guard<std::mutex> lock(registry.mutex);
        const auto iterator = registry.classes.find(name);
        if(iterator != registry.classes.end()) return iterator->second;
    }

    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "objc_getClass");
    if(!guestPtr) return 0;

    DynarmicGuestStackString guest_name(name);
    u32 args[] = {guest_name.guestPtr};
    const u32 guestClass = LC32InvokeGuestC(
        guestPtr, false, sizeof(args)/sizeof(*args), args);
    if(!guestClass) return 0;

    /* Cache only successful lookups. A class which is absent now may still be
     * registered later by a guest image or dynamic resolver. */
    std::lock_guard<std::mutex> lock(registry.mutex);
    const auto result = registry.classes.emplace(name, guestClass);
    return result.first->second;
}

Class guest_objc_getClass_retHostClass(const char *name) {
    // Get the guest class pointer
    u32 guest_outClass = guest_objc_getClass(name);
    if(!guest_outClass) return nil;

    // Now that we will be recursively resolving subclass
    Class subclass;
    u32 guest_superclass = guest_class_getSuperclass(guest_outClass);
    DynarmicHostString superclassName(guest_class_getName(guest_superclass));
    subclass = objc_getClass(superclassName.hostPtr);
    if(!subclass) return nil;

    // Now we can construct the class
    Class outClass = objc_allocateClassPair(subclass, name, 0);
    if(!outClass) return nil;
    /*
     * Install custom reference counting before any message can realize this
     * class. libobjc caches whether objc_retain/objc_release may use their
     * root fast paths; adding -release after realization can otherwise let a
     * native ARC release bypass LC32GuestMirrorRelease and strand a live weak
     * mapping to freed storage.
     */
    LC32InstallGuestMirrorReferenceCounting(outClass);
    // set class to class
    [(id)outClass setGuest_self:guest_outClass];
    // set metaclass to metaclass
    [(id)object_getClass(outClass) setGuest_self:guest_object_getClass(guest_outClass)];
    // resolve methods and register a dynamic resolver
    [LC32ObjCMethodResolver registerClass:outClass];
    LC32UIKitPrepareGuestClass(outClass);
    // register to objc
    objc_registerClassPair(outClass);
    [outClass setGuestClass:YES];
    [(id)object_getClass(outClass) setGuestClass:YES];
    return outClass;
}

static u64 LC32GuestObjCMsgSendWithoutDeferredReleases(
        int argc, u32 *args) {
#ifdef LC32_TRACE_GUEST_OBJC_MSGSEND
    if(argc >= 2 && args != nullptr) {
        bool trace = args[1] == 0;
#ifdef LC32_TRACE_GUEST_OBJC_RECEIVER
        trace = trace || args[0] ==
            static_cast<u32>(LC32_TRACE_GUEST_OBJC_RECEIVER);
#endif
        if(trace) {
            fprintf(stderr,
                "LC32 guest objc_msgSend trace: receiver=0x%08x "
                "selector=0x%08x argc=%d host_thread=%u caller=%p\n",
                args[0], args[1], argc,
                pthread_mach_thread_np(pthread_self()),
                __builtin_return_address(0));
        }
    }
#endif
    static std::atomic<u32> cache{0};
    const u32 guestPtr = LC32CachedGuestSymbol(cache, "objc_msgSend");
    return LC32InvokeGuestC(guestPtr, true, argc, args);
}

u64 guest_objc_msgSend(int argc, u32 *args) {
    LC32DrainDeferredGuestPinReleases();
    return LC32GuestObjCMsgSendWithoutDeferredReleases(argc, args);
}

/*
 * Native collections retain a dynamically mirrored host object without
 * touching the ARM32 object's retain count. Keep one guest-only +1 alive for
 * exactly as long as the host mirror rather than trying to mirror every host
 * retain/release. Ordinary guest ownership operations continue to mirror
 * their corresponding native ownership operations.
 */
enum class LC32GuestReleaseKind : uint8_t {
    LifetimePin,
    LogicalOwnership,
    OrdinaryOwnership,
    BlockRuntime,
    GuestMirrorFinalRelease,
};

static bool LC32AdjustGuestReferenceNow(
        u32 guestObject, bool retaining,
        LC32GuestReleaseKind releaseKind =
            LC32GuestReleaseKind::LifetimePin) {
    assert(releaseKind !=
           LC32GuestReleaseKind::GuestMirrorFinalRelease);
    if(releaseKind == LC32GuestReleaseKind::BlockRuntime) {
        assert(!retaining);
        static std::atomic<u32> blockRelease{0};
        const u32 releaseFunction = LC32CachedGuestSymbol(
            blockRelease, "_Block_release");
        if(!releaseFunction) {
            fprintf(stderr,
                "LC32: guest _Block_release is unavailable for 0x%x\n",
                guestObject);
            return false;
        }
        u32 args[] = {guestObject};
        (void)LC32InvokeGuestC(releaseFunction, false,
                              sizeof(args) / sizeof(*args), args);
        return true;
    }

    if(!retaining && releaseKind == LC32GuestReleaseKind::LifetimePin) {
        static std::atomic<u32> lifetimePinRelease{0};
        const u32 releaseFunction = LC32CachedGuestSymbol(
            lifetimePinRelease, "LC32ReleaseGuestLifetimePin");
        if(!releaseFunction) {
            fprintf(stderr,
                "LC32: guest lifetime-pin release is unavailable for 0x%x\n",
                guestObject);
            return false;
        }
        u32 args[] = {guestObject};
        return LC32InvokeGuestC(releaseFunction, false,
                               sizeof(args) / sizeof(*args), args) != 0;
    }

    static std::atomic<u32> retainSelector{0};
    static std::atomic<u32> releaseSelector{0};
    static std::atomic<u32> logicalReleaseSelector{0};
    static std::atomic<u32> ordinaryReleaseSelector{0};
    u32 selector;
    if(retaining) {
        selector = LC32CachedGuestSelector(
            retainSelector, "LC32_retain");
    } else if(releaseKind == LC32GuestReleaseKind::LogicalOwnership) {
        selector = LC32CachedGuestSelector(
            logicalReleaseSelector,
            "LC32_releaseGuestOwnershipOnly");
    } else if(releaseKind == LC32GuestReleaseKind::OrdinaryOwnership) {
        // Public release is the ownership-bridging implementation after the
        // guest NSObject method exchange. It remains guest-only when no peer
        // exists, and also decrements a peer acquired since scheduling.
        selector = LC32CachedGuestSelector(
            ordinaryReleaseSelector, "release");
    } else {
        selector = LC32CachedGuestSelector(
            releaseSelector, "LC32_release");
    }
    u32 args[] = {guestObject, selector};
    /* A lifetime-pin retain can run while the guest holds its publication
     * writer gate. Draining unrelated deferred releases here may destroy an
     * object whose guest allocation hashes to that same gate stripe and
     * deadlock on the interrupted writer. Internal ownership bookkeeping
     * must not introduce unrelated teardown; public callback entries retain
     * their normal deferred-release drain. */
    LC32GuestObjCMsgSendWithoutDeferredReleases(
        sizeof(args) / sizeof(*args), args);
    return true;
}

struct LC32DeferredGuestRelease {
    u32 guestObject;
    LC32GuestReleaseKind kind;
    u64 retainedHostObject;
    u64 weakRegistryGeneration;
};

static std::mutex& LC32DeferredGuestPinReleaseMutex() {
    // Associated objects can be torn down during process shutdown. Intentionally
    // keep this synchronization state alive until the process exits.
    static std::mutex *mutex = new std::mutex;
    return *mutex;
}

static std::vector<LC32DeferredGuestRelease>&
LC32DeferredGuestPinReleases() {
    static std::vector<LC32DeferredGuestRelease> *releases =
        new std::vector<LC32DeferredGuestRelease>;
    return *releases;
}

static thread_local bool LC32DrainingGuestPinReleases;

static void LC32ReleaseGuestReference(
        u32 guestObject, LC32GuestReleaseKind kind,
        id hostObjectToKeepAlive = nil,
        u64 weakRegistryGeneration = 0) {
    if(threadHandle.jit && threadHandle.cb) {
        if(kind == LC32GuestReleaseKind::LifetimePin) {
            LC32RefreshHostWeakMappingRetiringOwner(
                guestObject, weakRegistryGeneration);
        }
        const bool lifetimePinWasFinal =
            LC32AdjustGuestReferenceNow(guestObject, false, kind);
        if(kind == LC32GuestReleaseKind::LifetimePin &&
           lifetimePinWasFinal) {
            /* Guest root -dealloc removed the guest key before freeing its
             * allocation. Native -dealloc may still be unwinding, so defer
             * final destruction of the detached native-address tombstone. */
            LC32DeferHostWeakMappingRetirementFinalization(
                guestObject, weakRegistryGeneration);
        }
        return;
    }
    if(kind == LC32GuestReleaseKind::BlockRuntime &&
            Dynarmic_submit_guest_block_release(guestObject)) {
        return;
    }

    u64 retainedHostObject = 0;
    if(hostObjectToKeepAlive) {
        LC32ObjCRetainWithoutARC(hostObjectToKeepAlive);
        retainedHostObject = (u64)hostObjectToKeepAlive;
    }

    // The guest reference remains held until a registered guest thread drains
    // this entry. Logical ownership releases also keep the native mirror alive
    // so they can safely clear its reverse mapping before releasing it.
    std::lock_guard<std::mutex> lock(
        LC32DeferredGuestPinReleaseMutex());
    LC32DeferredGuestPinReleases().push_back({
        guestObject, kind, retainedHostObject, weakRegistryGeneration,
    });
}

static void LC32ReleaseGuestPin(u32 guestObject,
                                u64 weakRegistryGeneration) {
    LC32ReleaseGuestReference(
        guestObject, LC32GuestReleaseKind::LifetimePin, nil,
        weakRegistryGeneration);
}

static void LC32ReleaseGuestLogicalOwnership(
        u32 guestObject, id hostObject) {
    LC32ReleaseGuestReference(
        guestObject, LC32GuestReleaseKind::LogicalOwnership,
        hostObject);
}

static void LC32ReleaseGuestOrdinaryOwnership(u32 guestObject) {
    LC32ReleaseGuestReference(
        guestObject, LC32GuestReleaseKind::OrdinaryOwnership);
}

static void LC32DeferGuestMirrorFinalRelease(id hostObject) {
    if(!hostObject) return;

    /* The caller transfers the temporary root guard which is now the mirror's
     * final native +1. Do not retain it again: a registered guest thread will
     * detach the lifetime pin first and consume this exact reference last. */
    std::lock_guard<std::mutex> lock(
        LC32DeferredGuestPinReleaseMutex());
    LC32DeferredGuestPinReleases().push_back({
        0, LC32GuestReleaseKind::GuestMirrorFinalRelease,
        (u64)hostObject, 0,
    });
}

extern "C" u32 LC32CopyGuestBlock(u32 guestBlock) {
    if(!guestBlock || !threadHandle.jit || !threadHandle.cb) return 0;

    static std::atomic<u32> blockCopy{0};
    const u32 copyFunction = LC32CachedGuestSymbol(
        blockCopy, "_Block_copy");
    if(!copyFunction) {
        fprintf(stderr,
            "LC32: guest _Block_copy is unavailable for 0x%x\n",
            guestBlock);
        return 0;
    }
    u32 args[] = {guestBlock};
    return (u32)LC32InvokeGuestC(copyFunction, false,
                                sizeof(args) / sizeof(*args), args);
}

extern "C" void LC32ReleaseGuestBlock(u32 guestBlock) {
    if(!guestBlock) return;
    LC32ReleaseGuestReference(
        guestBlock, LC32GuestReleaseKind::BlockRuntime);
}

static void LC32DrainDeferredGuestPinReleases() {
    if(LC32DrainingGuestPinReleases ||
            !threadHandle.jit || !threadHandle.cb) {
        return;
    }

    std::vector<LC32DeferredGuestRelease> pending;
    {
        std::lock_guard<std::mutex> lock(
            LC32DeferredGuestPinReleaseMutex());
        pending.swap(LC32DeferredGuestPinReleases());
    }
    if(pending.empty()) return;

    LC32DrainingGuestPinReleases = true;
    for(const LC32DeferredGuestRelease &release : pending) {
        if(release.kind ==
                LC32GuestReleaseKind::GuestMirrorFinalRelease) {
            LC32RetireGuestMirrorWithTransferredReference(
                (id)release.retainedHostObject);
            continue;
        }
        if(release.kind == LC32GuestReleaseKind::LifetimePin) {
            LC32RefreshHostWeakMappingRetiringOwner(
                release.guestObject, release.weakRegistryGeneration);
        }
        const bool lifetimePinWasFinal = LC32AdjustGuestReferenceNow(
            release.guestObject, false, release.kind);
        if(release.kind == LC32GuestReleaseKind::LifetimePin &&
           lifetimePinWasFinal) {
            LC32FinalizeHostWeakMappingRetirement(
                release.guestObject, release.weakRegistryGeneration);
        }
        if(release.retainedHostObject) {
            LC32ReleaseOwnedHostObject(
                (id)release.retainedHostObject);
        }
    }
    LC32DrainingGuestPinReleases = false;
}

@interface LC32GuestLifetimePin : NSObject {
@public
    u32 guestObject;
    u64 weakRegistryGeneration;
    u64 tracedHostObject;
    const char *tracedClassName;
}
@end

@implementation LC32GuestLifetimePin
- (void)dealloc {
    if(guestObject) {
        LC32MarkHostWeakMappingRetiring(
            guestObject, weakRegistryGeneration,
            LC32HostMappingRetirementProvenance::
                NativePeerDeallocating);
        if(tracedHostObject) {
            LC32OperationTraceDeallocated(
                tracedHostObject, guestObject, tracedClassName);
        }
        LC32ReleaseGuestPin(guestObject, weakRegistryGeneration);
    }
#if !__has_feature(objc_arc)
    [super dealloc];
#endif
}
@end

@interface LC32GuestAutoreleaseToken : NSObject {
@public
    u32 guestObject;
    id hostObject;
}
@end

@implementation LC32GuestAutoreleaseToken
- (void)dealloc {
    if(hostObject) {
        LC32OperationTraceLiveObject(
            "guest-autorelease-drain", hostObject, guestObject);
        // Keep hostObject strongly held while the guest removes its
        // corresponding logical +1 and, if final, clears the host's reverse
        // mapping. The token owns the paired native autorelease operation.
        LC32ReleaseGuestLogicalOwnership(guestObject, hostObject);
    } else {
        /*
         * The object was guest-only when autoreleased. Use an ordinary guest
         * release: it remains local if the object was never bridged, or also
         * decrements a peer created before this token drained.
         */
        LC32ReleaseGuestOrdinaryOwnership(guestObject);
    }
#if !__has_feature(objc_arc)
    LC32ReleaseOwnedHostObject(hostObject);
    [super dealloc];
#endif
}
@end

static void LC32ScheduleGuestAutoreleaseNow(
        id __unsafe_unretained hostObject, u32 guestObject) {
    if(!guestObject) return;

#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: schedule begin guest=0x%08x "
        "host=%p host_thread=%u\n",
        guestObject, hostObject,
        pthread_mach_thread_np(pthread_self()));
#endif

    if(hostObject) {
        LC32OperationTraceLiveObject(
            "guest-autorelease", hostObject, guestObject);
    }

    LC32GuestAutoreleaseToken *token =
        [LC32GuestAutoreleaseToken new];
#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: token allocated guest=0x%08x "
        "token=%p\n",
        guestObject, token);
#endif
    token->guestObject = guestObject;
#if __has_feature(objc_arc)
    token->hostObject = hostObject;
    // Transfer a dedicated +1 to the host autorelease pool. Clearing the ARC
    // local first leaves exactly that transferred ownership outstanding.
    void *retainedToken = (__bridge_retained void *)token;
    token = nil;
    LC32ObjCAutoreleaseWithoutARC((__bridge id)retainedToken);
#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: token queued guest=0x%08x "
        "token=%p\n",
        guestObject, retainedToken);
#endif
#else
    token->hostObject = LC32RetainOwnedHostObject(hostObject);
    [token autorelease];
#endif

    if(hostObject) {
        // The token now owns the mirror's existing guest-paired +1. Its
        // eventual destruction releases guest logical ownership first, then
        // this host +1.
        LC32ReleaseOwnedHostObject(hostObject);
    }
#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: schedule end guest=0x%08x\n",
        guestObject);
#endif
}

extern "C" u32 LC32ScheduleGuestAutorelease(
        u32 hostLow, u32 hostHigh, u32 guestStackPointer) {
    const u64 hostAddress = hostLow | ((u64)hostHigh << 32);
#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: entry host=%p guest_sp=0x%08x "
        "host_thread=%u\n",
        (void *)(uintptr_t)hostAddress, guestStackPointer,
        pthread_mach_thread_np(pthread_self()));
#endif
    // SVC 1002 forwards r2/r3 directly and passes the guest SP as its third
    // native argument. The next ARMv7 vararg (the guest object) is at [SP].
    const u32 guestObject =
        Dynarmic_current_user_callbacks()->MemoryRead32(guestStackPointer);
#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: decoded guest=0x%08x from "
        "guest_sp=0x%08x\n",
        guestObject, guestStackPointer);
#endif
    if(!guestObject) return 0;

    LC32HostInvocationReceiverGuard receiverGuard;
    id hostObject = nil;
    if(hostAddress && !LC32AcquireHostInvocationReceiver(
            hostAddress, receiverGuard, &hostObject, false)) {
        /* A Live registry entry can outlast a native peer whose weak slot has
         * already zeroed. Treat that guest autorelease as guest-only; the final
         * guest release will remove the stale exact generation without ever
         * retaining or releasing the former native address. */
        hostObject = nil;
    }
    LC32ScheduleGuestAutoreleaseNow(hostObject, guestObject);
#ifdef LC32_TRACE_AUTORELEASE
    fprintf(stderr,
        "LC32 autorelease trace: return guest=0x%08x\n",
        guestObject);
#endif
    return 0;
}

static void LC32PinGuestObjectToHost(id hostObject, u32 guestObject,
                                     bool retainGuestObject) {
    if(!hostObject || !guestObject) return;

    @synchronized(hostObject) {
        LC32GuestLifetimePin *existing = objc_getAssociatedObject(
            hostObject, LC32GuestLifetimePinKey);
        if(existing) {
            assert(existing->guestObject == guestObject);
            return;
        }

        if(retainGuestObject) {
            assert(threadHandle.jit && threadHandle.cb);
            LC32AdjustGuestReferenceNow(guestObject, true);
        }

        LC32GuestLifetimePin *pin = [LC32GuestLifetimePin new];
        pin->guestObject = guestObject;
        pin->weakRegistryGeneration =
            LC32RegisterHostWeakMapping(hostObject, guestObject);
        assert(pin->weakRegistryGeneration != 0);
        if(LC32OperationTraceEnabled() &&
           LC32HostObjectIsOperation(hostObject)) {
            pin->tracedHostObject = (u64)hostObject;
            pin->tracedClassName = class_getName(object_getClass(hostObject));
            LC32OperationTraceLiveObject(
                retainGuestObject ? "pin-create-retaining-guest"
                                  : "pin-create-adopting-guest",
                hostObject, guestObject);
        }
        objc_setAssociatedObject(hostObject, LC32GuestLifetimePinKey, pin,
                                 OBJC_ASSOCIATION_RETAIN_NONATOMIC);
#if !__has_feature(objc_arc)
        [pin release];
#endif
    }
}

static std::mutex& LC32GuestMirrorReleaseMutex() {
    /* Native releases of the last two owners may race. Serialize the short
     * root-reference handoff so exactly one caller becomes the retirement
     * owner. Guest destruction itself runs after this lock is released. */
    static std::mutex *mutex = new std::mutex;
    return *mutex;
}

static constexpr const char *LC32GuestMirrorRetiringIvarName =
    "__lc32_host_mirror_retiring_state_7f84d2";

static u8 *LC32GuestMirrorRetiringState(id hostObject) {
    if(!hostObject) return nullptr;
    Ivar ivar = class_getInstanceVariable(
        object_getClass(hostObject), LC32GuestMirrorRetiringIvarName);
    if(!ivar) return nullptr;
    return reinterpret_cast<u8 *>(hostObject) + ivar_getOffset(ivar);
}

static u32 LC32ReleaseNativeProxyOwnership(
        u32 guestObject, u64 hostAddress, bool releaseHostOwnership) {
    std::shared_ptr<LC32HostWeakMappingEntry> entry;
    {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        if(iterator == registry.entries.end() ||
           iterator->second->expectedHostAddress != hostAddress ||
           iterator->second->state != LC32HostWeakMappingState::Live ||
           iterator->second->lifetime != LC32HostMappingLifetime::Pinned ||
           !iterator->second->weakCompatible ||
           !iterator->second->invocationRetainCompatible) {
            return LC32NativeProxyReleaseNotApplicable;
        }
        entry = iterator->second;
    }

    const auto reject = [&]() -> u32 {
        fprintf(stderr,
            "LC32: cannot release native proxy guest=0x%x host=0x%llx "
            "generation=%llu without a live matching lifetime pin\n",
            guestObject, (unsigned long long)hostAddress,
            (unsigned long long)entry->generation);
        return LC32NativeProxyReleaseRejected;
    };

    /* Never inspect a raw native address before an atomic weak promotion.
     * The lease also keeps native deallocation from consuming the guest pin
     * while we decide which part of the guest root count may be released. */
    id __unsafe_unretained hostObject = LC32LoadWeakRetainedHostObject(
        &entry->weakHostObject, (id)(uintptr_t)hostAddress);
    if(!hostObject) return reject();
    LC32HostInvocationReceiverGuard nativeGuard;
    (void)nativeGuard.adoptRetained(hostObject);

    /* Synthesized guest classes retain their existing coordinated teardown
     * contract. This path is only for ordinary native framework objects. */
    if(LC32GuestMirrorRetiringState(hostObject)) {
        return LC32NativeProxyReleaseNotApplicable;
    }

    static std::atomic<u32> releasePrimitive{0};
    const u32 guestRelease = LC32CachedGuestSymbol(
        releasePrimitive, "LC32ReleaseGuestNativeProxyOwnership");
    if(!guestRelease) return reject();

    const auto mappingIsCurrent = [&]() {
        LC32HostWeakRegistry &registry = LC32HostWeakMappings();
        std::lock_guard<std::mutex> lock(registry.mutex);
        auto iterator = registry.entries.find(guestObject);
        return iterator != registry.entries.end() &&
            iterator->second.get() == entry.get() &&
            iterator->second->generation == entry->generation &&
            entry->expectedHostAddress == hostAddress &&
            entry->state == LC32HostWeakMappingState::Live &&
            entry->lifetime == LC32HostMappingLifetime::Pinned &&
            entry->retirementProvenance ==
                LC32HostMappingRetirementProvenance::None;
    };

    /* Use the public peer monitor only to acquire the matching pin. Taking
     * a guest SideTable lock while holding that monitor could deadlock with
     * a guest weak load whose native -retainWeakReference uses the monitor.
     * The pin lease also delays its -dealloc if an association is detached
     * while this release is in flight. Neither lease is destroyed in a lock. */
    LC32HostInvocationReceiverGuard pinGuard;
    LC32GuestLifetimePin *__unsafe_unretained pin = nil;
    @synchronized(hostObject) {
        if(!mappingIsCurrent()) return reject();
        pin = objc_getAssociatedObject(
            hostObject, LC32GuestLifetimePinKey);
        if(!pin || pin->guestObject != guestObject ||
           pin->weakRegistryGeneration != entry->generation) return reject();
        (void)pinGuard.adoptRetained(LC32RetainOwnedHostObject(pin));
    }

    {
        std::lock_guard<std::mutex> lock(entry->nativeProxyReleaseMutex);
        if(!mappingIsCurrent() || pin->guestObject != guestObject ||
           pin->weakRegistryGeneration != entry->generation) return reject();
        /* Serialize only the tiny root-counter primitive. Unlike public
         * guest_objc_msgSend, LC32InvokeGuestC does not opportunistically drain
         * unrelated releases, whose callbacks could reenter this private gate.
         * No peer monitor or registry lock spans this bounded guest call. */
        u32 args[] = {guestObject};
        if(!LC32InvokeGuestC(guestRelease, false, 1, args)) return reject();
    }

    /* A +0 native result can have owners invisible to the guest root count.
     * An old caller's excess guest release must still reach native reference
     * counting; it must not destroy the proxy while those owners remain.
     * This does not make native over-release safe: the caller still needs a
     * real native +1 to consume. Neither native release nor final lease
     * destruction may run under the private gate above. */
    if(releaseHostOwnership) {
        LC32GuestHostCallQuiescence quiescence;
        LC32ReleaseOwnedHostObject(hostObject);
        quiescence.finish();
    }
    return LC32NativeProxyReleaseHandled;
}

static bool LC32GuestMirrorIsRetiring(id hostObject) {
    u8 *state = LC32GuestMirrorRetiringState(hostObject);
    return state && __atomic_load_n(state, __ATOMIC_ACQUIRE) != 0;
}

static void LC32SetGuestMirrorRetiring(id hostObject, bool retiring) {
    u8 *state = LC32GuestMirrorRetiringState(hostObject);
    assert(state);
    __atomic_store_n(state, retiring ? 1 : 0, __ATOMIC_RELEASE);
}

static bool LC32GuestMirrorPinSnapshot(
        id hostObject, u32 *guestObject, u64 *generation) {
    LC32GuestLifetimePin *pin = objc_getAssociatedObject(
        hostObject, LC32GuestLifetimePinKey);
    if(!pin || !pin->guestObject) return false;
    *guestObject = pin->guestObject;
    *generation = pin->weakRegistryGeneration;
    return true;
}

static bool LC32DetachGuestMirrorPin(
        id hostObject, u32 *guestObject, u64 *generation,
        u64 *tracedHostObject, const char **tracedClassName) {
    @synchronized(hostObject) {
        LC32GuestLifetimePin *pin = objc_getAssociatedObject(
            hostObject, LC32GuestLifetimePinKey);
        if(!pin || !pin->guestObject) return false;

        *guestObject = pin->guestObject;
        *generation = pin->weakRegistryGeneration;
        *tracedHostObject = pin->tracedHostObject;
        *tracedClassName = pin->tracedClassName;

        /* Disarm the associated object's ordinary -dealloc path. Its release
         * may be autorelease-delayed by objc_getAssociatedObject, whereas the
         * coordinated guest release below must finish under the host guard. */
        pin->guestObject = 0;
        pin->weakRegistryGeneration = 0;
        pin->tracedHostObject = 0;
        pin->tracedClassName = nullptr;
        objc_setAssociatedObject(hostObject, LC32GuestLifetimePinKey, nil,
                                 OBJC_ASSOCIATION_RETAIN_NONATOMIC);
        return true;
    }
}

static void LC32ClearGuestSelfIfEqualWhileSynchronized(
        id hostObject, u32 expectedGuestObject) {
    NSNumber *mappedGuestSelf =
        objc_getAssociatedObject(hostObject, kGuestSelf);
    if(mappedGuestSelf.unsignedLongLongValue != expectedGuestObject) {
        return;
    }
    LC32OperationTraceLiveObject(
        "reverse-map-clear", hostObject, expectedGuestObject);
    objc_setAssociatedObject(hostObject, kGuestSelf, nil,
                             OBJC_ASSOCIATION_RETAIN_NONATOMIC);
}

static void LC32ClearGuestSelfIfEqual(id hostObject,
                                      u32 expectedGuestObject) {
    @synchronized(hostObject) {
        LC32ClearGuestSelfIfEqualWhileSynchronized(
            hostObject, expectedGuestObject);
    }
}

class LC32TransferredRootReference {
public:
    explicit LC32TransferredRootReference(id object) : object(object) {}
    ~LC32TransferredRootReference() {
        releaseNow();
    }

    LC32TransferredRootReference(const LC32TransferredRootReference&) = delete;
    LC32TransferredRootReference& operator=(
        const LC32TransferredRootReference&) = delete;

    id relinquish() {
        id transferred = object;
        object = nil;
        return transferred;
    }

    void releaseNow() {
        if(!object) return;
        id releasedObject = object;
        object = nil;
        LC32GuestHostCallQuiescence quiescence;
        _objc_rootRelease(releasedObject);
        quiescence.finish();
    }

private:
    id object;
};

static void LC32RetireGuestMirrorWithTransferredReference(id hostObject) {
    if(!hostObject) return;
    LC32TransferredRootReference nativeGuard(hostObject);

    u32 guestObject = 0;
    u64 generation = 0;
    id retryReference = nil;
    {
        std::lock_guard<std::mutex> lock(
            LC32GuestMirrorReleaseMutex());
        if(!LC32GuestMirrorPinSnapshot(
                hostObject, &guestObject, &generation)) {
            retryReference = nativeGuard.relinquish();
        } else if(_objc_rootRetainCount(hostObject) != 1) {
            /* A foreign-thread deferral may give an existing owner time to
             * retain the mirror. Restore publication, then drop this queued
             * reference through the coordinated release path: a racing owner
             * can otherwise disappear after this lock is released and make a
             * raw root release become the uncoordinated final release. */
            LC32SetGuestMirrorRetiring(hostObject, false);
            LC32RestoreHostWeakMappingLive(guestObject, generation);
            retryReference = nativeGuard.relinquish();
        }
    }
    if(retryReference) {
        LC32GuestMirrorReleaseImplementation(retryReference);
        return;
    }

    u64 tracedHostObject = 0;
    const char *tracedClassName = nullptr;
    if(!LC32DetachGuestMirrorPin(
            hostObject, &guestObject, &generation,
            &tracedHostObject, &tracedClassName)) {
        /* The snapshot above proved that a pin existed. If another teardown
         * path consumed it first, let native deallocation complete and leave
         * the exact generation quarantined instead of decrementing a guest
         * reference whose ownership can no longer be proven. */
        fprintf(stderr,
            "LC32: retiring guest mirror 0x%llx lost its lifetime pin\n",
            (unsigned long long)(u64)hostObject);
        LC32MarkHostWeakMappingRetiring(
            guestObject, generation,
            LC32HostMappingRetirementProvenance::NativePeerDeallocating);
        nativeGuard.releaseNow();
        return;
    }

    /* This may run arbitrary guest -dealloc code. Keep both the native root
     * guard and reverse mapping intact until it returns, so messages sent on
     * `self` during teardown still resolve to this live native mirror. The pin
     * was detached and disarmed first so its guest +1 is consumed exactly once
     * whether or not another guest owner remains. */
    LC32RefreshHostWeakMappingRetiringOwner(
        guestObject, generation);
    const bool lifetimePinWasFinal = LC32AdjustGuestReferenceNow(
        guestObject, false, LC32GuestReleaseKind::LifetimePin);
    if(tracedHostObject) {
        LC32OperationTraceDeallocated(
            tracedHostObject, guestObject, tracedClassName);
    }
    LC32ClearGuestSelfIfEqual(hostObject, guestObject);
    LC32MarkHostWeakMappingRetiring(
        guestObject, generation,
        lifetimePinWasFinal
            ? LC32HostMappingRetirementProvenance::FinalHostRelease
            : LC32HostMappingRetirementProvenance::NativePeerDeallocating);
    nativeGuard.releaseNow();
    if(lifetimePinWasFinal) {
        LC32FinalizeHostWeakMappingRetirement(guestObject, generation);
    }
}

static BOOL LC32GuestMirrorRetainWeakReference(id self, SEL) {
    /* libobjc invokes this with the object's SideTable lock held.  The root
     * helper is the no-lock try-retain primitive intended for that context;
     * do not take LC32GuestMirrorReleaseMutex here. */
    if(LC32GuestMirrorIsRetiring(self)) return NO;
    if(!_objc_rootTryRetain(self)) return NO;
    if(LC32GuestMirrorIsRetiring(self)) {
        /* Retirement may have been published between the first state load
         * and the no-lock root try-retain. Balance that +1 away from the
         * SideTable critical section and fail the weak load. */
        LC32DeferOwnedHostRelease((void *)self);
        return NO;
    }
    if(!LC32HostOwnershipObservationSuppression::consumeIfMatching(
            self, LC32HostOwnershipOperation::WeakRetain)) {
        /* A successful weak promotion returns a real native +1 whose later
         * release passes through LC32GuestMirrorRelease. Count both halves so
         * an initializer's balanced weak load cannot masquerade as consuming
         * its original receiver ownership. */
        LC32HostInitializerInvocationScope::observeNativeRetain(self);
    }
    return YES;
}

static BOOL LC32GuestMirrorAllowsWeakReference(id self, SEL) {
    /* See LC32GuestMirrorRetainWeakReference: this callback also runs under
     * libobjc's weak-reference lock. */
    if(LC32GuestMirrorIsRetiring(self)) return NO;
    return _objc_rootIsDeallocating(self) ? NO : YES;
}

static id LC32GuestMirrorRetain(id self, SEL) {
    if(!LC32HostOwnershipObservationSuppression::consumeIfMatching(
            self, LC32HostOwnershipOperation::Retain)) {
        LC32HostInitializerInvocationScope::observeNativeRetain(self);
    }
    return _objc_rootRetain(self);
}

static void LC32GuestMirrorReleaseImplementation(
        id self, unsigned ownedReferenceCount) {
    if(!self) return;
    assert(ownedReferenceCount != 0);

    std::unique_lock<std::mutex> lock(
        LC32GuestMirrorReleaseMutex());
    if(LC32GuestMirrorIsRetiring(self)) {
        /* Each transferred reference is an exact native +1. Deferred retirement
         * guards are transferred directly to Retire and never enter here. Drop
         * these references outside the global release mutex: the last may run
         * native teardown recursively. */
        lock.unlock();
        for(unsigned index = 0; index < ownedReferenceCount; index++) {
            _objc_rootRelease(self);
        }
        return;
    }

    /* Hold a temporary root reference while consuming every exact +1 supplied
     * by the caller. The special guest-release path supplies both its paired
     * native ownership and the invocation guard; ordinary callers supply one.
     * If another real owner remains, drop the temporary and finish normally. If
     * it is now the sole owner, transfer it to coordinated retirement. */
    _objc_rootRetain(self);
    for(unsigned index = 0; index < ownedReferenceCount; index++) {
        _objc_rootRelease(self);
    }
    if(_objc_rootRetainCount(self) != 1) {
        _objc_rootRelease(self);
        return;
    }

    u32 guestObject = 0;
    u64 generation = 0;
    if(!LC32GuestMirrorPinSnapshot(
            self, &guestObject, &generation)) {
        lock.unlock();
        _objc_rootRelease(self);
        return;
    }

    LC32SetGuestMirrorRetiring(self, true);
    LC32MarkHostWeakMappingRetiring(
        guestObject, generation,
        LC32HostMappingRetirementProvenance::
            GuestMirrorWithTransferredReference);
    /* A native weak load which entered before the retiring marker may have
     * completed its root try-retain. Recheck after publication while the
     * release handoff is still serialized; later weak loads are rejected by
     * the custom weak-RR methods below. */
    if(_objc_rootRetainCount(self) != 1) {
        LC32SetGuestMirrorRetiring(self, false);
        LC32RestoreHostWeakMappingLive(guestObject, generation);
        _objc_rootRelease(self);
        return;
    }
    lock.unlock();

    if(!Dynarmic_guest_thread_is_registered()) {
        LC32DeferGuestMirrorFinalRelease(self);
        return;
    }
    LC32RetireGuestMirrorWithTransferredReference(self);
}

static void LC32GuestMirrorRelease(id self, SEL) {
    LC32HostInitializerInvocationScope::observeNativeRelease(self);
    LC32GuestMirrorReleaseImplementation(self);
}

static void LC32InstallGuestMirrorReferenceCounting(Class cls) {
    if(!cls) return;
    Class superclass = class_getSuperclass(cls);
    if(!class_getInstanceVariable(
            superclass, LC32GuestMirrorRetiringIvarName)) {
        const bool added = class_addIvar(
            cls, LC32GuestMirrorRetiringIvarName, sizeof(u8), 0, "C");
        assert(added);
    }
    assert(class_getInstanceVariable(
        cls, LC32GuestMirrorRetiringIvarName));

    Method retainMethod = class_getInstanceMethod(
        NSObject.class, @selector(retain));
    class_replaceMethod(cls, @selector(retain),
        (IMP)&LC32GuestMirrorRetain,
        retainMethod ? method_getTypeEncoding(retainMethod) : "@@:");

    Method releaseMethod = class_getInstanceMethod(
        NSObject.class, @selector(release));
    const char *types = releaseMethod
        ? method_getTypeEncoding(releaseMethod) : "v@:";
    class_replaceMethod(cls, @selector(release),
                        (IMP)&LC32GuestMirrorRelease, types);

    const SEL retainWeakSelector =
        sel_registerName("retainWeakReference");
    Method retainWeakMethod = class_getInstanceMethod(
        NSObject.class, retainWeakSelector);
    class_replaceMethod(cls, retainWeakSelector,
        (IMP)&LC32GuestMirrorRetainWeakReference,
        retainWeakMethod ? method_getTypeEncoding(retainWeakMethod) : "c@:");

    const SEL allowsWeakSelector =
        sel_registerName("allowsWeakReference");
    Method allowsWeakMethod = class_getInstanceMethod(
        NSObject.class, allowsWeakSelector);
    class_replaceMethod(cls, allowsWeakSelector,
        (IMP)&LC32GuestMirrorAllowsWeakReference,
        allowsWeakMethod ? method_getTypeEncoding(allowsWeakMethod) : "c@:");
}

objc_hook_getClass host_getClass;
BOOL host_hook_getClass(const char *name, Class *outClass) {
    if(host_getClass && host_getClass(name, outClass)) {
        return true;
    }

    /*
     * NSZombie implements diagnostics by looking up private replacement
     * classes named _NSZombie_<original class>.  Those names are host runtime
     * bookkeeping, not guest classes.  Synthesizing an ARM32-backed class for
     * them corrupts the zombie object before it can report the original
     * over-release (and can crash during early framework initialization).
     */
    if(name && strncmp(name, "_NSZombie_", 10) == 0) {
        return false;
    }

    LC32_DEBUG_PRINTF("host_hook_getClass: %s\n", name);
    *outClass = guest_objc_getClass_retHostClass(name);
    return *outClass != nil;
}

@implementation NSObject(LC32)
- (void)setGuestClass:(BOOL)value {
    return objc_setAssociatedObject(self, kGuestClass, @(value), OBJC_ASSOCIATION_RETAIN_NONATOMIC);
}

- (BOOL)isGuestClass {
    return ((NSNumber *)objc_getAssociatedObject(self, kGuestClass)).boolValue;
}

// Set the equivalent guest object pointer.
// Called from guest_self if the object has not been known by guest before (eg passing UIApplication object to guest)
// Called from guest's setHost_self if the object is created by guest code (eg creating AppDelegate, UIWindow, etc)
- (void)setGuest_self:(u32)ptr {
    //assert(!self.guest_selfOrNull);
    @synchronized(self) {
        objc_setAssociatedObject(self, kGuestSelf, @(ptr),
            OBJC_ASSOCIATION_RETAIN_NONATOMIC);
    }
    LC32OperationTraceLiveObject("reverse-map-set", self, ptr);
}

- (u32)guest_selfOrNull {
    return ((NSNumber *)objc_getAssociatedObject(self, kGuestSelf)).unsignedLongValue;
}

- (u32)LC32_bindGuestSelfIfAbsent:(u32)ptr {
    u32 boundGuestObject;
    @synchronized(self) {
        const u32 existing = self.guest_selfOrNull;
        if(existing) {
            boundGuestObject = existing;
        } else {
            objc_setAssociatedObject(self, kGuestSelf, @(ptr),
                OBJC_ASSOCIATION_RETAIN_NONATOMIC);
            boundGuestObject = ptr;
        }
    }
    /*
     * An initializer may replace its +alloc placeholder (class clusters do
     * this routinely). Move the lifetime guarantee to the returned native
     * object as soon as it acquires the reverse mapping. The placeholder's
     * pin is released independently when that object is destroyed.
     */
    if(boundGuestObject == ptr) {
        /*
         * This is also needed when +alloc and -init return the same object:
         * +alloc installed its reverse mapping but deliberately did not pin
         * native class-cluster placeholders, which may be shared.
         */
        LC32PinGuestObjectToHost(self, ptr, true);
    }
    return boundGuestObject;
}

- (void)LC32_clearGuestSelfIfEqual:(u64)expectedGuestSelf {
    LC32ClearGuestSelfIfEqual(self, (u32)expectedGuestSelf);
}

- (u32)guest_self {
    u32 ptr = self.guest_selfOrNull;
    if(ptr) {
        LC32OperationTraceLiveObject("guest-self-existing", self, ptr);
        return ptr;
    }
    if(LC32GuestMirrorIsRetiring(self)) return 0;

    @synchronized(self) {
    ptr = self.guest_selfOrNull;
    if(ptr) {
        LC32OperationTraceLiveObject(
            "guest-self-existing-locked", self, ptr);
        return ptr;
    }
    if(LC32GuestMirrorIsRetiring(self)) return 0;

    Class hostClass = self.class;
    const char *className = class_getName(hostClass);
    Class matchedHostClass = hostClass;
    if(LC32HostObjectIsDispatchData(self)) {
        /*
         * OS_dispatch_data stores its state in a private allocation made by
         * libdispatch's +allocWithZone:.  A normal bridge proxy created with
         * class_createInstance has none of that state, so guest -length and
         * -bytes read beyond the object.  Treat native dispatch data as an
         * immutable NSData peer; LC32CopyHostDataBytes flattens it natively.
         */
        matchedHostClass = [NSData class];
        ptr = guest_objc_getClass("NSData");
    } else {
        while(matchedHostClass != Nil) {
            ptr = guest_objc_getClass(class_getName(matchedHostClass));
            if(ptr) break;
            matchedHostClass = class_getSuperclass(matchedHostClass);
        }
    }
    if(!ptr) {
        printf("LC32: Error: Host required missing guest class %s\n", className);
        return 0;
    }
    if(matchedHostClass != hostClass) {
        LC32_DEBUG_PRINTF("LC32: mapping host class %s through guest superclass %s\n",
            className, class_getName(matchedHostClass));
    }
    if(object_isClass(self)) return self.guest_self = ptr;

    static std::atomic<u32> guestSetHostSelfCache{0};
    const u32 guest_setHost_self = LC32CachedGuestSelector(
        guestSetHostSelfCache, "initWithHostSelf:");
    ptr = guest_class_createInstance(ptr, 0);

    //guest_objc_performSelector(ptr, guest_setHost_self, (u32)(u64)self, (u32)((u64)self >> 32));
    {
        u32 args[] = {ptr, guest_setHost_self, (u32)(u64)self, (u32)((u64)self >> 32)};
        ptr = guest_objc_msgSend(sizeof(args)/sizeof(*args), args);
    }

    if(!ptr) return 0;

    self.guest_self = ptr;
    /*
     * Host-to-guest conversion is a borrowed (+0) operation.  Consume the
     * class_createInstance +1 as a lifetime pin owned by the native object,
     * so a callback argument or autoreleased method result cannot leave a
     * guest proxy containing a dangling host pointer.  Guest retain/release
     * calls made after conversion still create and remove paired ownership on
     * both sides through the LC32 NSObject swizzles.
     *
     * Dynamic guest classes already used this pin.  Ordinary native classes
     * need it too: NSBlockOperation convenience results exposed the missing
     * half when their host autorelease pool drained before later guest use.
     */
    LC32PinGuestObjectToHost(self, ptr, false);
    LC32OperationTraceLiveObject("guest-self-created", self, ptr);
    return ptr;
    }
}
@end

extern "C" u32 LC32GuestObjectForOwnedHostObject(CFTypeRef object) {
    if(!object) return 0;

    id hostObject = (id)object;
    u32 guestObject = 0;
    @synchronized(hostObject) {
        /*
         * -guest_self is always a borrowed conversion whose initial guest +1
         * belongs to the native lifetime pin.  A Create/Copy result carries a
         * separate native +1, so add the corresponding guest-only +1 whether
         * this call created or reused the proxy.  The public guest release
         * will later decrement both sides exactly once.
         *
         * Keep conversion and ownership transfer under the same object lock:
         * another native guest thread may otherwise change the reverse
         * mapping between those operations.
         */
        guestObject = [hostObject guest_self];
        if(guestObject) {
            LC32AdjustGuestReferenceNow(guestObject, true);
            LC32OperationTraceLiveObject(
                "owned-result-add-guest-reference",
                hostObject, guestObject);
        }
    }

    if(!guestObject) CFRelease(object);
    return guestObject;
}

static u32 LC32GuestObjectForBorrowedHostResult(id hostObject) {
    if(!hostObject) return 0;

    /*
     * The native method already supplies the autorelease lifetime required by
     * a +0 return.  The reverse-mapping lifetime pin owns the proxy's initial
     * guest reference until that native object dies.  Adding another paired
     * autorelease here duplicates the native +0 lifetime and can leave the
     * method's original autorelease-pool entry pointing at a freed object.
     */
    const u32 guestObject = [hostObject guest_self];
    LC32OperationTraceLiveObject(
        "borrowed-result", hostObject, guestObject);
    return guestObject;
}

extern "C" u32 LC32GuestObjectForOwnedHostObjectAddress(u64 object) {
    return LC32GuestObjectForOwnedHostObject(
        reinterpret_cast<CFTypeRef>(static_cast<uintptr_t>(object)));
}

static const char *LC32UnqualifiedType(const char *type) {
    while(type && *type && strchr("rnNoORVA", *type)) type++;
    return type;
}

static char LC32VoidSingleFloatingArgumentType(const char *types) {
    if(!types) return '\0';
    auto skipOffset = [&]() {
        while(*types >= '0' && *types <= '9') types++;
    };
    auto consume = [&](char expected) {
        types = LC32UnqualifiedType(types);
        if(*types != expected) return false;
        types++;
        skipOffset();
        return true;
    };
    // Match only void(self, _cmd, float/double), not a floating field inside
    // an aggregate or a mixed signature needing additional register capture.
    if(!consume('v') || !consume('@') || !consume(':')) return '\0';
    types = LC32UnqualifiedType(types);
    const char argumentType = *types;
    if(argumentType != 'f' && argumentType != 'd') return '\0';
    types++;
    skipOffset();
    return *types == '\0' ? argumentType : '\0';
}

static const char *LC32ProtocolMethodTypes(Protocol *protocol, SEL selector,
                                           BOOL instanceMethod,
                                           unsigned int depth) {
    if(!protocol || depth > 16) return nullptr;

    for(BOOL required : {YES, NO}) {
        const struct objc_method_description description =
            protocol_getMethodDescription(protocol, selector, required,
                                          instanceMethod);
        if(description.name && description.types) return description.types;
    }

    unsigned int adoptedCount = 0;
    Protocol *__unsafe_unretained *adoptedProtocols =
        protocol_copyProtocolList(protocol, &adoptedCount);
    for(unsigned int index = 0; index < adoptedCount; index++) {
        const char *types = LC32ProtocolMethodTypes(
            adoptedProtocols[index], selector, instanceMethod, depth + 1);
        if(types) {
            free(adoptedProtocols);
            return types;
        }
    }
    free(adoptedProtocols);
    return nullptr;
}

static const char *LC32ClassProtocolMethodTypes(Class cls, SEL selector,
                                                BOOL instanceMethod) {
    if(!cls) return nullptr;

    unsigned int protocolCount = 0;
    Protocol *__unsafe_unretained *protocols =
        class_copyProtocolList(cls, &protocolCount);
    for(unsigned int index = 0; index < protocolCount; index++) {
        const char *types = LC32ProtocolMethodTypes(
            protocols[index], selector, instanceMethod, 0);
        if(types) {
            free(protocols);
            return types;
        }
    }
    free(protocols);
    return nullptr;
}

static const char *LC32ClassHierarchyProtocolMethodTypes(
        Class cls, SEL selector, BOOL instanceMethod) {
    for(Class current = cls; current;
            current = class_getSuperclass(current)) {
        const char *types = LC32ClassProtocolMethodTypes(
            current, selector, instanceMethod);
        if(types) return types;
    }
    return nullptr;
}

// Protocol adoption belongs to the ordinary class, but class methods are
// installed on its metaclass. Keep the owner available while a newly
// allocated class is still unregistered and cannot yet be found by name.
static const void *LC32ProtocolOwnerClassKey =
    &LC32ProtocolOwnerClassKey;

static Class LC32ProtocolOwnerClass(Class cls) {
    if(!class_isMetaClass(cls)) return cls;

    Class owner = (Class)objc_getAssociatedObject(
        (id)cls, LC32ProtocolOwnerClassKey);
    if(owner) return owner;
    return objc_lookUpClass(class_getName(cls));
}

/*
 * A guest CGFloat is encoded as `f`, while the same public method is `d` in
 * the ARM64 UIKit ABI.  Prefer the native declaration inherited by the mirror
 * class, then a native protocol declaration adopted anywhere in its class
 * hierarchy.
 */
static const char *LC32ExpectedHostMethodTypes(Class cls, SEL selector) {
    Class superclass = class_getSuperclass(cls);
    Method inheritedMethod = superclass
        ? class_getInstanceMethod(superclass, selector)
        : nullptr;
    if(inheritedMethod) return method_getTypeEncoding(inheritedMethod);

    const BOOL instanceMethod = !class_isMetaClass(cls);
    return LC32ClassHierarchyProtocolMethodTypes(
        LC32ProtocolOwnerClass(cls), selector, instanceMethod);
}

@implementation LC32ObjCMethodResolver
+ (void)addMethod:(Method)method toClass:(Class)cls {
    class_addMethod(cls, method_getName(method), method_getImplementation(method), method_getTypeEncoding(method));
}

+ (void)addGuestIvar:(u32)guest_ivar toClass:(Class)cls {
    DynarmicHostString name(guest_ivar_getName(guest_ivar));
    DynarmicHostString typeEncoding(guest_ivar_getTypeEncoding(guest_ivar));
    const char *guestType = typeEncoding.hostPtr;
    while(*guestType && strchr("rnNoORVA", *guestType)) guestType++;
    const u32 guestOffsetPointer =
        Dynarmic_current_user_callbacks()->MemoryRead32(guest_ivar);
    const u32 guestOffset = guestOffsetPointer
        ? Dynarmic_current_user_callbacks()->MemoryRead32(guestOffsetPointer)
        : 0;

    // According to https://github.com/Quotation/LongestCocoa#longest-objective-c-property-names, the longest public property has 56 characters
    // still, we need to add an assert
    char setterName[0x50];
    char literalIvarSetterName[0x50] = {};
    assert(strlen(name.hostPtr) + 4 < sizeof(setterName));
    if(name.hostPtr[0] == '_') {
        snprintf(setterName, sizeof(setterName)-1, "_set%c%s:", toupper(name.hostPtr[1]), &name.hostPtr[2]);
        // Some older nibs archive the literal ivar name as their KVC key.
        // KVC asks for set_btnGameMode: when the key is _btnGameMode, while
        // _setBtnGameMode: above is the accessor for the logical key
        // btnGameMode. Register both spellings against the same guest ivar.
        snprintf(literalIvarSetterName, sizeof(literalIvarSetterName)-1,
                 "set%s:", name.hostPtr);
    } else {
        snprintf(setterName, sizeof(setterName)-1, "set%c%s:", toupper(name.hostPtr[0]), &name.hostPtr[1]);
    }

    char setterTypeEncoding[10];
    snprintf(setterTypeEncoding, sizeof(setterTypeEncoding)-1,
             "v@:%c", *guestType);

    IMP setterImplementation = nullptr;
    switch(*guestType) {
        case '@':
        case '#':
            setterImplementation = (IMP)&LC32SetGuestNSObjectIvar;
            break;
        case 'B':
        case 'C':
        case 'I':
        case 'L':
        case 'Q':
        case 'S':
        case 'c':
        case 'i':
        case 'l':
        case 'q':
        case 's':
            setterImplementation = (IMP)&LC32SetGuestScalarIvar;
            break;
        default:
            printf("LC32: skipping ivar %s with unhandled type %s\n", name.hostPtr, typeEncoding.hostPtr);
            break;
    }
    if(setterImplementation) {
        SEL setterSelector = sel_registerName(setterName);
        if(class_addMethod(cls, setterSelector, setterImplementation,
                           setterTypeEncoding)) {
            LC32RegisterGuestIvarAccessor(
                cls, setterSelector, name.hostPtr, guestOffset, *guestType);
        }
        if(literalIvarSetterName[0]) {
            SEL literalSetterSelector =
                sel_registerName(literalIvarSetterName);
            if(class_addMethod(cls, literalSetterSelector,
                               setterImplementation, setterTypeEncoding)) {
                LC32RegisterGuestIvarAccessor(
                    cls, literalSetterSelector, name.hostPtr,
                    guestOffset, *guestType);
            }
        }
    }

    /*
     * Mirror the setters with KVC-compliant getters.  Without them the host
     * proxy has no accessor for a guest ivar, so KVC falls into
     * valueForUndefinedKey: and raises NSUnknownKeyException.  Register both
     * the raw underscore spelling (the literal ivar name) and the
     * property-cased spelling for underscored ivars, matching how KVC probes
     * both when the key itself is underscored.
     */
    char getterName[0x50];
    char propertyGetterName[0x50] = {};
    if(name.hostPtr[0] == '_') {
        snprintf(getterName, sizeof(getterName)-1, "%s", name.hostPtr);
        snprintf(propertyGetterName, sizeof(propertyGetterName)-1,
                 "%c%s", tolower(name.hostPtr[1]), &name.hostPtr[2]);
    } else {
        snprintf(getterName, sizeof(getterName)-1, "%s", name.hostPtr);
    }

    char getterTypeEncoding[10];
    snprintf(getterTypeEncoding, sizeof(getterTypeEncoding)-1,
             "%c@:", *guestType);

    IMP getterImplementation = nullptr;
    switch(*guestType) {
        case '@':
        case '#':
            getterImplementation = (IMP)&LC32GetGuestNSObjectIvar;
            break;
        case 'B':
        case 'C':
        case 'I':
        case 'L':
        case 'Q':
        case 'S':
        case 'c':
        case 'i':
        case 'l':
        case 'q':
        case 's':
            getterImplementation = (IMP)&LC32GetGuestScalarIvar;
            break;
        default:
            break;
    }
    if(getterImplementation) {
        SEL rawGetterSelector = sel_registerName(getterName);
        if(class_addMethod(cls, rawGetterSelector, getterImplementation,
                           getterTypeEncoding)) {
            LC32RegisterGuestIvarAccessor(
                cls, rawGetterSelector, name.hostPtr,
                guestOffset, *guestType);
        }
        if(propertyGetterName[0]) {
            SEL propertyGetterSelector =
                sel_registerName(propertyGetterName);
            if(class_addMethod(cls, propertyGetterSelector,
                               getterImplementation, getterTypeEncoding)) {
                LC32RegisterGuestIvarAccessor(
                    cls, propertyGetterSelector, name.hostPtr,
                    guestOffset, *guestType);
            }
        }
    }

    // We currently don't bind setter, just leaving here for future references
    // for getter booleans, we have to register total 3 variants: name, hasName and isName, since we don't want to run a LLM here to predict which is best ¯\_(ツ)_/¯
}

+ (void)addGuestMethod:(u32)guest_method selector:(SEL)sel toClass:(Class)cls {
    objc_method_32 host_method_32;
    Dynarmic_mem_1read(guest_method, sizeof(host_method_32), (char *)&host_method_32);
    DynarmicHostString host_method_types(host_method_32.method_types);
    if(!sel) {
        DynarmicHostString host_sel(host_method_32.method_name);
        sel = sel_registerName(host_sel.hostPtr);
    }
    LC32RegisterGuestSelectorMapping(
        sel, host_method_32.method_name);

    // The Objective-C runtime calls these lifecycle hooks as id (*)(id) and
    // void (*)(id), without a selector argument. Installing the generic
    // (id, SEL, ...) trampoline therefore interprets garbage in x1 as _cmd.
    // Guest C++ ivars belong to the guest object and are already managed by
    // the guest runtime, so forwarding would also construct/destruct twice.
    const char *selectorName = sel_getName(sel);
    if(!strcmp(selectorName, ".cxx_construct") ||
            !strcmp(selectorName, ".cxx_destruct") ||
            !strcmp(selectorName, "dealloc")) {
        return;
    }

    const char *guestMethodTypes = host_method_types.hostPtr;
    const char *installedMethodTypes = guestMethodTypes;
    const char guestReturnType = *LC32UnqualifiedType(guestMethodTypes);
    char hostReturnType = guestReturnType;
    IMP floatingImplementation = nullptr;
    if(guestReturnType == 'f' || guestReturnType == 'd') {
        const char *expectedHostTypes =
            LC32ExpectedHostMethodTypes(cls, sel);
        if(expectedHostTypes) {
            const char expectedReturnType =
                *LC32UnqualifiedType(expectedHostTypes);
            if(expectedReturnType == 'f' || expectedReturnType == 'd') {
                installedMethodTypes = expectedHostTypes;
                hostReturnType = expectedReturnType;
            }
        }

        if(guestReturnType == 'f') {
            floatingImplementation = hostReturnType == 'd'
                ? (IMP)&LC32InvokeGuestSelectorGuestFloatHostDouble
                : (IMP)&LC32InvokeGuestSelectorGuestFloatHostFloat;
        } else {
            floatingImplementation = hostReturnType == 'f'
                ? (IMP)&LC32InvokeGuestSelectorGuestDoubleHostFloat
                : (IMP)&LC32InvokeGuestSelectorGuestDoubleHostDouble;
        }
        if(hostReturnType != guestReturnType) {
            fprintf(stderr,
                "LC32: floating return ABI for %s: guest %c -> host %c\n",
                selectorName, guestReturnType, hostReturnType);
        }
    }

    IMP implementation = floatingImplementation
        ? floatingImplementation
        : (IMP)&LC32InvokeGuestSelector;
    const char *expectedHostTypes =
        LC32ExpectedHostMethodTypes(cls, sel);
    const char guestFloatingArgument =
        LC32VoidSingleFloatingArgumentType(guestMethodTypes);
    if(guestFloatingArgument) {
        const char expectedFloatingArgument =
            LC32VoidSingleFloatingArgumentType(expectedHostTypes);
        const char hostFloatingArgument = expectedFloatingArgument
            ? expectedFloatingArgument : guestFloatingArgument;
        if(guestFloatingArgument == 'f') {
            implementation = hostFloatingArgument == 'd'
                ? (IMP)&LC32InvokeGuestSelectorFloatingArgument<float, double>
                : (IMP)&LC32InvokeGuestSelectorFloatingArgument<float, float>;
        } else {
            implementation = hostFloatingArgument == 'f'
                ? (IMP)&LC32InvokeGuestSelectorFloatingArgument<double, float>
                : (IMP)&LC32InvokeGuestSelectorFloatingArgument<double, double>;
        }
        // Do not publish ARM32 argument offsets to native NSInvocation/KVC.
        installedMethodTypes = expectedFloatingArgument ? expectedHostTypes :
            (hostFloatingArgument == 'f' ? "v@:f" : "v@:d");
    }
    if(LC32CGSizeToCGSizeSignatureMatches(guestMethodTypes, 'f') &&
       LC32CGSizeToCGSizeSignatureMatches(expectedHostTypes, 'd')) {
        implementation =
            (IMP)&LC32InvokeGuestSelectorCGSizeToCGSize;
        installedMethodTypes = expectedHostTypes;
    }
    if(LC32PointObjectSignatureMatches(guestMethodTypes, 'f') &&
       LC32PointObjectSignatureMatches(expectedHostTypes, 'd')) {
        implementation =
            (IMP)&LC32InvokeGuestSelectorPointObject;
        installedMethodTypes = expectedHostTypes;
    }
    if(!strcmp(selectorName, "getCharacters:range:") &&
       LC32UniCharRangeSignatureMatches(guestMethodTypes, 'I')) {
        if(LC32UniCharRangeSignatureMatches(expectedHostTypes, 'Q')) {
            implementation =
                (IMP)&LC32InvokeGuestSelectorUniCharRange;
            installedMethodTypes = expectedHostTypes;
        }
    }
    if(!strcmp(selectorName, "read:maxLength:") &&
       LC32InputStreamReadSignatureMatches(guestMethodTypes, false)) {
        const char *readHostTypes = expectedHostTypes;
        if(!readHostTypes) {
            // Legacy stream adapters may duck-type NSInputStream directly
            // from NSObject. The exact selector and verified ARM32 signature
            // still identify a binary read primitive; publish the canonical
            // native declaration instead of the guest's 32-bit offsets.
            Method nativeRead = class_getInstanceMethod(
                objc_getClass("NSInputStream"), sel);
            readHostTypes = nativeRead ? method_getTypeEncoding(nativeRead) :
                "q@:^CQ";
        }
        if(LC32InputStreamReadSignatureMatches(readHostTypes, true)) {
            implementation = (IMP)&LC32InvokeGuestSelectorInputStreamRead;
            installedMethodTypes = readHostTypes;
        }
    }
    if(!strcmp(selectorName, "getObjects:range:") &&
       LC32ObjectRangeSignatureMatches(guestMethodTypes, 'I') &&
       LC32ObjectRangeSignatureMatches(expectedHostTypes, 'Q')) {
        implementation =
            (IMP)&LC32InvokeGuestSelectorObjectRange;
        installedMethodTypes = expectedHostTypes;
    }
    /*
     * KVO's opaque context has the same logical register ABI on both sides,
     * and LC32HostToGuestArgument safely round-trips the zero-extended ARM32
     * token.  Install the native declaration so Foundation reflection and
     * forwarding see ARM64-sized argument offsets instead of guest metadata.
     */
    if(!strcmp(selectorName,
               "observeValueForKeyPath:ofObject:change:context:")) {
        if(expectedHostTypes) installedMethodTypes = expectedHostTypes;
    }
    if(!strcmp(selectorName,
               "countByEnumeratingWithState:objects:count:") &&
            LC32FastEnumerationSignatureMatches(guestMethodTypes) &&
            LC32FastEnumerationSignatureMatches(expectedHostTypes)) {
        implementation =
            (IMP)&LC32InvokeGuestSelectorFastEnumeration;
        installedMethodTypes = expectedHostTypes;
    }
    if(strstr(installedMethodTypes, "{CGRect=")) {
        NSMethodSignature *signature =
            [NSMethodSignature signatureWithObjCTypes:installedMethodTypes];
        if(signature.numberOfArguments == 3) {
            const char *argumentType = [signature getArgumentTypeAtIndex:2];
            while(*argumentType && strchr("rnNoORVA", *argumentType)) {
                argumentType++;
            }
            if(!strncmp(argumentType, "{CGRect=", sizeof("{CGRect=") - 1)) {
                if(floatingImplementation) {
                    if(guestReturnType == 'f') {
                        implementation = hostReturnType == 'd'
                            ? (IMP)&LC32InvokeGuestSelectorCGRectGuestFloatHostDouble
                            : (IMP)&LC32InvokeGuestSelectorCGRectGuestFloatHostFloat;
                    } else {
                        implementation = hostReturnType == 'f'
                            ? (IMP)&LC32InvokeGuestSelectorCGRectGuestDoubleHostFloat
                            : (IMP)&LC32InvokeGuestSelectorCGRectGuestDoubleHostDouble;
                    }
                } else {
                    implementation = (IMP)&LC32InvokeGuestSelectorCGRect;
                }
            }
        }
    }
    if(LC32CGRectToCGRectSignatureMatches(guestMethodTypes, 'f') &&
       LC32CGRectToCGRectSignatureMatches(expectedHostTypes, 'd')) {
        implementation =
            (IMP)&LC32InvokeGuestSelectorCGRectToCGRect;
        installedMethodTypes = expectedHostTypes;
    }
    class_addMethod(cls, sel, implementation, installedMethodTypes);
}


+ (void)addGuestProtocol:(u32)guest_protocol toClass:(Class)cls {
    DynarmicHostString host_protocolName(guest_protocol_getName(guest_protocol));
    Protocol *protocol = objc_getProtocol(host_protocolName.hostPtr);
    if(protocol) {
        class_addProtocol(cls, protocol);
    } else {
        printf("LC32: skipping nonexistent protocol %s\n", host_protocolName.hostPtr);
    }
}

+ (void)registerClass:(Class)clsObject {
    u32 count;
    u32 list;

    Class cls = object_getClass(clsObject);
    objc_setAssociatedObject((id)cls, LC32ProtocolOwnerClassKey,
                             (id)clsObject, OBJC_ASSOCIATION_ASSIGN);
    [self addMethod:class_getClassMethod(self, @selector(resolveClassMethod:)) toClass:cls];
    [self addMethod:class_getClassMethod(self, @selector(resolveInstanceMethod:)) toClass:cls];
    [cls resolveInstanceMethod:@selector(init)];
    [cls setGuestClass:YES];

    // FIXME: can't call free on copied lists
    // Register protocols
    list = guest_class_copyProtocolList([clsObject guest_self], &count);
    for(int i = 0; i < count; i++, list += sizeof(u32)) {
        [self addGuestProtocol:Dynarmic_current_user_callbacks()->MemoryRead32(list) toClass:clsObject];
    }
    //if(list) guest_free(list);

    // Register class methods. Pass metaclass (cls) here!
    list = guest_class_copyMethodList([cls guest_self], &count);
    for(int i = 0; i < count; i++, list += sizeof(u32)) {
        [self addGuestMethod:Dynarmic_current_user_callbacks()->MemoryRead32(list) selector:nil toClass:cls];
    }
    //if(list) guest_free(list);

    // Register instance methods
    list = guest_class_copyMethodList([clsObject guest_self], &count);
    for(int i = 0; i < count; i++, list += sizeof(u32)) {
        [self addGuestMethod:Dynarmic_current_user_callbacks()->MemoryRead32(list) selector:nil toClass:clsObject];
    }
    //if(list) guest_free(list);

    // Add synthetic ivar setters only after real guest methods. They are a
    // fallback for nib/KVC assignment when the binary has no setter; adding
    // them first would shadow an app's retaining property implementation.
    list = guest_class_copyIvarList([clsObject guest_self], &count);
    for(int i = 0; i < count; i++, list += sizeof(u32)) {
        [self addGuestIvar:Dynarmic_current_user_callbacks()->MemoryRead32(list) toClass:clsObject];
    }
    //if(list) guest_free(list);
}

// FIXME: currently using class_get*Method which may return superclass's method, but I guess this shouldn't affect anything
+ (BOOL)resolveClassMethod:(SEL)sel {
    LC32_DEBUG_PRINTF("resolveClassMethod %s\n", sel_getName(sel));
    /*
     * Native frameworks may ask about an optional selector from one of their
     * own queues.  Such a thread has no ARM32 JIT or guest stack.  All methods
     * present when the mirrored class was registered were already installed
     * above, so safely decline this dynamic fallback instead of borrowing a
     * different guest thread's execution context.
     */
    if(!Dynarmic_guest_thread_is_registered()) {
        return [super resolveClassMethod:sel];
    }
    u32 guest_sel = guest_sel_registerName(sel_getName(sel));
    u32 guest_method = guest_class_getClassMethod(self.guest_self, guest_sel);
    if(guest_method) {
        [LC32ObjCMethodResolver addGuestMethod:guest_method selector:sel toClass:self.class];
    }
    return [super resolveClassMethod:sel];
}

+ (BOOL)resolveInstanceMethod:(SEL)sel {
    LC32_DEBUG_PRINTF("resolveInstanceMethod %s\n", sel_getName(sel));
    if(!Dynarmic_guest_thread_is_registered()) {
        return [super resolveInstanceMethod:sel];
    }
    u32 guest_sel = guest_sel_registerName(sel_getName(sel));
    u32 guest_method = guest_class_getInstanceMethod(self.guest_self, guest_sel);
    if(guest_method) {
        [LC32ObjCMethodResolver addGuestMethod:guest_method selector:sel toClass:self];
    }
    return [super resolveInstanceMethod:sel];
}
@end

__attribute__((constructor)) void LC32InstallGetClassHook() {
    objc_setHook_getClass(host_hook_getClass, &host_getClass);
}
